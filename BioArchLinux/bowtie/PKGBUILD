# Maintainer: Athemis <alexander.minges[at]googlemail[dot]com>
# Contributor: sauliusl
pkgname=bowtie
pkgver=1.3.1
pkgrel=5
pkgdesc="An alignment tool for short nucleotide sequences against long templates \
	 https://doi.org/10.1186/gb-2009-10-3-r25"
url="http://bowtie-bio.sf.net"
arch=('x86_64' 'aarch64')
license=('Artistic-2.0')
depends=('gcc-libs' 'bash' 'perl' 'glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/BenLangmead/bowtie/archive/v${pkgver}.tar.gz")
sha256sums=('147d9fe9652f7c5f351bfc0eb012e06981986fb43bd6bdfe88a95c02eabc6573')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix=/usr DESTDIR="${pkgdir}" install

  install -Dm755 -t "${pkgdir}/usr/share/${pkgname}/scripts/" scripts/*.{pl,sh}
  install -Dm644 -t "${pkgdir}/usr/share/doc/$pkgname/" MANUAL{,.markdown} TUTORIAL AUTHORS NEWS
}
