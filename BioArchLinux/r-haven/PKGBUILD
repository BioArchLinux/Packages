# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Matt Frichtl <frichtlm@gmail.com>
# Contributor: Taekyung Kim <Taekyung.Kim.Maths@gmail.com>
# Contributor: Alex Branham <branham@utexas.edu>

_pkgname=haven
_pkgver=2.5.4
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Import and Export 'SPSS', 'Stata' and 'SAS' Files"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-cli
  r-forcats
  r-hms
  r-lifecycle
  r-readr
  r-rlang
  r-tibble
  r-tidyselect
  r-vctrs
  readstat
)
makedepends=(
  r-cpp11
)
checkdepends=(
  r-testthat
)
optdepends=(
  r-covr
  r-crayon
  r-fs
  r-knitr
  r-pillar
  r-rmarkdown
  r-testthat
  r-utf8
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz"
        "system-readstat.patch")
md5sums=('ef0337da48b61ae98b03413e02177822'
         '73da7cc2ff7f7c1a5d4ffbaa45e359df')
b2sums=('d78328297778232f316972def5ddaf6e25cac721c84b3e9d4b30167a0484ad4c71a8d4f2798d2869b4350fd222aeeeb9a508092c85f860264d4bec20228147db'
        'a0bb3a653cc0140c9f345b1b4918c483348f03c0e0a903fb963b39592c55bf5a3c5d3d59dd808fbe9a997fa3682eec5153a67bf90f207032a4491eee3eaf9bfe')

prepare() {
  # build with system readstat
  patch -Np1 -i system-readstat.patch
}

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
