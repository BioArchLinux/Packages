# Maintainer: Michael Schubert <mschu.dev at gmail> github.com/mschubert/PKGBUILDs
# Contributor: Mick Elliot <micke at sfu dot ca>
pkgname=phyml-mpi
pkgver=3.3.20220408
pkgrel=4
pkgdesc="Builds phylogenies from DNA or protein sequences using a maximum likelihood approach. https://doi.org/10.1093/sysbio/syq010"
arch=('i686' 'x86_64')
url="https://github.com/stephaneguindon/phyml"
license=('GPL2')
conflicts=('phyml')
depends=('openmpi' 'gcc-libs')
makedepends=('make' 'autoconf')
source=("phyml-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('9cef375c0186e8a8d4db72ac02148707624c97174ffac244167a138ceb9f65bd')

build() {
  cd "$srcdir/phyml-$pkgver"
  autoreconf --force --install -v
  ./configure --prefix=/usr --enable-phyml-mpi
  make
}

package() {
  cd "$srcdir/phyml-$pkgver"
  make DESTDIR="$pkgdir" install
}
