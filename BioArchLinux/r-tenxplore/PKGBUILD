# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=tenXplore
_pkgver=1.20.0
pkgname=r-${_pkgname,,}
pkgver=1.20.0
pkgrel=1
pkgdesc='ontological exploration of scRNA-seq of 1.3 million mouse neurons from 10x genomics'
arch=('any')
url="https://bioconductor.org/packages/${_pkgname}"
license=('Artistic2.0')
depends=(
  r
  r-annotationdbi
  r-matrixstats
  r-ontoproc
  r-org.mm.eg.db
  r-restfulse
  r-shiny
  r-summarizedexperiment
)
optdepends=(
  r-knitr
  r-org.hs.eg.db
  r-rmarkdown
  r-testthat
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('9d8edb7e1933030968366e2f7e26e81269c2d43f4a5936d28207b23e30192436')

prepare(){
  # Do not import RESTfulSummarizedExperiment from RESTfulSE
  sed -i '/RESTfulSummarizedExperiment/d' ${_pkgname}/R/${_pkgname}.R ${_pkgname}/NAMESPACE
  tar -zcvf ${_pkgname}_${_pkgver}_1.tar.gz  ${_pkgname}

}

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}_1.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
}
# vim:set ts=2 sw=2 et:
