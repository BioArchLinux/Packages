# Maintainer: sukanka <su975853527@gmail.com>

_pkgname=BAS
_pkgver=1.7.3
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Bayesian Variable Selection and Model Averaging using Bayesian Adaptive Sampling"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-3.0-or-later')
depends=(
  blas
  lapack
  r
)
makedepends=(
  gcc-fortran
)
optdepends=(
  r-covr
  r-dplyr
  r-faraway
  r-ggally
  r-ggplot2
  r-glmbb
  r-knitr
  r-rmarkdown
  r-roxygen2
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('441a043126ea8a8a90177d14e9c33a1d')
b2sums=('c531092f170258e2489009a66069f980c4e2b07778b92f4676e5dcc07c3b6e90feb336ebb6488e684da058ed3d50795b4aaf1dddf8a89d10d438dc5ff4be0f3a')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
