# Maintainer: sukanka <su975853527@gmail.com>

_pkgname=BAS
_pkgver=1.7.2
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Bayesian Variable Selection and Model Averaging using Bayesian Adaptive Sampling"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-3.0-or-later')
depends=(
  blas
  lapack
  r
)
optdepends=(
  r-covr
  r-dplyr
  r-faraway
  r-ggally
  r-ggplot2
  r-glmbb
  r-knitr
  r-rmarkdown
  r-roxygen2
  r-testthat
)
makedepends=(gcc-fortran)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('de9e60a4534536ed6a1fda7b838f2938')
b2sums=('d20dbc4943c3af26de8bd7a0e4dd8262493b4207ad46b675797fb477fe3a064447942d536fb460f6d39609d038bb9d2327be0c63507f0245103d6676ba8752fd')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
