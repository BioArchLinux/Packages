# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=jpeg
_pkgver=0.1-11
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Read and write JPEG images"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-2.0-only OR GPL-3.0-only')
depends=(
  libjpeg-turbo
  r
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('415c72850990d7889408dcfd29805613')
b2sums=('ffa9cad7da57db429fe0030a52cdd69162e30e8da2d18fc269c49773b39cfa4473bb4ea3039f3f07b2beeb112fa9061f41d52e4656eda464af366d3e11b0ccd9')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
