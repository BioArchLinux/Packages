# Maintainer: Kiri <kiri@vern.cc>

pkgname=orange
_module=Orange3
pkgver=3.36.0
pkgrel=1
pkgdesc="Component-based data mining framework."
arch=('x86_64')
url="https://orangedatamining.com"
license=('GPL')
depends=(python
         python-anyqt
         python-baycomp
         python-bottleneck
         python-chardet
         python-httpx
         python-joblib
         python-keyring
         python-keyrings.alt
         python-matplotlib
         python-networkx
         python-numpy
         python-openpyxl
         python-opentsne
         python-orange-canvas-core
         python-orange-widget-base
         python-pandas
         python-pip
         python-pygments
         python-pyqtgraph
         python-python-louvain
         python-yaml
         python-qtconsole
         python-requests
         python-scikit-learn
         python-scipy
         python-serverfiles
         python-setuptools
         python-xlrd
         python-xlsxwriter)
makedepends=(cython
             python-setuptools
             python-sphinx)
source=("${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_module::1}/$_module/${_module}-${pkgver}.tar.gz")
sha256sums=('5add451c1cc4b08a68611628812346f770f97ba1879783e0ad6d09c2ca5bdf39')
prepare() {
  # create execute file for /ure/bin
  printf '#!/usr/bin/sh\nexec python -m Orange.canvas' > ${pkgname}
  # fix desktop.entry
  sed -e 's/Exec=orange-canvas/Exec=orange/' \
      -e 's|Icon=orange-canvas|Icon=/usr/share/icons/'"$pkgname"'.ico|' \
      -i "$_module-$pkgver/distribute/orange-canvas.desktop"
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build
}

package() {
    cd "${srcdir}/${_module}-${pkgver}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/orange/LICENSE"
    python setup.py install --root="$pkgdir" --optimize=1
    install -Dm 755 ${srcdir}/${pkgname} $pkgdir/usr/bin/${pkgname}
    install -Dm 644 distribute/orange-canvas.desktop $pkgdir/usr/share/applications/${pkgname}.desktop
    install -Dm 644 Orange/canvas/icons/orange.ico ${pkgdir}/usr/share/icons/${pkgname}.ico
}
