# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: Pranav K Anupam <pranavanupam@yahoo.com>
# Contributor: Alex Branham <branham@utexas.edu>

_pkgname=reprex
_pkgver=2.0.2
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=3
pkgdesc="Prepare Reproducible Example Code via the Clipboard"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=(MIT)
depends=(
  pandoc
  r-callr
  r-cli
  r-clipr
  r-fs
  r-glue
  r-knitr
  r-lifecycle
  r-rlang
  r-rmarkdown
  r-rstudioapi
  r-withr
)
checkdepends=(
  r-mockr
  r-styler
  r-testthat
)
optdepends=(
  r-covr
  r-fortunes
  r-miniui
  r-mockr
  r-rprojroot
  r-sessioninfo
  r-shiny
  r-spelling
  r-styler
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('dfda2989d9317160661dcec6a8f2fed8')
b2sums=('d922d870648e455f20c4bfdbadfd6cce02c9ce634d6de1ce122ee2247987bf06d4f303635eb4308bc93355456e7daf250a80b20fc09f373e80595d67dd81aab2')

prepare() {
  # fix snapshot test
  sed -e 's/Please use /i Please use /' \
      -e 's/Use /i Use /' \
      -e 's/\*/i/' \
      -e 's/Working directory /i Working directory /' \
      -i "$_pkgname/tests/testthat/_snaps/utils-io.md"
}

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
