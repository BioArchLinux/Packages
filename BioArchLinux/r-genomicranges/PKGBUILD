# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=GenomicRanges
_pkgver=1.58.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Representation and manipulation of genomic intervals"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0')
depends=(
  r-biocgenerics
  r-genomeinfodb
  r-iranges
  r-s4vectors
  r-xvector
)
checkdepends=(
  r-runit
)
optdepends=(
  r-annotate
  r-annotationdbi
  r-annotationhub
  r-biobase
  r-biocstyle
  r-biostrings
  r-bsgenome
  r-bsgenome.hsapiens.ucsc.hg38
  r-bsgenome.mmusculus.ucsc.mm10
  r-bsgenome.scerevisiae.ucsc.saccer2
  r-deseq2
  r-dexseq
  r-digest
  r-edger
  r-genomicalignments
  r-genomicfeatures
  r-gviz
  r-hgu95av2.db
  r-hgu95av2probe
  r-kegggraph
  r-keggrest
  r-knitr
  r-pasillabamsubset
  r-rmarkdown
  r-rnaseqdata.hnrnpc.bam.chr14
  r-rsamtools
  r-rtracklayer
  r-runit
  r-summarizedexperiment
  r-txdb.athaliana.biomart.plantsmart22
  r-txdb.dmelanogaster.ucsc.dm3.ensgene
  r-txdb.hsapiens.ucsc.hg38.knowngene
  r-txdb.mmusculus.ucsc.mm10.knowngene
  r-txdbmaker
  r-variantannotation
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('cca337e05cca2a77511c410397e8d415')
b2sums=('4ef10a255bfdd6e00efff1be06b759599f8fd4ff80a2a71e26b2dfab9d77c0373c924e9ca994e5e77d902bed58d88d06356453991f57de04830aac72f107f26e')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" Rscript --vanilla run_unitTests.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
