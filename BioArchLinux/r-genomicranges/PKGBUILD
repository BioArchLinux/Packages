# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=GenomicRanges
_pkgver=1.54.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Representation and manipulation of genomic intervals"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0')
depends=(
  r-biocgenerics
  r-genomeinfodb
  r-iranges
  r-s4vectors
  r-xvector
)
checkdepends=(
  r-runit
)
optdepends=(
  r-annotate
  r-annotationdbi
  r-annotationhub
  r-biobase
  r-biocstyle
  r-biostrings
  r-bsgenome
  r-bsgenome.hsapiens.ucsc.hg19
  r-bsgenome.mmusculus.ucsc.mm10
  r-bsgenome.scerevisiae.ucsc.saccer2
  r-deseq2
  r-dexseq
  r-digest
  r-edger
  r-genomicalignments
  r-genomicfeatures
  r-gviz
  r-hgu95av2.db
  r-hgu95av2probe
  r-kegggraph
  r-keggrest
  r-knitr
  r-pasillabamsubset
  r-rmarkdown
  r-rnaseqdata.hnrnpc.bam.chr14
  r-rsamtools
  r-rtracklayer
  r-runit
  r-summarizedexperiment
  r-txdb.athaliana.biomart.plantsmart22
  r-txdb.dmelanogaster.ucsc.dm3.ensgene
  r-txdb.hsapiens.ucsc.hg19.knowngene
  r-txdb.mmusculus.ucsc.mm10.knowngene
  r-variantannotation
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('19fe37133cae70c1c3a1a164e6c8eaed')
b2sums=('d08b113f0f0afbcf154f91f582d4d91f8c97c79567ecd8993cc4ff2b1a3f63d62026b9c6c457b17f6fd470c82b73431a1a2a46508ed7062d5332c7146e881cb9')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" Rscript --vanilla run_unitTests.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
