# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=ShortRead
_pkgver=1.60.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="FASTQ input and manipulation"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0')
depends=(
  r-biobase
  r-biocgenerics
  r-biocparallel
  r-biostrings
  r-genomeinfodb
  r-genomicalignments
  r-genomicranges
  r-hwriter
  r-iranges
  r-latticeextra
  r-rsamtools
  r-s4vectors
  r-zlibbioc
  zlib
)
makedepends=(
  r-rhtslib
  r-xvector
)
optdepends=(
  r-biocstyle
  r-biomart
  r-genomicfeatures
  r-runit
  r-yeastnagalakshmi
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz"
        "$_pkgname-fix-format.patch::https://github.com/Bioconductor/ShortRead/pull/16.patch")
md5sums=('480786b1db8f8138a7a4e8c36f9bdfc4'
         'f627a5a0cb37167b7f60d98fe6381045')
b2sums=('7b20c86f1092be6cc5f0d4e1becfe257def06e3e958def1a235fd982238a62ed3ff605677a48fd50e3f73b90b3ffb676885d3a1efe5e0e7f9ea1acd56107d291'
        'b35a65671c15a8df40bac51e1985025a33d37a119534501c8f41ae50097a94dcdd73c6d631d34c58abefcc45c63a5e329d12ee02ceeeb28aa7757a1ae3a94fb2')

prepare() {
  # fix format string errors
  patch -Np1 -d "$_pkgname" < "$_pkgname-fix-format.patch"
}

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
