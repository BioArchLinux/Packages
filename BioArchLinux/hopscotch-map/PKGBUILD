# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Adrien Wu <adrien.sf.wu@gmail.com>

pkgname=hopscotch-map
pkgver=2.3.0
pkgrel=2
pkgdesc="C++ implementation of a fast hash map and hash set using hopscotch hashing"
arch=(any)
url="https://github.com/Tessil/hopscotch-map"
license=(MIT)
makedepends=(cmake)
checkdepends=(boost)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz")
sha256sums=('a59d65b552dc7682521989842418c92257147f5068152b5af50e917892ad9317')

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  cmake -B tests -S "$pkgname-$pkgver/tests"
  cmake --build tests
  ./tests/tsl_hopscotch_map_tests
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/LICENSE"
}
