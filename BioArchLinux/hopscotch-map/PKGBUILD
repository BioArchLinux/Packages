# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Adrien Wu <adrien.sf.wu@gmail.com>

pkgname=hopscotch-map
pkgver=2.4.0
pkgrel=1
pkgdesc="C++ implementation of a fast hash map and hash set using hopscotch hashing"
arch=(any)
url="https://github.com/Tessil/hopscotch-map"
license=(MIT)
makedepends=(cmake)
checkdepends=(boost)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz")
sha256sums=('c147d1f6af9559c0e91af3ecf62274404ce5fb35ce94d2234c080ccc7a5913de')

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  cmake -B tests -S "$pkgname-$pkgver/tests"
  cmake --build tests
  ./tests/tsl_hopscotch_map_tests
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/LICENSE"
}
