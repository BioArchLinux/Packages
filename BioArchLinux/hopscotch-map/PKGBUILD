# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Adrien Wu <adrien.sf.wu@gmail.com>

pkgname=hopscotch-map
pkgver=2.3.1
pkgrel=1
pkgdesc="C++ implementation of a fast hash map and hash set using hopscotch hashing"
arch=(any)
url="https://github.com/Tessil/hopscotch-map"
license=(MIT)
makedepends=(cmake)
checkdepends=(boost)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz")
sha256sums=('53dab49005cd5dc859f2546d0d3eef058ec7fb3b74fc3b19f4965a9a151e9b20')

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  cmake -B tests -S "$pkgname-$pkgver/tests"
  cmake --build tests
  ./tests/tsl_hopscotch_map_tests
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/LICENSE"
}
