# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Matt Frichtl <frichtlm@gmail.com>
# Contributor: Kibouo <csonka.mihaly@hotmail.com>
# Contributor: Ward Segers <w@rdsegers.be>
# Contributor: Alex Branham <alex.branham@gmail.com>

_pkgname=testthat
_pkgver=3.2.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Unit Testing for R"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-brio
  r-callr
  r-cli
  r-desc
  r-digest
  r-evaluate
  r-jsonlite
  r-lifecycle
  r-magrittr
  r-pkgload
  r-praise
  r-processx
  r-ps
  r-r6
  r-rlang
  r-waldo
  r-withr
)
optdepends=(
  r-covr
  r-curl
  r-diffviewer
  r-knitr
  r-rmarkdown
  r-rstudioapi
  r-shiny
  r-usethis
  r-vctrs
  r-xml2
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('cb9ddcf70c6b030743c9685e0f104f67')
b2sums=('83be91814b5b4606a0581fd30153cd7c0189abf7634396243f5a639fde023de65a0ba742da4259bbd5062585638203fa61a2a92b13a007db3c1349c1f3585730')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
