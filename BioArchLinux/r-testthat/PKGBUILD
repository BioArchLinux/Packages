# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=testthat
_pkgver=3.1.5
pkgname=r-${_pkgname,,}
pkgver=3.1.5
pkgrel=2
pkgdesc='Unit Testing for R'
arch=('x86_64')
url="https://cran.r-project.org/package=${_pkgname}"
license=('MIT')
depends=(
  r
  r-brio
  r-callr
  r-cli
  r-desc
  r-digest
  r-ellipsis
  r-evaluate
  r-jsonlite
  r-lifecycle
  r-magrittr
  r-pkgload
  r-praise
  r-processx
  r-ps
  r-r6
  r-rlang
  r-waldo
  r-withr
)
optdepends=(
  r-covr
  r-curl
  r-diffviewer
  r-knitr
  r-mockery
  r-rmarkdown
  r-rstudioapi
  r-shiny
  r-usethis
  r-vctrs
  r-xml2
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('a8f56b9426206ddfc30b550c82ff2f042ebe1c2f5bfd4184aec8facac8f5b7fc')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
  install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
