# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Matt Frichtl <frichtlm@gmail.com>
# Contributor: Kibouo <csonka.mihaly@hotmail.com>
# Contributor: Ward Segers <w@rdsegers.be>
# Contributor: Alex Branham <alex.branham@gmail.com>

_pkgname=testthat
_pkgver=3.2.2
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=0
pkgdesc="Unit Testing for R"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-brio
  r-callr
  r-cli
  r-desc
  r-digest
  r-evaluate
  r-jsonlite
  r-lifecycle
  r-magrittr
  r-pkgload
  r-praise
  r-processx
  r-ps
  r-r6
  r-rlang
  r-waldo
  r-withr
)
optdepends=(
  r-covr
  r-curl
  r-diffviewer
  r-knitr
  r-rmarkdown
  r-rstudioapi
  r-s7
  r-shiny
  r-usethis
  r-vctrs
  r-xml2
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('8259f5323867d86b628580bfe70ab924')
b2sums=('10ec4de0db26f16aa99885b47ec27ca0bbc4535b073179e34c88388bbdeb261c167575b3792906239df116367dec2ec7cdd1a5c764f6f1c428653563e523fe3c')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
