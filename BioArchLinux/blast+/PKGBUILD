# Maintainer: Yishen Miao <mys721tx@gmail.com>
# Maintainer: Nathaniel Stickney <nstickney@gmail.com>
# Contributor: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>
# shellcheck disable=SC2034,SC2148,SC2154
pkgname=blast+
pkgver=29.6.0
_pkgver=${pkgver//./_}
pkgrel=1
pkgdesc="BLAST tool suite (blastn, blastp, blastx, psiblast) built from NCBI C++ toolkit sources"
arch=('i686' 'x86_64')
url="http://blast.ncbi.nlm.nih.gov/"
license=('NCBI-PD')
depends=('python' 'pcre2' 'sqlite' 'lzo' 'libtiff' 'giflib' 'libxpm' 'glibc' 'gcc-libs' 'curl'
         'mariadb-libs' 'libpng' 'gnutls' 'libxml2' 'libxslt' 'lmdb' 'gnutls' 'openssl' 'krb5' 'hdf5' 'bzip2' 'zlib' 'lzo' 'zstd' 'bash' 'libjpeg-turbo' 'e2fsprogs' 'perl' 'db')
makedepends=('autoconf' 'cpio')
conflicts=('blast' 'blast+-bin' 'ncbi-blast' 'ncbi-tools++')
provides=('blast' 'igblast')
replaces=('ncbi-blast')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/ncbi/ncbi-cxx-toolkit-public/archive/refs/tags/release/${pkgver}.tar.gz
        LICENSE)
sha256sums=('c370ede357471dfb8872c13b128ea9c16040511ad90d6bce700880a463351792'
            '78bbf3f310ff43f1b5f711e7221d51da1e6f055831bd6c6941e0650bf1261df2')

build() {
  # Adapted from debian packaging
  # refer https://ncbi.github.io/cxx-toolkit/pages/ch_config.html#ch_config.Configuring_with_UNI for details about build FLAGS
  cd "$srcdir/ncbi-cxx-toolkit-public-release-${pkgver}"
  rm -f "$srcdir"/build/inc/ncbiconf_unix.h # in case of previous build
  #---------Prebuild project_tree_builder binary separately--------------------------------
  export CONFIGURE_COMMON_FLAGS="\
                          --without-autodep \
                          --without-makefile-auto-update \
                          --with-flat-makefile \
                          --without-caution \
                          --without-lzo \
                          --without-debug \
                          --without-downloaded-vdb \
                          --without-sse42"
  export NATIVE=".native"
  export NATIVE_BINDIR="${srcdir}/BUILD${NATIVE}/bin"

  export PREBUILT_DATATOOL_EXE=${NATIVE_BINDIR}/datatool
  export PREBUILT_PTB_EXE=${NATIVE_BINDIR}/project_tree_builder
  #export PREBUILT_DATATOOL_EXE PREBUILT_PTB_EXE
  PREBUILT_PTB_EXE=bootstrap \
            ./configure ${CONFIGURE_COMMON_FLAGS} --without-app \
             --with-build-root=${srcdir}/BUILD${NATIVE}  ||  \
            (tail -v -n +0 config.log BUILD.native/status/config.log; exit 1)
	make -C ${srcdir}/BUILD${NATIVE}/build -f Makefile.flat \
	    datatool.exe

  #-----------build only blast binaries and dependencies------------------------------------
    export proj="algo/blast/ app/ objmgr/ objtools/align_format/ objtools/blast/"
    ./configure --without-autodep \
      --without-makefile-auto-update \
      --with-flat-makefile\
      --without-caution\
      --without-debug \
      --without-downloaded-vdb \
      --prefix=/usr \
      --libdir=/lib/blast+ \
      --with-bin-release \
      --with-dll \
      --with-mt \
      --with-lmdb=/usr/lib \
      --with-hdf5=/usr/lib \
      --with-libxslt=/usr/lib \
      --with-z=/usr/lib \
      --with-bz2=/usr/lib \
      --with-lzo=/usr/lib \
      --with-zstd=/usr/lib \
      --with-pcre2=/usr/lib \
      --with-gnutls=/usr/lib \
      --with-openssl=/usr/lib \
      --with-krb5=/usr/lib/ \
      --with-bdb=/usr/lib/ \
      --with-curl=/usr/lib/ \
      --without-boost \
      --with-64 \
      --with-build-root=${srcdir}/build_blast

    cd $srcdir/build_blast/build
    make -f Makefile.flat all_projects="${proj}"
}

package() {
  cd "${srcdir}/ncbi-cxx-toolkit-public-release-${pkgver}"
  make prefix="${pkgdir}"/usr libdir="${pkgdir}"/usr/lib/blast+ install
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/license
}
