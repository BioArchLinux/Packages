# import from AUR: bipin kumar <kbipinkumar@pm.me>
# Maintainer: Matthew McGinn <mamcgi@gmail.com>
# Contributor: Stunts <f.pinamartins@gmail.com>

pkgname=python-matplotlib-venn
_module=${pkgname#python-}
pkgver=1.1.2
pkgrel=2
pkgdesc="Venn diagram plotting routines for python-matplotlib"
arch=('any')
url="https://github.com/konstantint/matplotlib-venn"
license=('MIT')
depends=('python-numpy' 'python-matplotlib' 'python-scipy')
makedepends=('python-setuptools')
source=($_module-$pkgver::"https://github.com/konstantint/matplotlib-venn/archive/refs/tags/$pkgver.tar.gz")
b2sums=('995747f378a0295011515b2448b2512e41b24ea9b4826b9f93c2089543bbf60859fa593a72c5a02f332264b97f166967ff7b46b042e8887a56257441831cdb20')

build() {
  cd "$_module-$pkgver"
  python setup.py build
}

package() {
  cd "$_module-$pkgver"
  python setup.py install --root="${pkgdir}"
  install -Dm644 "$srcdir/$_module-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}
