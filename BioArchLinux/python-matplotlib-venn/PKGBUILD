# import from AUR: bipin kumar <kbipinkumar@pm.me>
# Maintainer: Matthew McGinn <mamcgi@gmail.com>
# Contributor: Stunts <f.pinamartins@gmail.com>

pkgname=python-matplotlib-venn
_module=${pkgname#python-}
pkgver=1.0.0
pkgrel=1
pkgdesc="Venn diagram plotting routines for python-matplotlib"
arch=('any')
url="https://github.com/konstantint/matplotlib-venn"
license=('MIT')
depends=('python-numpy' 'python-matplotlib' 'python-scipy')
makedepends=('python-setuptools')
source=($_module-$pkgver::"https://github.com/konstantint/matplotlib-venn/archive/refs/tags/$pkgver.tar.gz")
b2sums=('48475fa29d2525fb9a44d72b37008773b015184ed1357cae309d77d8ff8a180d84705b7540b62fb137d2949983b0a0b3550df9c86b853930a83d3af5904b3d77')

build() {
  cd "$_module-$pkgver"
  python setup.py build
}

package() {
  cd "$_module-$pkgver"
  python setup.py install --root="${pkgdir}"
  install -Dm644 "$srcdir/$_module-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}
