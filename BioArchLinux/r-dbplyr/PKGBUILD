# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Robert Greener <me@r0bert.dev>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Matt Frichtl <frichtlm@gmail.com>
# Contributor: Alex Branham <branham@utexas.edu>

_pkgname=dbplyr
_pkgver=2.5.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="A 'dplyr' Back End for Databases"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-blob
  r-cli
  r-dbi
  r-dplyr
  r-glue
  r-lifecycle
  r-magrittr
  r-pillar
  r-purrr
  r-r6
  r-rlang
  r-tibble
  r-tidyr
  r-tidyselect
  r-vctrs
  r-withr
)
checkdepends=(
  mariadb
  postgresql
  r-bit64
  r-rmariadb
  r-rpostgres
  r-rsqlite
  r-testthat
)
optdepends=(
  r-bit64
  r-covr
  r-knitr
  r-lahman
  r-nycflights13
  r-odbc
  r-rmariadb
  r-rmarkdown
  r-rpostgres
  r-rpostgresql
  r-rsqlite
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('e72abd281aaaeeabe1a88e9c1877c0fe')
b2sums=('1902d757641cac63ede87695a1919493d41eff63a8455317b19ee1f1025d5fd2b36fd8ee143973736ae55ebef701556b5978363ffd0bfd11c711fbed9740742b')

prepare() {
  cd "$_pkgname/tests/testthat"
  # connect to database using sockets in tests
  sed -i '/host = "localhost",/d' helper-src.R
  # skip snapshot test that records sqlite version
  sed -e '/"check_from is deprecated"/a\ \ skip("records sqlite version")' \
      -i test-tbl-sql.R
}

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"

  # create mariadb database for tests
  export HOME="$srcdir" # make sure mariadb doesn't read ~/.my.cnf
  export MARIADB_HOME="$srcdir/mariadb"
  mkdir -p "$MARIADB_HOME/data"
  cat > "$MARIADB_HOME/my.cnf" << EOF
[client-server]
socket="$MARIADB_HOME/mariadb.sock"

[server]
skip_networking=1
datadir="$MARIADB_HOME/data"
EOF
  mariadb-install-db
  mariadbd &

  # create postgres database for tests
  export PGDATA="$srcdir/postgres-db"
  export PGHOST="$srcdir/postgres-host"
  mkdir "$PGHOST"
  initdb
  pg_ctl -o "-h '' -k ${PGHOST@Q}" start
  createdb test

  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R

  # shut down test databases
  mariadb-admin shutdown
  pg_ctl stop
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
