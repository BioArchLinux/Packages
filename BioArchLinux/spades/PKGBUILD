# Maintainer: PumpkinCheshire <me at pumpkincheshire dot top>
# Maintainer: alienzj <alienchuj at gmail dot com>

pkgname=spades
pkgver=3.15.5
pkgrel=2
pkgdesc='SPAdes Genome Assembler'
url='http://cab.spbu.ru/software/spades/'
arch=('x86_64')
license=('GPL2')
depends=(
  python
)
makedepends=(
  bzip2
  cmake
  openmp
  zlib
)
options=("!lto")

source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/ablab/spades/archive/refs/tags/v${pkgver}.tar.gz"
)
sha512sums=('30aed03cc6047b10657a850897fc43058c0e2deef5402081278b91442820d0829c02505357c84c3dd809ae41adf315c7da195027c5a58fdd9ce9a343e0049879')

build() {
  cmake -B build -S ${pkgname}-${pkgver}/src \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DCMAKE_BUILD_TYPE=None
  make -C build
}

package() {
  make DESTDIR="${pkgdir}" -C build install
  ln -s "/usr/bin/spades.py" "${pkgdir}/usr/bin/${pkgname}"
}
# vim:set ts=2 sw=2 et:
