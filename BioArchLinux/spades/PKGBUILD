# Maintainer: PumpkinCheshire <me at pumpkincheshire dot top>
# Maintainer: alienzj <alienchuj at gmail dot com>

pkgname=spades
pkgver=4.1.0
pkgrel=1
pkgdesc='SPAdes Genome Assembler'
url='http://cab.spbu.ru/software/spades/'
arch=('x86_64')
license=('GPL2')
depends=(
  python
)
makedepends=(
  bzip2
  cmake
  openmp
  zlib
)
options=("!lto")

source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/ablab/spades/archive/refs/tags/v${pkgver}.tar.gz"
)
sha512sums=('2b515d06e5fe1dc6ea1a2ce23302a3c97a344d7f3302710de7f9c7c3a6448b7460cd1d587fba7f32a88bb6a10828a8a4c22bbda9e3aca4da4a43b3b4dba9c1ed')

build() {
  cmake -B build -S ${pkgname}-${pkgver}/src \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DCMAKE_BUILD_TYPE=None
  make -C build
}

package() {
  make DESTDIR="${pkgdir}" -C build install
  ln -s "/usr/bin/spades.py" "${pkgdir}/usr/bin/${pkgname}"
}
# vim:set ts=2 sw=2 et:
