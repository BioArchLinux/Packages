# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=XVector
_pkgver=0.48.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Foundation of external vector representation and manipulation in Bioconductor"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0')
depends=(
  r-biocgenerics
  r-iranges
  r-s4vectors
  zlib
)
optdepends=(
  r-biostrings
  r-drosophila2probe
  r-runit
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('7e071d7e7cbf69a9a84890b93e64dba8')
b2sums=('4c4c0ada295bfe015c842d5bf4f1977fdb2218710aeee6503d5000c7b800af105f37f0822ddb8a274b974d38fd9292ffc16dc99169337e86e4780fb85ee0eeac')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
