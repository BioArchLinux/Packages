# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=XVector
_pkgver=0.46.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Foundation of external vector representation and manipulation in Bioconductor"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0')
depends=(
  r-biocgenerics
  r-iranges
  r-s4vectors
  r-zlibbioc
  zlib
)
optdepends=(
  r-biostrings
  r-drosophila2probe
  r-runit
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('038527773e20903db8e40a7cfa44ab27')
b2sums=('2213876efe04755c9c2f1b9adb7a1d6a74856f7c798b0ae7a642128e8fa1cb7e1d374c74cbc4432d2e0f8a8ace0c8d0253be814b1725cf605bed9793aaf29512')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
