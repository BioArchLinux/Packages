# Maintainer:  Mick Elliot <m.g.elliot@rug.nl>
# Contributor: Mick Elliot <m.g.elliot@rug.nl>

pkgname=minimap2
pkgver=2.26
pkgrel=2
pkgdesc='A versatile pairwise aligner for genomic and spliced nucleotide sequences. https://doi.org/10.1093/bioinformatics/bty191'
arch=('x86_64')
url="https://github.com/lh3/${pkgname}"
license=('MIT')
depends=('zlib' 'glibc')
provides=('minimap2-lite' 'sdust')
source=("${pkgver}.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('f4c8c3459c7b87e9de6cbed7de019b48d9337c2e46b87ba81b9f72d889420b3c')

prepare() {
  cd "${pkgname}-${pkgver}"
  # Add hardening flags for binaries
  sed -i '1s/CFLAGS=/CFLAGS+=/' Makefile
  sed -i '2s/CPPFLAGS=/CPPFLAGS+=/' Makefile
  sed -i '52s/$(LIBS)/$(LIBS) $(LDFLAGS)/' Makefile
  sed -i '55s/$(LIBS)/$(LIBS) $(LDFLAGS)/' Makefile
  sed -i '61s/-lz/-lz $(LDFLAGS)/' Makefile
  sed -i '57a\\t\ \trm -f "$@"' Makefile
  sed -i '59s/-csru/csrD/' Makefile
}

build() {
  cd "${pkgname}-${pkgver}"
  # Build all binaries
  make extra

}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 libminimap2.a "${pkgdir}/usr/lib/libminimap2.a"
  install -Dm755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm755 minimap2-lite "${pkgdir}/usr/bin/minimap2-lite"
  install -Dm755 sdust "${pkgdir}/usr/bin/sdust"
  # install test Data
  install -d ${pkgdir}/usr/share/${pkgname}/
  cp -r test/ ${pkgdir}/usr/share/${pkgname}/
  # install Manpages
  mkdir -p ${pkgdir}/usr/share/man/man1/
  cp  minimap2.1 ${pkgdir}/usr/share/man/man1/minimap2.1
}
