# Maintainer:  Mick Elliot <m.g.elliot@rug.nl>
# Contributor: Mick Elliot <m.g.elliot@rug.nl>

pkgname=minimap2
pkgver=2.29
pkgrel=1
pkgdesc='A versatile pairwise aligner for genomic and spliced nucleotide sequences. https://doi.org/10.1093/bioinformatics/bty191'
arch=('x86_64')
url="https://github.com/lh3/${pkgname}"
license=('MIT')
depends=('zlib' 'glibc')
provides=('minimap2-lite' 'sdust' 'libminimap2.so')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz"
        "hardening.patch")
sha256sums=('008d5e9848b918e4eb09d054aa3d6c272d50e92b8fc8537abf5081a507019718'
            'ff10a6b715db2e08add52b371842778f855cb4edc9f7fc373c6e1b670ef1415f')

prepare() {
  cd "${pkgname}-${pkgver}"
  # Add hardening flags for binaries
  patch -p1 < "${srcdir}"/hardening.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  # Build all binaries
  make extra

}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm755 libminimap2.so "${pkgdir}"/usr/lib/libminimap2.so
  install -Dm755 ${pkgname} "${pkgdir}"/usr/bin/${pkgname}
  install -Dm755 minimap2-lite "${pkgdir}"/usr/bin/minimap2-lite
  install -Dm755 sdust "${pkgdir}"/usr/bin/sdust
  # install Manpages
  install -Dm644 -t "$pkgdir/usr/share/man/man1" minimap2.1
  # install test Data
  install -d "${pkgdir}"/usr/share/${pkgname}/test
  cd test
  for file in *; do
    install -Dm644 "$file" "${pkgdir}"/usr/share/${pkgname}/test/"${file}"
  done
}
