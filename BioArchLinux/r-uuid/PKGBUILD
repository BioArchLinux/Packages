# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: Pranav K Anupam <pranavanupam@yahoo.com>

_pkgname=uuid
_pkgver=1.1-1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Tools for Generating and Handling of UUIDs"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=(MIT)
depends=(
  r
  util-linux-libs
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz"
        "system-uuid.patch")
md5sums=('51555df70ad6c99fd966f78f45abebaa'
         'fc5b61316f82d529e10881188302d532')
sha256sums=('1611240eb706e6f53400b25c9cf792ad90f151b72ed0918a1e756997f7abb716'
            '3cac969fc2d4b8b64bbb21c9967d9f97c57e086341fd29495d6d99d7f139a7c5')

prepare() {
  # build with system libuuid
  cd "$_pkgname/src"
  rm *.h
  patch -Np1 -i ../../system-uuid.patch
}

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"

  # use system uuid.h
  ln -sf /usr/include/uuid/uuid.h "$pkgdir/usr/lib/R/library/$_pkgname/include"
}
