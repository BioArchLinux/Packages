# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: Pranav K Anupam <pranavanupam@yahoo.com>

_pkgname=uuid
_pkgver=1.2-0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Tools for Generating and Handling of UUIDs"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r
  util-linux-libs
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz"
        "system-uuid.patch")
md5sums=('e2888f2648528022ab731b3683bb7ea6'
         'fc5b61316f82d529e10881188302d532')
b2sums=('b46dad29c4d3e2c6e593eecaf330ff2dde255ed26d47d387a455d2032eed47e47249472d65190ff7d511ee2389a6cf94ddd728cf1be737f9abcfb189e66615cf'
        '7c0911a64fed42baec57ec5fccf8861c735e31da6b94b80f2f2c2b14e7fdd4843c6aad5c4e6b85dd1008bd83f523a47869a70ffdfa5259719f9d8277890d6e52')

prepare() {
  # build with system libuuid
  cd "$_pkgname/src"
  rm *.h
  patch -Np1 -i ../../system-uuid.patch
}

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"

  # use system uuid.h
  ln -sf /usr/include/uuid/uuid.h "$pkgdir/usr/lib/R/library/$_pkgname/include"
}
