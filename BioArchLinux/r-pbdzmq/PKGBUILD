# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: portaloffreedom

_pkgname=pbdZMQ
_pkgver=0.3-13
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Programming with Big Data -- Interface to 'ZeroMQ'"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-3.0-only')
depends=(
  r
  zeromq
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('55d1c6bd40d0d037bcb6537d2550be81')
b2sums=('48522eba4fc94c013710ca98213c274d5cb9d0ae6ad37cc5d51682ec5e3c75b0db5b7ec8e5bc8f995d40689a19ecde03d3e0f0a145bc256d4ef864ced38d5f88')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
