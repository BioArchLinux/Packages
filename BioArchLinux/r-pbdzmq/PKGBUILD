# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: portaloffreedom

_pkgname=pbdZMQ
_pkgver=0.3-11
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Programming with Big Data -- Interface to 'ZeroMQ'"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=(GPL3)
depends=(
  r
  zeromq
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('01d91cffe8b6ac672a1aa1c71ca76121')
b2sums=('95a3cb026e9adcd78c976707b1ae28b34c9d9f2bfda4d5ce7b6399d6cfedf4a75d0d691abde1ad1c85c5255ccc759c34df674a8292df50d750c819e1238c5cd4')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
