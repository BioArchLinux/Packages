# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=bowtie2
pkgver=2.5.3
pkgrel=1
pkgdesc="Tool for aligning sequencing reads to long reference sequences \
	 https://doi.org/10.1038/nmeth.1923"
arch=('x86_64' 'aarch64')
url="http://bowtie-bio.sourceforge.net/bowtie2/index.shtml"
license=('GPL-3.0-only')
depends=('glibc' 'zstd' 'gcc-libs' 'zlib' 'python' 'bash')
source=("$pkgname-$pkgver.tar.gz::https://github.com/BenLangmead/bowtie2/archive/v${pkgver}.tar.gz")
sha256sums=('2f86dbfbf3dcb8521d559f830594fe28ac6e4c40d81313a8c2bfb17c82a501e0')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make clean
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make PREFIX=/usr WITH_ZSTD=1
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install

    install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
    cp -a "${srcdir}/bowtie2-${pkgver}/doc" "${pkgdir}/usr/share/doc/${pkgname}"
}
