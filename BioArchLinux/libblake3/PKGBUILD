# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: rpkak <rpkak@users.noreply.github.com>

pkgname=libblake3
pkgver=1.5.0
pkgrel=1
pkgdesc="the C implementation of the BLAKE3 cryptographic hash function"
arch=(x86_64)
url="https://github.com/BLAKE3-team/BLAKE3"
license=(custom:CC0 Apache)
depends=(
  glibc
)
makedepends=(
  cmake
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('f506140bc3af41d3432a4ce18b3b83b08eaa240e94ef161eb72b2e57cdc94c69')

build() {
  cmake -B build -S "BLAKE3-$pkgver/c" \
      -DCMAKE_BUILD_TYPE=None \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_SHARED_LIBS=ON \
      -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "BLAKE3-$pkgver/LICENSE"
}
