# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: rpkak <rpkak@users.noreply.github.com>

pkgname=libblake3
pkgver=1.8.1
pkgrel=1
pkgdesc="the C implementation of the BLAKE3 cryptographic hash function"
arch=(x86_64)
url="https://github.com/BLAKE3-team/BLAKE3"
license=('CC0-1.0 OR Apache-2.0 OR Apache-2.0 WITH LLVM-exception')
depends=(
  glibc
)
makedepends=(
  cmake
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('fc2aac36643db7e45c3653fd98a2a745e6d4d16ff3711e4b7abd3b88639463dd')

build() {
  cmake -B build -S "BLAKE3-$pkgver/c" \
      -DCMAKE_BUILD_TYPE=None \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_SHARED_LIBS=ON \
      -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "BLAKE3-$pkgver/LICENSE_A2LLVM"
}
