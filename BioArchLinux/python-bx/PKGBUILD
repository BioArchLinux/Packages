# Maintainer: sukanka <su975853527  at gmail>
pkgname=python-bx
_pkgname=bx-python
pkgver=0.8.14
pkgrel=2
pkgdesc="Tools for manipulating biological data, particularly multiple sequence alignments"
arch=('x86_64')
url="https://github.com/bxlab/bx-python"
license=('MIT')
depends=('lzo' 'python' 'python-numpy')
makedepends=('python-setuptools' 'cython' )
provides=('bx-python')
options=(!emptydirs)
source=($_pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha256sums=('a8a75cdab4682d3f223b761dc9aae297e3bb9959aad91c07f3e8b47cf56effa0')

build(){
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py build
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
