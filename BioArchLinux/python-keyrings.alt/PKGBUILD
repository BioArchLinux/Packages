# Maintainer: Kiri <kiri@vern.cc>

pkgname='python-keyrings.alt'
_module=keyrings.alt
pkgver=5.0.1
pkgrel=2
pkgdesc="Alternate keyring implementations"
arch=('x86_64')
url="https://github.com/jaraco/keyrings.alt"
license=('MIT')
depends=(python
         python-jaraco.classes)
makedepends=(python-build
             python-installer
             python-setuptools
             python-setuptools-scm
             python-wheel)
source=("${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_module::1}/$_module/${_module}-${pkgver}.tar.gz")
sha256sums=('cd372a1ec446a1bc5a90624a52c88e83b9330218e39047a6c9a48ae37d116745')

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_module}-${pkgver}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python-keyrings.alt/LICENSE"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
