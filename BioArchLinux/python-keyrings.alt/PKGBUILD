# Maintainer: Kiri <kiri@vern.cc>

pkgname='python-keyrings.alt'
_module=keyrings.alt
pkgver=4.2.0
pkgrel=1
pkgdesc="Alternate keyring implementations"
arch=('x86_64')
url="https://github.com/jaraco/keyrings.alt"
license=('MIT')
depends=(python
         python-jaraco.classes)
makedepends=(python-build
             python-installer
             python-setuptools
             python-setuptools-scm
             python-wheel)
source=("${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_module::1}/$_module/${_module}-${pkgver}.tar.gz")
sha256sums=('2ba3d56441ba0637f5f9c096068f67010ac0453f9d0b626de2aa3019353b6431')

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_module}-${pkgver}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python-keyrings.alt/LICENSE"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
