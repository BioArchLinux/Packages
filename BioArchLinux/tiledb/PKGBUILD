# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Josh Sixsmith <josh dot sixsmith at gmail dot com>
# Contributor: Morteza NourelahiAlamdari <m@0t1.me>

pkgname=tiledb
pkgver=2.23.0
pkgrel=0
pkgdesc="The Universal Storage Engine"
arch=(x86_64)
url="https://tiledb.com"
license=('MIT')
depends=(
  bzip2
  capnproto
  curl
  file
  fmt
  lz4
  openssl
  spdlog
  zlib
  zstd
)
makedepends=(
  git
  cmake
)
source=("git+https://github.com/TileDB-Inc/TileDB.git#tag=$pkgver"
        "system-libs.patch")
b2sums=('1423e7562ecace0f8b45d0b574b68532f6beed8cb257820d4f3fa08b14e8eab77bc7e9afee51e1d69512c340f7a95929aae82631548739a29c53d079531b43e3'
        '4c99b48a72cc921caa071d25f37380ea09878924d5b00c403bf00ca065e28a2d4e4304d85a2da4b6dc924532fa16f178fc8f3ec7c3b0c37f831f203b977c4117')

prepare() {
  # fix build with system libraries
  patch -Np1 -d TileDB < system-libs.patch
}

build() {
  TILEDB_DISABLE_AUTO_VCPKG=y \
      cmake -B build -S TileDB \
      -DCMAKE_BUILD_TYPE=None \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DTILEDB_SERIALIZATION=ON \
      -DTILEDB_TESTS=OFF \
      -DTILEDB_WEBP=OFF \
      -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build/tiledb
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" TileDB/LICENSE
}
