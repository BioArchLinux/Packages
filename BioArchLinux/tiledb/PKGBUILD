# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Josh Sixsmith <josh dot sixsmith at gmail dot com>
# Contributor: Morteza NourelahiAlamdari <m@0t1.me>

pkgname=tiledb
pkgver=2.22.0
pkgrel=2
pkgdesc="The Universal Storage Engine"
arch=(x86_64)
url="https://tiledb.com"
license=(MIT)
depends=(
  bzip2
  capnproto
  curl
  file
  fmt
  lz4
  openssl
  spdlog
  zlib
  zstd
)
makedepends=(
  cmake
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/TileDB-Inc/TileDB/archive/$pkgver.tar.gz"
        "system-libs.patch")
sha256sums=('55848552a169a029d2b73e14f4efc846117f41e42f742402f774290d12f410be'
            'f1507699a21b988876310aa9232f959a9e0311806344752aca0d41fd518fedc3')

prepare() {
  cd "TileDB-$pkgver"
  # use system libmagic and capnproto
  patch -Np1 -i ../system-libs.patch
}

build() {
  cmake -B build -S "TileDB-$pkgver" \
      -DCMAKE_BUILD_TYPE=None \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DTILEDB_SERIALIZATION=ON \
      -DTILEDB_TESTS=OFF \
      -DTILEDB_VCPKG=OFF \
      -DTILEDB_WEBP=OFF \
      -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build/tiledb
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "TileDB-$pkgver/LICENSE"
}
