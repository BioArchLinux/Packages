# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Josh Sixsmith <josh dot sixsmith at gmail dot com>
# Contributor: Morteza NourelahiAlamdari <m@0t1.me>

pkgname=tiledb
pkgver=2.24.1
pkgrel=1
pkgdesc="The Universal Storage Engine"
arch=(x86_64)
url="https://tiledb.com"
license=('MIT')
depends=(
  bzip2
  capnproto
  curl
  file
  fmt
  lz4
  openssl
  spdlog
  zlib
  zstd
)
makedepends=(
  git
  cmake
)
source=("git+https://github.com/TileDB-Inc/TileDB.git#tag=$pkgver"
        "system-libs.patch")
b2sums=('3af0fc4a928c82b81047d0676208f34a64423d0eaa03efa25795f3bb4bc509ed56f04a2933cb3b7b6ba6479658bb36736f99753170b981912a84ba31420db344'
        '4c99b48a72cc921caa071d25f37380ea09878924d5b00c403bf00ca065e28a2d4e4304d85a2da4b6dc924532fa16f178fc8f3ec7c3b0c37f831f203b977c4117')

prepare() {
  # fix build with system libraries
  patch -Np1 -d TileDB < system-libs.patch
}

build() {
  TILEDB_DISABLE_AUTO_VCPKG=y \
      cmake -B build -S TileDB \
      -DCMAKE_BUILD_TYPE=None \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DTILEDB_SERIALIZATION=ON \
      -DTILEDB_TESTS=OFF \
      -DTILEDB_WEBP=OFF \
      -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build/tiledb
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" TileDB/LICENSE
}
