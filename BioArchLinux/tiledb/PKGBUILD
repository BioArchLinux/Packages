# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Josh Sixsmith <josh dot sixsmith at gmail dot com>
# Contributor: Morteza NourelahiAlamdari <m@0t1.me>

pkgname=tiledb
pkgver=2.17.4
pkgrel=1
pkgdesc="The Universal Storage Engine"
arch=(x86_64)
url="https://tiledb.com"
license=(MIT)
depends=(
  bzip2
  capnproto
  curl
  file
  fmt
  lz4
  openssl
  spdlog
  zlib
  zstd
)
makedepends=(
  cmake
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/TileDB-Inc/TileDB/archive/$pkgver.tar.gz"
        "system-libs.patch")
sha256sums=('7ef1c88415045eda28666f7eceb75146c49443b1eee162aa28c3b7e27496b769'
            '47241a8b29d0094454be6b21fb5942bdb757e6f1cc72e4598d18eba405f15fc4')

prepare() {
  cd "TileDB-$pkgver"
  # use system libmagic and capnproto
  patch -Np1 -i ../system-libs.patch
}

build() {
  cmake -B build -S "TileDB-$pkgver" \
      -DCMAKE_BUILD_TYPE=None \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DTILEDB_SERIALIZATION=ON \
      -DTILEDB_TESTS=OFF \
      -DTILEDB_WEBP=OFF \
      -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build/tiledb
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "TileDB-$pkgver/LICENSE"
}
