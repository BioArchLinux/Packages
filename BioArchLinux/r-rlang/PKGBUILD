# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=rlang
_pkgver=1.1.3
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Functions for Base Types and Core R and 'Tidyverse' Features"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r
)
optdepends=(
  r-cli
  r-covr
  r-crayon
  r-fs
  r-glue
  r-knitr
  r-magrittr
  r-pillar
  r-rmarkdown
  r-testthat
  r-tibble
  r-usethis
  r-vctrs
  r-withr
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('cf4c3b1270d4d047d5c8104da270ba30')
b2sums=('c4641ee54942c6621f0b7b744da3197f582302222dad8f89eec4e7f4e4439deebd641a8292d6aad840690a7f24e43b9af316b353b72d5efbce712b8c9ea15648')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
