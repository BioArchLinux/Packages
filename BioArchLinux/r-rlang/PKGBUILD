# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=rlang
_pkgver=1.1.2
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Functions for Base Types and Core R and 'Tidyverse' Features"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=(MIT)
depends=(
  r
)
optdepends=(
  r-cli
  r-covr
  r-crayon
  r-fs
  r-glue
  r-knitr
  r-magrittr
  r-pillar
  r-rmarkdown
  r-testthat
  r-tibble
  r-usethis
  r-vctrs
  r-withr
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('ae2e1e4d5cee429b6871e6e9d3e3280e')
b2sums=('f4137660dddc6106661898ec5200364fa87c392dd27bd9c1b5883563b86029c67972c12989b4ab68c3e56ba8727ad18386f29b62a75eded43170da6144d2b772')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
