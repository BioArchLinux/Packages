# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=tidyseurat
_pkgver=0.7.9
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Brings Seurat to the Tidyverse"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=(GPL3)
depends=(
  r-cli
  r-dplyr
  r-ellipsis
  r-fansi
  r-ggplot2
  r-lifecycle
  r-magrittr
  r-pillar
  r-plotly
  r-purrr
  r-rlang
  r-seurat
  r-seuratobject
  r-stringr
  r-tibble
  r-tidyr
  r-tidyselect
  r-ttservice
  r-vctrs
)
checkdepends=(
  r-testthat
)
optdepends=(
  r-ggally
  r-knitr
  r-markdown
  r-singler
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('e112239c72797c4e3a81852c6f3bb555')
b2sums=('414d3b412235ce8ddeca9e7d81e02504b8cf94a5ca8b19ca2fbc4b6bdacfdc9f0885e577ecb8caa437f0ee5ad0fa4dabc46349c177d0ec488ae6ecd66afce82c')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
