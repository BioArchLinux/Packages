# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=BiocParallel
_pkgver=1.44.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Bioconductor facilities for parallel evaluation"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('GPL-2.0-or-later')
depends=(
  r-futile.logger
  r-snow
)
makedepends=(
  r-bh
  r-cpp11
)
optdepends=(
  r-batchtools
  r-bbmisc
  r-biocgenerics
  r-biocstyle
  r-data.table
  r-doparallel
  r-foreach
  r-genomicalignments
  r-genomicranges
  r-knitr
  r-rnaseqdata.hnrnpc.bam.chr14
  r-rsamtools
  r-runit
  r-shortread
  r-txdb.hsapiens.ucsc.hg19.knowngene
  r-variantannotation
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('75ab7c6895d7c10cbf15f4335adc6f08')
b2sums=('331b492fff7820270e77566ec9d7277452ccc7ade2446ec468dd4a8b13306ef413847367380c2fb05b0aa44bed9cc09104bb9b98a49b35192de6a4603fd5511c')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
