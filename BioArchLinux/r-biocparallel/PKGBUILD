# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=BiocParallel
_pkgver=1.38.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Bioconductor facilities for parallel evaluation"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('GPL-2.0-only OR GPL-3.0-only')
depends=(
  r-futile.logger
  r-snow
)
makedepends=(
  r-bh
  r-cpp11
)
optdepends=(
  r-batchtools
  r-bbmisc
  r-biocgenerics
  r-biocstyle
  r-data.table
  r-doparallel
  r-foreach
  r-genomicalignments
  r-genomicranges
  r-knitr
  r-rnaseqdata.hnrnpc.bam.chr14
  r-rsamtools
  r-runit
  r-shortread
  r-txdb.hsapiens.ucsc.hg19.knowngene
  r-variantannotation
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('1871c9e248d8da68b52166de0026fd2c')
b2sums=('1a1d2dc9a8769156e16001ff3a353d007a3c0b6e0e4d5c64424ff7920735611dcafbb9f4fa1d7014655ca007afbf9742d7b0d90cce483d4b309b8e3b7aca6a3c')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
