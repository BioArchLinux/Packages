# system requirements: C++11
# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=BiocParallel
_pkgver=1.32.0
pkgname=r-${_pkgname,,}
pkgver=1.32.0
pkgrel=1
pkgdesc='Bioconductor facilities for parallel evaluation'
arch=('x86_64')
url="https://bioconductor.org/packages/${_pkgname}"
license=('GPL')
depends=(
  r
  r-bh
  r-futile.logger
  r-snow
  r-cpp11
)
optdepends=(
  r-batchjobs
  r-batchtools
  r-bbmisc
  r-biocgenerics
  r-biocstyle
  r-codetools
  r-data.table
  r-doparallel
  r-foreach
  r-genomicalignments
  r-genomicranges
  r-knitr
  r-rmpi
  r-rnaseqdata.hnrnpc.bam.chr14
  r-rsamtools
  r-runit
  r-shortread
  r-tools
  r-txdb.hsapiens.ucsc.hg19.knowngene
  r-variantannotation
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('b3375066f6ad68229cd14f8df020af6fb698efc25b432c3f2e32b45bd783108a')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
}
# vim:set ts=2 sw=2 et:
