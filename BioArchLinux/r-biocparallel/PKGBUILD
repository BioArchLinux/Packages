# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=BiocParallel
_pkgver=1.36.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Bioconductor facilities for parallel evaluation"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=(GPL)
depends=(
  r-futile.logger
  r-snow
)
makedepends=(
  r-bh
  r-cpp11
)
optdepends=(
  r-batchtools
  r-bbmisc
  r-biocgenerics
  r-biocstyle
  r-data.table
  r-doparallel
  r-foreach
  r-genomicalignments
  r-genomicranges
  r-knitr
  r-rnaseqdata.hnrnpc.bam.chr14
  r-rsamtools
  r-runit
  r-shortread
  r-txdb.hsapiens.ucsc.hg19.knowngene
  r-variantannotation
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('a6a331ac453920e713836ddd0de38e25')
sha256sums=('cbf8af552c7c30c26d8d17876245dad583f712e49e7336ef78aeb70f1c0608a6')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
