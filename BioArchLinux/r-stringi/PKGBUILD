# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>
# Contributor: Alex Branham <branham@utexas.edu>

_pkgname=stringi
_pkgver=1.8.7
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=3
pkgdesc="Fast and Portable Character String Processing Facilities"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('BSD-3-Clause AND GPL-2.0-or-later')
depends=(
  icu
  r
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('e8d621e4b35873cd406271d3fe8c3281')
b2sums=('b798c4589e47dd6d768ff34f0dc57388ad40002589c57f4547995b2a4970d41c035532673c0a17d92740547111be1976707e8e1b69e6a5a74cb9bc76f8172f31')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname" \
      --configure-args=--disable-icu-bundle
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
