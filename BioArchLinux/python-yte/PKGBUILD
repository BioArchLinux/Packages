# Maintainer: Ivan Batrakov <blackfan321 at disroot dot org>
# Contributor: Philipp A. <flying-sheep@web.de>

_name=yte
pkgname=python-$_name
pkgver=1.9.4
pkgrel=1
pkgdesc='YAML template engine with Python expressions'
arch=(any)
url="https://github.com/koesterlab/$_name"
license=(MIT)
depends=(python-dpath python-plac python-pyyaml)
makedepends=(python-poetry-core python-build python-installer python-wheel python-uv-build)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('86a47e6d722cec9419a7ac88be57d0d6c4ce28f02860393b71a66f2c674069f6')

build() {
	cd "$_name-$pkgver"
	python -m build --wheel --no-isolation
}

package() {
	cd "$_name-$pkgver"
	python -m installer --destdir="$pkgdir" dist/*.whl
}
