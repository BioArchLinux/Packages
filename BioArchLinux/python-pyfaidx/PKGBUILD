# Maintainer: Charles Vejnar <first name [dot] last name [at] gmail [dot] com>
# import from AUR: bipin kumar <kbipinkumar@pm.me>

pkgname=python-pyfaidx
_name=pyfaidx
pkgver=0.8.1.3
pkgrel=4
pkgdesc="Efficient pythonic random access to fasta subsequences."
arch=("any")
url="https://pypi.python.org/pypi/pyfaidx"
license=("BSD-3-Clause")
depends=(
         'python'
         'python-setuptools'
         'python-setuptools-scm'
         'python-six'
         'python-pyvcf3'
        )
makedepends=('python-build'
             'python-installer'
             'python-wheel'
             'python-pytest'
             'python-pytest-cov'
             'cython'
             'python-numpy'
             'python-biopython'
             'python-requests'
             'python-coverage'
             'python-fsspec'
             'git')

source=("${_name}::git+https://github.com/mdshw5/pyfaidx.git#tag=v${pkgver}")

sha256sums=('7b3d419980574327456c47fa401160a3025c06a1d1bfc34666d1fcb2c1f78d97')

prepare() {
    git -C "${srcdir}/${_name}" clean -dfx
    touch "${srcdir}/${_name}"/tests/__init__.py
    echo "downloading test nucleotide data from NCBI"
    python "${srcdir}/${_name}"/tests/data/download_gene_fasta.py

}

build() {
    cd "$_name"
    export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"
    python -m build --wheel --no-isolation
}

check(){
    cd $_name

    local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$python_version" pytest
}
package() {
    cd "$_name"
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
