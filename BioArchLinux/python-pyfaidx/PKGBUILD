# Maintainer: Charles Vejnar <first name [dot] last name [at] gmail [dot] com>
# import from AUR: bipin kumar <kbipinkumar@pm.me>

pkgname=python-pyfaidx
_name=pyfaidx
pkgver=0.8.1.3
pkgrel=1
pkgdesc="Efficient pythonic random access to fasta subsequences."
arch=("any")
url="https://pypi.python.org/pypi/pyfaidx"
license=("BSD")
depends=(
         'python'
         'python-setuptools'
         'python-setuptools-scm'
         'python-six'
         'python-pyvcf3'
        )
makedepends=('python-build'
             'python-installer'
             'python-wheel')
source=("$pkgname-$pkgver.tar.gz::https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('feb599f8dfd192cf68bdb24969a5fd8f46772a7bd75d059e764bfd86b888e9b2')

build() {
    cd "$_name-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "$_name-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
