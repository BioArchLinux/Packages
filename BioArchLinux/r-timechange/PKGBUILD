# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=timechange
_pkgver=0.2.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=3
pkgdesc="Efficient Manipulation of Date-Times"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-3.0-only')
depends=(
  cctz
  r
)
makedepends=(
  r-cpp11
)
checkdepends=(
  r-testthat
)
optdepends=(
  r-knitr
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz"
        "system-cctz.patch")
md5sums=('9e8dea7a23e233cccec70f7838381582'
         'a573af9ccae2bad5627178a50de35e5b')
b2sums=('02501ac599f480549a3b2b351f82a04937ece87832ae39258f02ae980b6c115c7d79efa4f21739c580aa736c2dc7279ce1cf123b56193f20a894226f2b1123d9'
        'a49706ea47fa79a44d09ee646e27d4b03fb820346b3c80b43e85b43c59c75457f24fe748d95f4ae4f7ab6433d94cec1a9f94a8520fa2179af35e8e9a50b7c2df')

prepare() {
  # build against system cctz
  patch -Np1 -i system-cctz.patch
}

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
