# Maintainer: Shun Wang <shuonwang at gmail dot com>
# Contributor: Shun Wang <shuonwang at gmail dot com>

_pkgname=berryFunctions
_pkgver=1.22.5
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Function Collection Related to Plotting and Hydrology"
arch=('any')
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-2.0-or-later')
depends=(
  r-abind
)
optdepends=(
  r-rcolorbrewer
  r-pbapply
  r-knitr
  r-rmarkdown
  r-gstat
  r-rcurl
  r-colorspace
  r-vioplot
  r-spatstat.geom
  r-ade4
  r-nortest
  r-rstudioapi
  r-leaflet
  r-leaflet.extras
  r-zoo
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('92ba44152b782881848df66f56e86396')
b2sums=('a6149ee84eddcc0f1dfc23dfeb5cda154405bd2ede8e6ad04bf223befd4f99ea897ef70f5169747a9e5927d26c55577463655d508e99d9d65a5c4a810474f29f')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENCE" "$pkgdir/usr/share/licenses/$pkgname"
}
