# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>

_pkgname=sf
_pkgver=1.0-15
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=0
pkgdesc="Simple Features for R"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=(GPL2)
depends=(
  gdal
  geos
  proj
  r-classint
  r-dbi
  r-magrittr
  r-rcpp
  r-s2
  r-units
)
checkdepends=(
  postgis
  r-blob
  r-dplyr
  r-raster
  r-rpostgres
  r-rpostgresql
  r-sp
  r-testthat
  r-tidyr
)
optdepends=(
  r-blob
  r-covr
  r-dplyr
  r-ggplot2
  r-knitr
  r-lwgeom
  r-maps
  r-mapview
  r-microbenchmark
  r-nanoarrow
  r-odbc
  r-pbapply
  r-pillar
  r-pool
  r-raster
  r-rlang
  r-rmarkdown
  r-rpostgres
  r-rpostgresql
  r-rsqlite
  r-sp
  r-spatstat
  r-spatstat.geom
  r-spatstat.linnet
  r-spatstat.random
  r-spatstat.utils
  r-stars
  r-terra
  r-testthat
  r-tibble
  r-tidyr
  r-tidyselect
  r-tmap
  r-vctrs
  r-wk
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('b3c5d978da86e8da20f577b65c4eaaff')
b2sums=('e310735fe1e746f2a47d363c486f1a05a2479dbdd0cc68e351165016ddbce108b6ce70a456a19c98162a53eed1c2f6630abef1c46323d302b083d5cd22209842')

prepare() {
  # use PGHOST variable in tests
  sed -e 's/host = "localhost",//' \
      -e 's/host=localhost //' \
      -i "$_pkgname/tests/testthat/test_postgis_RPostgres.R"
}

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"

  # create database for tests
  export PGDATA=db
  export PGHOST="$(pwd)/host"
  mkdir host
  initdb
  pg_ctl -o "-h '' -k ${PGHOST@Q}" start
  createdb postgis
  psql postgis -c "CREATE EXTENSION postgis;"
  createdb empty

  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R

  # shut down test database
  pg_ctl stop
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
