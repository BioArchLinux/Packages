# system requirements: GNU make, libcurl
# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=RCurl
_pkgver=1.98-1.13
pkgname=r-${_pkgname,,}
pkgver=1.98.1.13
pkgrel=1
pkgdesc='General Network (HTTP/FTP/...) Client Interface for R'
arch=('x86_64')
url="https://cran.r-project.org/package=${_pkgname}"
license=('BSD')
depends=(
  r
  r-bitops
)
optdepends=(
  r-xml
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('05a52f6329b4ea91dc92ae23b2528b99d654532c8291712bd591521d6eacf306')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
  install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
