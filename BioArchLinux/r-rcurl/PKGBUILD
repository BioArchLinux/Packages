# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=RCurl
_pkgver=1.98-1.17
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="General Network (HTTP/FTP/...) Client Interface for R"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('BSD-3-Clause')
depends=(
  curl
  r-bitops
)
optdepends=(
  r-xml
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('19402758e52be28de7caeb2ac4df25da')
b2sums=('c000a00de93ec1f726e4ba854fe5b166d00b20499c0f481152a24cf1e4c76190ab897c0c09bdd83298ca16e06eab2f7d9087724475ea9e137c69e22763d3d910')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
