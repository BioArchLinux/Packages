# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=RCurl
_pkgver=1.98-1.16
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="General Network (HTTP/FTP/...) Client Interface for R"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('BSD-3-Clause')
depends=(
  curl
  r-bitops
)
optdepends=(
  r-xml
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('3727edecf0189d460661535b6e7b1a46')
b2sums=('9c49e78be4ee2a3e20bddd77409a1c966e9b36d7b78f093b1794d1cd86289c8d0a2be60d669327a730af98397d7f49fed747d4e6ebc1492ff146c5f2d96475d7')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
