# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
pkgname=wf-shell
pkgver=0.8.0.20240313.132658
pkgrel=1
pkgdesc='GTK3-based panel for the Wayfire compositor'
url=https://wayfire.org
arch=(x86_64)
license=(custom:MIT)
conflicts=("${pkgname}-git")
depends=(gtk-layer-shell wayfire alsa-lib libpulse gtkmm3 libdbusmenu-gtk3)
makedepends=(wayland wayland-protocols cmake meson ninja gobject-introspection glm git)
source=("git+https://github.com/WayfireWM/wf-shell.git")
sha256sums=('SKIP')

pkgver () {
    cd "$srcdir/$pkgname"
    printf "$(cat meson.build | grep '^\sversion:' | sed "s/'/ /g" | awk '{print $2}').%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)"
}


build () {
	rm -rf build
	arch-meson "${pkgname}" build -Dpulse=enabled
	ninja -C build
}

check () {
	meson test -C build
}

package () {
	DESTDIR="${pkgdir}" ninja -C build install
	cd "${pkgname}"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 wf-shell.ini.example "${pkgdir}/usr/share/doc/${pkgname}/wf-shell.ini.example"
}
