# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=transformr
_pkgver=0.1.4
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=3
pkgdesc="Polygon and Path Transformations"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-lpsolve
  r-rlang
  r-sf
  r-tweenr
  r-vctrs
)
makedepends=(
  r-cpp11
)
optdepends=(
  r-covr
  r-magrittr
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('94b6d03d1bd089fa3dc47de6dac69d90')
b2sums=('eee23bc79d3dbbb92bf02f596d80ee993b36475ff720e9bd271b61e72611e787a7b8cfdac80384cf610b110e970d5d681d2989212d97ede60eeaca753067aab9')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
