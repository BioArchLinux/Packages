# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: peippo <christoph+aur@christophfink.com>

_pkgname=pkgcache
_pkgver=2.2.2
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Cache 'CRAN'-Like Metadata and R Packages"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-callr
  r-cli
  r-curl
  r-filelock
  r-jsonlite
  r-processx
  r-r6
)
checkdepends=(
  r-mockery
  r-spelling
  r-testthat
  r-webfakes
)
optdepends=(
  r-covr
  r-debugme
  r-desc
  r-fs
  r-mockery
  r-pillar
  r-pingr
  r-rprojroot
  r-sessioninfo
  r-spelling
  r-testthat
  r-webfakes
  r-withr
  r-zip
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('032a72ebbc7709b292461d40dfccb16b')
b2sums=('67edee48aa204c985398efad2a4c87e0c8f6266d695e8df176a7d95c3404bf6e66b8a88522ccc17ce30cbe7046a8f6283f814ecfe89955ed3876a3201ca00860')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
