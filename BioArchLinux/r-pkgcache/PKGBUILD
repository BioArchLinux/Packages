# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: peippo <christoph+aur@christophfink.com>

_pkgname=pkgcache
_pkgver=2.2.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Cache 'CRAN'-Like Metadata and R Packages"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-callr
  r-cli
  r-curl
  r-filelock
  r-jsonlite
  r-processx
  r-r6
)
checkdepends=(
  r-mockery
  r-spelling
  r-testthat
  r-webfakes
)
optdepends=(
  r-covr
  r-debugme
  r-desc
  r-fs
  r-mockery
  r-pillar
  r-pingr
  r-rprojroot
  r-sessioninfo
  r-spelling
  r-testthat
  r-webfakes
  r-withr
  r-zip
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('f8769843c1df6314db2241aa01ee7224')
b2sums=('cec310f9b8675482560fc10671f3e8580bc613ad410ffea4dfe8092e65ac356da57cc7e88bac96c175871768c01fa7e4241cb2aec26d39750800bcc1d21ad577')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
