# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=bamtools
pkgver=2.5.3
pkgrel=1
pkgdesc="C++ API & command-line toolkit for working with BAM data"
arch=('x86_64' 'i686')
url="https://github.com/pezmaster31/bamtools"
license=('custom')
depends=('gcc-libs' 'zlib' 'jsoncpp')
makedepends=('cmake')
source=($pkgname-$pkgver.tar.gz::https://github.com/pezmaster31/bamtools/archive/v$pkgver.tar.gz)
sha256sums=('7d4e59bac7c03bde488fe43e533692f78b5325a097328785ec31373ffac08344')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  mkdir -p build
}

build() {
  cd $srcdir/$pkgname-$pkgver/build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    ..

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/build

  make DESTDIR=$pkgdir install

  install -Dm644 ../LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
