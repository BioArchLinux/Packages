# Maintainer: Roy Oursler <roy.j.oursler@intel.com>
pkgname=isa-l
pkgver=2.31.1
pkgrel=2
pkgdesc="A collection of optimized low-level functions targeting storage applications"
arch=(x86_64)
url="https://github.com/intel/$pkgname"
license=('BSD')
makedepends=('nasm' 'patchelf')
source=("$url/archive/v$pkgver.tar.gz")
sha1sums=('6ff9da63837cab5233925b63461d85c91c3c66f8')

build() {
    cd "${srcdir}/$pkgname-$pkgver"
    ./autogen.sh
    ./configure --prefix="/usr" -q

    make
}

check() {
    cd "${srcdir}/$pkgname-$pkgver"
    make DESTDIR=${pkgdir} check
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"
    make DESTDIR=${pkgdir} install
    mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    patchelf --remove-rpath $pkgdir/usr/bin/igzip
}
