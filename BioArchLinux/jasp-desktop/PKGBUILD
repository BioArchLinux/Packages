#Maintainer: sukanka <su975853527 AT gmail.com>

_pkgname=jasp
_pkgver=0.95.1
pkgname=jasp-desktop
pkgver=0.95.1
_ColumnEncoder_ver=0.95.1
pkgrel=1
pkgdesc="A complete statistical package for both Bayesian and Frequentist statistical methods"
arch=('x86_64' 'aarch64')
url="https://github.com/jasp-stats/jasp-desktop"
license=('AGPL-3.0-or-later')
makedepends=("cmake" 'boost' 'jsoncpp'
    'openssl'
    'autoconf'
    'zlib'
    'bison'
    'flex'
    'jags'
    'gcc-fortran'
    'qtcreator'
    'git'
    'patchelf'
    'ninja'
    'libfreexl'
    'vulkan-headers'
)
depends=('r'
    'qt6-5compat'
    'readstat'
    'libarchive'
    'r-rinside'
    'qt6-base'
    'qt6-webengine'
    'qt6-shadertools'
    librdata
    libfreexl
)
provides=($_pkgname)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jasp-stats/jasp-desktop/archive/refs/tags/v${pkgver}.tar.gz"
    'jasp.sh'
    "jaspColumnEncoder-${pkgver}.tar.gz::https://github.com/jasp-stats/jaspColumnEncoder/archive/32c53153da95087feb109c0f5f69534ffa3f32b7.zip"
    "jaspBase-${pkgver}.tar.gz::https://github.com/jasp-stats/jaspBase/archive/0f0f59e18f5524ea422cb3dc5ad76ace6f0518e4.zip"
    # "jaspGraphs-${pkgver}.tar.gz::https://github.com/jasp-stats/jaspGraphs/archive/refs/tags/v${pkgver}.tar.gz"
    "jaspModuleBundleManager-${pkgver}.tar.gz::https://github.com/jasp-stats/jaspModuleBundleManager/archive/466057fe2ca0602917be964ce032c4cb03a8619a.zip"
    "remove_binary_pkgs.patch::https://github.com/jasp-stats/jasp-desktop/commit/3ff41e292c.patch"
)
sha256sums=('7c0e0fdd6a28ddd484ccd39069da6cb20e03547d2d8ec14b69df5f05cabfa0b2'
    'e0714d980e7549b4c7dcbae50370e95b6ad2e7f0cf21a534ceb3a5a83ee583fd'
    '8afdd494d59bc6812fb4d957070d7a92eb21eceecc954636abb264a37e1bcf86'
    '6a6ed34ed5f2514fff7e9568c045ccf530586ed37ce08e409729c52ba1336c47'
    'c6e58ca68cc157b7ba56ed6ac83ad1340be5c9f2148c80fb9713562e2dbe6e3f'
    'f5c145c33cdcb4c254f7e9f0eb7639e8cdf6887e56e2f7c15074973504469889')

_add_libs() {
    local JASP_LIBS lib
    JASP_LIBS=(
        # base
        "jaspBase"
        "jaspGraphs"
        "jaspTools"
        # common
        "jaspDescriptives"
        "jaspTTests"
        "jaspAnova"
        "jaspMixedModels"
        "jaspRegression"
        "jaspFrequencies"
        "jaspFactor"
        # extra
        "jaspAcceptanceSampling"
        "jaspAudit"
        "jaspBain"
        "jaspBFF"
        "jaspBfpack"
        "jaspBsts"
        "jaspCircular"
        "jaspCochrane"
        "jaspDistributions"
        "jaspEquivalenceTTests"
        # "jaspEsci"
        "jaspJags"
        "jaspLearnBayes"
        "jaspLearnStats"
        "jaspMachineLearning"
        "jaspMetaAnalysis"
        "jaspNetwork"
        "jaspPower"
        "jaspPredictiveAnalytics"
        "jaspProcess"
        "jaspProphet"
        "jaspQualityControl"
        "jaspReliability"
        "jaspRobustTTests"
        "jaspSem"
        "jaspSurvival"
        "jaspSummaryStatistics"
        "jaspTimeSeries"
        "jaspVisualModeling"
        # "jaspTestModule"
    )
    install -d ${pkgdir}/usr/lib/jasp-desktop/Modules/module_libs/
    for lib in "${JASP_LIBS[@]}"; do
        depends+=("r-${lib,,}")
        ln -s /usr/lib/R/library ${pkgdir}/usr/lib/jasp-desktop/Modules/module_libs/${lib}
    done
}
_apply_patch() {
    pushd $srcdir/${pkgname}-${pkgver}
    patch -p1 <$srcdir/remove_binary_pkgs.patch
    popd

}
prepare() {
    for d in jaspBase jaspModuleBundleManager; do
        cp -ar ${d}-*/* ${pkgname}-${pkgver}/Engine/${d}
    done
    cp -ar jaspColumnEncoder-*/* ${pkgname}-${pkgver}/Common/jaspColumnEncoder
    cd $srcdir/${pkgname}-${pkgver}
    _apply_patch

    find Tools/CMake -name *.cmake -print0 | xargs -0 sed -i "s|/usr/local|/usr|g"
    sed -i "s|lib='\${R_LIBRARY_PATH}'|lib='${srcdir}/usr/lib/R'|g" Tools/CMake/R.cmake
    sed -i 's|set(R_CPP_INCLUDES_LIBRARY.*|set(R_CPP_INCLUDES_LIBRARY /usr/lib/R/library)|g' Tools/CMake/R.cmake R-Interface/CMakeLists.txt

    # Do NOT install modules here, they are listed in dependencies
    # find Modules/ -name '*.in' -print0 | xargs -0 sed -i '1,$d;1a print("I am OK!")'
}

build() {
    local cmake_args=(
        -GNinja
        -DCMAKE_BUILD_TYPE=None
        -DCMAKE_INSTALL_PREFIX=/usr/lib/${pkgname}
        -DCMAKE_INSTALL_LIBDIR=lib
        # -DCUSTOM_R_PATH=/usr/lib/R
        -DLINUX_LOCAL_BUILD=OFF
        -DFLATPAK_USED=OFF
        -DINSTALL_R_MODULES=OFF
        -DBUILD_TESTS=OFF
        -DGITHUB_PAT=None

    )
    # export GITHUB_PAT="None"
    install -d build/Modules/Tools/ ${srcdir}/usr/lib/R
    cmake -S ${pkgname}-${pkgver} -B build "${cmake_args[@]}"
    ninja -C build
}

package() {
    cd $srcdir/build
    DESTDIR=${pkgdir} ninja install
    install -Dm755 $srcdir/jasp.sh ${pkgdir}/usr/bin/jasp

    cd ${pkgdir}/usr/lib/${pkgname}
    mv share ${pkgdir}/usr
    mv Resources ${pkgdir}/usr/share/${pkgname}
    ln -s /usr/share/${pkgname} ${pkgdir}/usr/lib/${pkgname}/Resources

    rm -rf lib64
    rm -rf Modules/{renv-cache,*.log}

    # fix RPATH
    patchelf --add-rpath /usr/lib/R/library/RInside/lib/ \
        ${pkgdir}/usr/lib/jasp-desktop/bin/JASPEngine
    sed -i "s|^Exec.*|Exec=jasp %f|g" \
        ${pkgdir}/usr/share/applications/org.jaspstats.JASP.desktop

    _add_libs
    rm -rf ${pkgdir}/usr/lib/jasp-desktop/{renv-root,renv-cache,bin/org.jaspstats.JASP}
}
