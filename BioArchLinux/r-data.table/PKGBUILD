# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=data.table
_pkgver=1.18.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Extension of \`data.frame\`"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MPL-2.0')
depends=(
  r
  zlib
)
optdepends=(
  r-bit
  r-bit64
  r-knitr
  r-markdown
  r-r.utils
  r-xts
  r-yaml
  r-zoo
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('251bf3bdab82168bb677bde9ae261dc8')
b2sums=('dfcba124ef365da7f1edb74fe25a09c79ae5419b5d67198a966f08a54ae85f22f24d85c7f255ce12cecb04edbcd2333e66ea2db5f75f099605dc56357a79933c')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
