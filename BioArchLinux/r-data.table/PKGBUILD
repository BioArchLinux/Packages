# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=data.table
_pkgver=1.17.2
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Extension of \`data.frame\`"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MPL-2.0')
depends=(
  r
  zlib
)
optdepends=(
  r-bit
  r-bit64
  r-knitr
  r-markdown
  r-r.utils
  r-xts
  r-yaml
  r-zoo
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('890bec3b2297c9854f02c8e0d732cf06')
b2sums=('a4888f910341fa4e6e351d284bc6bcc03954bde6892e679b7ed5b220ebe356d969dbd2d6087d4c01921d83f4ddd07e717d71d504ce7be239af0eb80dbe34904e')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
