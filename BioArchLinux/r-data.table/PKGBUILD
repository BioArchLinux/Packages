# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=data.table
_pkgver=1.17.6
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Extension of \`data.frame\`"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MPL-2.0')
depends=(
  r
  zlib
)
optdepends=(
  r-bit
  r-bit64
  r-knitr
  r-markdown
  r-r.utils
  r-xts
  r-yaml
  r-zoo
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('25192346365e9383dd862bd8af287251')
b2sums=('cf566c1ad69a27db7ec9f6d7ee749da68123e1599148afbded7fac8f28ee8e77b3dfd2d8f89ef11afd0fc5d402127f626edc438cd6774ad19f23011c284a3658')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
