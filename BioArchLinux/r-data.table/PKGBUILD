# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=data.table
_pkgver=1.15.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=0
pkgdesc="Extension of \`data.frame\`"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MPL-2.0')
depends=(
  r
  zlib
)
optdepends=(
  r-bit
  r-bit64
  r-knitr
  r-markdown
  r-r.utils
  r-xts
  r-yaml
  r-zoo
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('45e341f35317c78b122317d1315a0240')
b2sums=('037a6551b32506fd0c74478a877a8843a89c920528d1b292c635d6636a9d56ee452d0ca0e8f1a64afc7055c226a54e5b415856d26492ee551e328cf204525ce2')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
