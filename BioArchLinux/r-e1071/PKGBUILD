# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=e1071
_pkgver=1.7-16
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Misc Functions of the Department of Statistics, Probability Theory Group (Formerly: E1071), TU Wien"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-2.0-only OR GPL-3.0-only')
depends=(
  r-proxy
)
optdepends=(
  r-mlbench
  r-randomforest
  r-slam
  r-sparsem
  r-xtable
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('c7bbeba044310808a8aaceddf3080742')
b2sums=('72856b0317d4f6d168be46970a3dc3aff4146e21fae648d72a1683393ffdab6a013a9604bb71376532b91a29b08a819c1e69e13a73df73f8148c31806b5e4de7')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
