# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=e1071
_pkgver=1.7-13
pkgname=r-${_pkgname,,}
pkgver=1.7.13
pkgrel=1
pkgdesc='Misc Functions of the Department of Statistics, Probability Theory Group (Formerly: E1071), TU Wien'
arch=('x86_64')
url="https://cran.r-project.org/package=${_pkgname}"
license=('GPL')
depends=(
  r
  r-proxy
)
optdepends=(
  r-cluster
  r-mass
  r-matrix
  r-mlbench
  r-nnet
  r-randomforest
  r-rpart
  r-slam
  r-sparsem
  r-xtable
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('da94e191af6e69aa0f9e3250d4b823674cc869339d914f761ebf2824177b6b2f')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
}
# vim:set ts=2 sw=2 et:
