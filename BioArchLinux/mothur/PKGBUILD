# import from AUR: Bipin Kumar <kbipinkumar@pm.me>
# Maintainer:  Andrew O'Neill <andrew at meanjollies dot com>
# Contributor: Christian Krause ("wookietreiber") <christian.krause@mailbox.org>

pkgname=mothur
pkgver=1.48.1
pkgrel=1
pkgdesc='A bioinformatics program for analyzing microbial communities.'
arch=('x86_64')
url='https://www.mothur.org/'
license=('GPL3')
provides=('uchime')
depends=('readline')
makedepends=('boost' 'hdf5' 'gsl' 'quilt' 'boost-libs')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz"
        "0002-Fix-include.patch"
        "c++14.patch"
        "add-hardening-opts.patch"
        "drop_sys_sysctl.h.patch"
        "fix_linking.patch"
        "gcc-11.patch"
        "series"
        )
sha256sums=('03a26d12719b8658051b0c2aefe791017d9bdcc6d50ba539f85ed232daafc0ac'
            'e29761b0e466da9b16ea06057b0443d93b5364a0f61653af922fbd89c6ae53bd'
            'eef923efb4102f8c252eb9fbc9829bcdc9d40827d38f5019682132c7b5a87f72'
            '24a355ff52029c0f86e8a410f0a8a94e68a9b282e14b35e61081c181cddb211e'
            '8c24a2a1bac5f592e3058e545ce26a5f0b2df7a06f4e1818922c43613bea4673'
            '1ef6216196a0b60624a4a856f76c0f770a66673f22dd4ee4e44e0eca1585e015'
            '845d293a02f84d30c06722729fd306f94c7dee0e85f3540bb1332bcacab169fb'
            '1727c2c5e79d41964c6790b10467f25c696d9649438d09746a24827745a6934f')

prepare() {
  mkdir -p ${pkgname}-${pkgver}/patches
  cp *.patch ${pkgname}-${pkgver}/patches
  cp series ${pkgname}-${pkgver}/patches
  cd "${pkgname}-${pkgver}"
  quilt push -a
}

build() {
  cd "${pkgname}-${pkgver}"

  make \
    DUSEBOOST=yes \
    DUSEHDF5=yes \
    DUSEGSL=yes \
    DBOOST_LIBRARY_DIR=/usr/lib \
    DBOOST_INCLUDE_DIR=/usr/include \
    DHDF5_LIBRARY_DIR=/usr/lib \
    DHDF5_INCLUDE_DIR=/usr/include \
    DGSL_LIBRARY_DIR=/usr/lib \
    DGSL_INCLUDE_DIR=/usr/include
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 mothur "${pkgdir}/usr/bin/mothur"
  install -Dm755 uchime "${pkgdir}/usr/bin/uchime"
}
