# import from AUR: Bipin Kumar <kbipinkumar@pm.me>
# Maintainer:  Andrew O'Neill <andrew at meanjollies dot com>
# Contributor: Christian Krause ("wookietreiber") <christian.krause@mailbox.org>

pkgname=mothur
pkgver=1.48.5
pkgrel=0
pkgdesc='A bioinformatics program for analyzing microbial communities.'
arch=('x86_64')
url='https://www.mothur.org/'
license=('GPL-3.0-only')
provides=('uchime')
depends=('readline' 'glibc' 'gcc-libs' 'boost-libs' 'hdf5' 'gsl')
makedepends=('boost')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz"
        "boost-gcc-fixes.patch"
        )
sha256sums=('d6bbd172cefdfe468d654532e620831e5e9a6814c751361034027aeb1cbccd27'
            'ca8c76f397cd238702798a95b3cc6e286b8218c0b85ecf0989f568bb260b4bc8')

prepare() {
  # Use system Boost/HDF5/GSL; fix Makefile for modern GCC
  cd "${pkgname}-${pkgver}"
  
  # Fix line endings
  sed -i 's/\r$//' source/uchime_src/myutils.cpp

  patch -p1 < ${srcdir}/boost-gcc-fixes.patch
}

build() {
  cd "${pkgname}-${pkgver}"

  make USEBOOST=yes USEHDF5=yes USEGSL=yes
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 mothur "${pkgdir}/usr/bin/mothur"
  install -Dm755 uchime "${pkgdir}/usr/bin/uchime"
}
