# import from AUR: Bipin Kumar <kbipinkumar@pm.me>
# Maintainer:  Andrew O'Neill <andrew at meanjollies dot com>
# Contributor: Christian Krause ("wookietreiber") <christian.krause@mailbox.org>

pkgname=mothur
pkgver=1.48.4
pkgrel=1
pkgdesc='A bioinformatics program for analyzing microbial communities.'
arch=('x86_64')
url='https://www.mothur.org/'
license=('GPL-3.0-only')
provides=('uchime')
depends=('readline' 'glibc' 'gcc-libs' 'boost-libs' 'hdf5' 'gsl')
makedepends=('boost')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz"
        "boost-gcc-fixes.patch"
        )
sha256sums=('aa0de1133fa554f91f861ba01bc63212236d66a7b414b7edea5729987350eca2'
            '88237da25ddc2bdb867fcd30523f94e63cfd4e1b194e50c3f094126dbc8f43fe')

prepare() {
  # Use system Boost/HDF5/GSL; fix Makefile for modern GCC
  cd "${pkgname}-${pkgver}"
  patch -p1 < ${srcdir}/boost-gcc-fixes.patch
}

build() {
  cd "${pkgname}-${pkgver}"

  make USEBOOST=yes USEHDF5=yes USEGSL=yes
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 mothur "${pkgdir}/usr/bin/mothur"
  install -Dm755 uchime "${pkgdir}/usr/bin/uchime"
}
