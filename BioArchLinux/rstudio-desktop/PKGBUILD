# Maintainer: Artem Klevtsov <a.a.klevtso@gmail.com>
# Maintainer: Stephen Martin <hwkiller@gmail.com>
# Contributor: Conor Anderson <conor@conr.ca>

pkgname=rstudio-desktop
pkgver=2024.04.2+764
_pkgver=${pkgver//+/.}
_srcname=rstudio-${pkgver//+/-}
_pandocver="current"
_quarto="FALSE"
_sociver="4.0.3"
pkgrel=2
pkgdesc="A powerful and productive integrated development environment \
	 (IDE) for R programming language"
arch=('x86_64')
url="https://posit.co/products/open-source/rstudio/"
license=('AGPL-3.0-only')
depends=('r>=3.3.0' 'boost-libs' 'qt5-sensors' 'qt5-svg' 'qt5-webengine' 'qt5-xmlpatterns' 'postgresql-libs' 'sqlite3' 'clang' 'hunspell-en_US' 'mathjax2' 'pandoc' 'yaml-cpp' 'r-rmarkdown' 'r-renv' 'r-testthat' 'r-xml2' 'r-yaml' 'r-purrr' 'r-digest')
makedepends=('git' 'cmake>=3.29' 'boost' 'ninja' 'desktop-file-utils' 'jdk8-openjdk' 'apache-ant' 'unzip' 'openssl' 'libcups' 'pam' 'patchelf' 'wget' 'yarn')
optdepends=('git: for git support'
            'subversion: for subversion support'
            'openssh-askpass: for a git ssh access'
        )

source=("rstudio-$_pkgver.tar.gz::https://github.com/rstudio/rstudio/archive/refs/tags/v${pkgver}.tar.gz"
        "git+https://github.com/quarto-dev/quarto.git#branch=release/rstudio-cherry-blossom"
        "https://github.com/SOCI/soci/archive/refs/tags/v${_sociver}.tar.gz"
        "qt.conf"
        "0002-allow_system_node.patch"
        "0003-fix_boost_186.patch")

sha256sums=('89b0d6741b6bd4dac52fd8bd9372d4f1919c97dd41056a03c975d0570eaf439e'
            'SKIP'
            '4b1ff9c8545c5d802fbe06ee6cd2886630e5c03bf740e269bb625b45cf934928'
            '723626bfe05dafa545e135e8e61a482df111f488583fef155301acc5ecbbf921'
            'ad4bd3076ff2bff7d075e3928a7e55c618fa744e3bf5d3d387bf70e01ff96c2f'
            'a30066b6aeaae1ad0ec2dcc561eb3043b91a431570002a5b9535306864cc2ae3')
options=('!emptydirs' '!debug')

prepare() {
    cd ${srcdir}/${_srcname}
    msg "Do not use outdated version name of pandoc"
    sed -i '/PANDOC_VERSION/s/2.18/current/' "${srcdir}/${_srcname}/CMakeGlobals.txt"

    msg "Suppress _FORTIFY_SOURCE mismatch warnings"
    sed -i 's/D_FORTIFY_SOURCE=2/D_FORTIFY_SOURCE=3/' "${srcdir}/${_srcname}/src/cpp/CMakeLists.txt"

    # Add option to use system node
    patch -p3 < ${srcdir}/0002-allow_system_node.patch

    # fix boost 1.86 incompatibility
    patch -p1 < ${srcdir}/0003-fix_boost_186.patch

    cd "${srcdir}/${_srcname}/dependencies/common"
    install -d pandoc/${_pandocver}

    ln -sfT /usr/share/myspell/dicts dictionaries
    ln -sfT /usr/share/mathjax2 mathjax-27
    ln -sfT /usr/bin/pandoc pandoc/${_pandocver}/pandoc

    # Fix links for src/cpp/session/CMakeLists.txt
    cd "${srcdir}/${_srcname}/dependencies"
    ln -sfT /usr/share/myspell/dicts dictionaries
    ln -sfT /usr/share/mathjax2 mathjax-27

    # Bundled SOCI libs
    cp -r "${srcdir}/soci-${_sociver}" "soci-${_sociver}"

    # Panmirror is picked up from Quarto repo
    ln -sfT "${srcdir}/quarto" "${srcdir}/${_srcname}/src/gwt/lib/quarto"
}

build() {

    cd "${srcdir}/${_srcname}/dependencies"
        install -d pandoc/${_pandocver}/bin/tools
        ln -sfT /usr/bin/pandoc pandoc/${_pandocver}/bin/tools/pandoc

    cd "${srcdir}/${_srcname}/dependencies/soci-${_sociver}"
    msg "Building SOCI libs..."

    local _CMAKE_SOCI_OPTIONS=(
      -DCMAKE_POLICY_DEFAULT_CMP0063="NEW"
      -DCMAKE_POLICY_DEFAULT_CMP0074="NEW"
      -DCMAKE_POSITION_INDEPENDENT_CODE:BOOL=true
      -DCMAKE_CXX_VISIBILITY_PRESET="$COMPILE_VISIBILITY"
      -DSOCI_TESTS=OFF
      -DSOCI_CXX11=ON
      -DSOCI_EMPTY=OFF
      -DWITH_BOOST=ON
      -DWITH_POSTGRESQL=ON
      -DWITH_SQLITE3=ON
      -DWITH_DB2=OFF
      -DWITH_MYSQL=OFF
      -DWITH_ORACLE=OFF
      -DWITH_FIREBIRD=OFF
      -DWITH_ODBC=OFF
    )

    cmake -G Ninja -S "${srcdir}/${_srcname}/dependencies/soci-${_sociver}" -B build "${_CMAKE_SOCI_OPTIONS[@]}"
    cmake --build build --target all

    export LDFLAGS="${LDFLAGS} -L${srcdir}/${_srcname}/dependencies/soci-${_sociver}/build/lib/"

    cd ${srcdir}
    # Use path of system provided R packages
    export R_LIBS_USER=/usr/lib/R/library/
    export RSTUDIO_TOOLS_ROOT="${srcdir}/${_srcname}/dependencies"

    export PATH=/usr/lib/jvm/java-8-openjdk/jre/bin/:${PATH}
    # These variables are used to create contents of Help > About dialog
    export osarch=$(uname -m)
    export distro=$(grep -E '^NAME=' /etc/os-release | cut -d= -f2- | tr -d '"')
    export RSTUDIO_VERSION_MAJOR=$(echo "$_pkgver" | cut -d "." -f1)
    export RSTUDIO_VERSION_MINOR=$(echo "$_pkgver" | cut -d "." -f2)
    export RSTUDIO_VERSION_PATCH=$(echo "$_pkgver" | cut -d "." -f3)
    export RSTUDIO_VERSION_SUFFIX="+$(echo "$_pkgver" | cut -d "." -f4)"
    export GIT_COMMIT=$(git ls-remote https://github.com/rstudio/rstudio.git | grep v${pkgver} | sed 's/^\(.\{7\}\).*/\1/')
    export PACKAGE_OS="$distro $osarch"

    export RSTUDIO_NODE_PATH=/usr/

    # node-gyp or node have a bug that prevents building with "text file busy"
    # if the kernel is too fast, so we have to disable IO_URING support. This
    # is cleary a hack and needs to be removed as soon as possible
    # nodejs/node#48444 is the necro bumped thread
    # originally from docker
    # https://github.com/nodejs/node/issues/48444
    export UV_USE_IO_URING=0

    # -DCMAKE_INSTALL_PREFIX seems ignored for sub-dependencies,
    # which results as empty '/usr/local/bin' in package
    # Following override works for cmake >3.29
    export CMAKE_INSTALL_PREFIX=/usr/lib/rstudio

     local _CMAKE_RSTUDIO_OPTIONS=(
      -DRSTUDIO_TARGET=Desktop
      -DCMAKE_BUILD_TYPE=None
      -DRSTUDIO_USE_SYSTEM_BOOST=yes
      -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake
      -DBoost_NO_BOOST_CMAKE=ON
      -DQUARTO_ENABLED=${_quarto}
      -DRSTUDIO_USE_SYSTEM_NODE=yes
      -DRSTUDIO_BUNDLE_QT=FALSE
    )
    cmake -G Ninja -S "${srcdir}/${_srcname}" -B build "${_CMAKE_RSTUDIO_OPTIONS[@]}"
    cmake --build build
}

package() {

    # Install the program
    DESTDIR="${pkgdir}" cmake --install build

    # Install the license
    install -Dm 644 "${srcdir}/${_srcname}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

    # Symlink main binary
    install -d "${pkgdir}/usr/bin"
    ln -s "/usr/lib/rstudio/bin/rstudio" "${pkgdir}/usr/bin/rstudio"

    # BUGFIX: qt5-webengine isn't init'ing properly. Likely an Rstudio bug.
    install -Dm 644 "${srcdir}/qt.conf" "${pkgdir}/usr/lib/qt/libexec/qt.conf"
}
