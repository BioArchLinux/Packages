# Maintainer: Artem Klevtsov <a.a.klevtso@gmail.com>
# Maintainer: Stephen Martin <hwkiller@gmail.com>
# Contributor: Conor Anderson <conor@conr.ca>

pkgname=rstudio-desktop
pkgver=2023.12.1+402
_pkgver=${pkgver//+/.}
_srcname=rstudio-${pkgver//+/-}
_pandocver="current"
_quarto="FALSE"
pkgrel=2
pkgdesc="A powerful and productive integrated development environment \
	 (IDE) for R programming language"
arch=('x86_64')
url="https://posit.co/products/open-source/rstudio/"
license=('AGPL3')
depends=('r>=3.3.0' 'boost-libs' 'qt5-sensors' 'qt5-svg' 'glibc' 'zlib' 'bash' 'gcc-libs' 'qt5-webengine' 'qt5-xmlpatterns' 'postgresql-libs' 'sqlite3' 'soci' 'clang' 'hunspell-en_US' 'mathjax2' 'pandoc' 'yaml-cpp' 'r-rmarkdown' 'r-renv' 'r-testthat' 'r-xml2' 'r-yaml' 'hicolor-icon-theme' 'openssl' 'util-linux-libs')
makedepends=('git' 'cmake>=3.1.0' 'boost' 'desktop-file-utils' 'jdk8-openjdk' 'apache-ant' 'unzip' 'openssl' 'libcups' 'pam' 'patchelf' 'wget' 'yarn')
optdepends=('git: for git support'
            'subversion: for subversion support'
            'openssh-askpass: for a git ssh access'
            )
source=("rstudio-$_pkgver.tar.gz::https://github.com/rstudio/rstudio/archive/refs/tags/${pkgver}.tar.gz"
        "https://github.com/quarto-dev/quarto/archive/refs/heads/release/rstudio-cherry-blossom.zip"
        "qt.conf"
        "pandoc_version.patch")

sha256sums=('196d31094d580a74737fbf689d2d0b302da5fec13694aa1d63f8875d3e45e4dd'
            'df3a040f0cf4ce1892519082dd6822a8ca433e9e4f02d9394ab2931733f7e5a6'
            '723626bfe05dafa545e135e8e61a482df111f488583fef155301acc5ecbbf921'
            '286925c442c1818979714feeec1577f03ae8a3527d2478b0f55238e2272a0b9e')

prepare() {
    cd ${srcdir}/${_srcname}
    # Do not use outdated version name of pandoc
    patch -p1 < ${srcdir}/pandoc_version.patch

    cd "${srcdir}/${_srcname}/dependencies/common"
    install -d pandoc/${_pandocver}
 
    ln -sfT /usr/share/myspell/dicts dictionaries
    ln -sfT /usr/share/mathjax2 mathjax-27
    ln -sfT /usr/bin/pandoc pandoc/${_pandocver}/pandoc

    # Fix links for src/cpp/session/CMakeLists.txt
    cd "${srcdir}/${_srcname}/dependencies"
    ln -sfT /usr/share/myspell/dicts dictionaries
    ln -sfT /usr/share/mathjax2 mathjax-27
    #ln -sfT /usr/bin/pandoc pandoc

    # While we are not building with quarto support, quarto source should be present in build directory
    # for compilation to finish properly
    mkdir -p "${srcdir}/${_srcname}/src/gwt/lib/quarto"
    cp -r "${srcdir}/quarto-release-rstudio-cherry-blossom/"* "${srcdir}/${_srcname}/src/gwt/lib/quarto"
}

build() {

    cd "${srcdir}/${_srcname}/dependencies"
        install -d pandoc/${_pandocver}/bin/tools
        ln -sfT /usr/bin/pandoc pandoc/${_pandocver}/bin/tools/pandoc

    cd ${srcdir}

    # Use path of system provided R packages
    export R_LIBS_USER=/usr/lib/R/library/

    export PATH=/usr/lib/jvm/java-8-openjdk/jre/bin/:${PATH}
    # These variables are used to create contents of Help > About dialog 
    export RSTUDIO_VERSION_MAJOR=$(echo "$_pkgver" | cut -d "." -f1)
    export RSTUDIO_VERSION_MINOR=$(echo "$_pkgver" | cut -d "." -f2)
    export RSTUDIO_VERSION_PATCH=$(echo "$_pkgver" | cut -d "." -f3)
    export RSTUDIO_VERSION_SUFFIX="+$(echo "$_pkgver" | cut -d "." -f4)"
    export GIT_COMMIT=$(git ls-remote https://github.com/rstudio/rstudio.git | grep v${pkgver} | sed 's/^\(.\{7\}\).*/\1/')
    export PACKAGE_OS=$(uname -om)
    
    cmake -S "${srcdir}/${_srcname}" -B build \
          -DRSTUDIO_TARGET=Desktop \
          -DCMAKE_BUILD_TYPE=None \
          -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio \
          -DRSTUDIO_USE_SYSTEM_BOOST=yes \
          -DRSTUDIO_USE_SYSTEM_YAML_CPP=yes \
          -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake \
          -DBoost_NO_BOOST_CMAKE=ON \
          -DQUARTO_ENABLED=${_quarto} \
          -DRSTUDIO_USE_SYSTEM_SOCI=yes \
          -DRSTUDIO_BUNDLE_QT=FALSE 

     cmake --build build
}

package() {
    # Install the program
     DESTDIR="$pkgdir" cmake --install build
    # Symlink main binary to /usr/bin
    install -d "${pkgdir}/usr/bin"
    ln -s "/usr/lib/rstudio/bin/rstudio" "${pkgdir}/usr/bin/rstudio"
    # BUGFIX: qt5-webengine isn't init'ing properly. Likely an Rstudio bug.
    install -Dm 644 "${srcdir}/qt.conf" "${pkgdir}/usr/lib/qt/libexec/qt.conf"
}
