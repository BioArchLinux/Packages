# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=BiocGenerics
_pkgver=0.54.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="S4 generic functions used in Bioconductor"
arch=(any)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0')
depends=(
  r-generics
)
optdepends=(
  r-affy
  r-affyplm
  r-annotate
  r-annotationdbi
  r-biobase
  r-delayedarray
  r-deseq2
  r-flowclust
  r-genomicranges
  r-hdf5array
  r-iranges
  r-msnbase
  r-pwalign
  r-rsamtools
  r-runit
  r-s4arrays
  r-s4vectors
  r-sparsearray
  r-multiassayexperiment
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('43d1e10d0c205a8140d1d75881952a67')
b2sums=('a047e539c4043173407fa4b67c9f7ebdad059ffbaa8b7fa991c1aab19bdb212a0f226356d1ba811b4b279867d483051f75b9f105264a7ac22fa0496b1464cf7b')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
