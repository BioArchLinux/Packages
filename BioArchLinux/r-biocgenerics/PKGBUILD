# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=BiocGenerics
_pkgver=0.54.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="S4 generic functions used in Bioconductor"
arch=(any)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0')
depends=(
  r-generics
)
optdepends=(
  r-affy
  r-affyplm
  r-annotate
  r-annotationdbi
  r-biobase
  r-delayedarray
  r-deseq2
  r-flowclust
  r-genomicranges
  r-hdf5array
  r-iranges
  r-msnbase
  r-pwalign
  r-rsamtools
  r-runit
  r-s4arrays
  r-s4vectors
  r-sparsearray
  r-multiassayexperiment
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('76b2dc23aa27f92693340b01594a97c2')
b2sums=('bcbf275d44b2aefd9ae8f4df952a6da780d67620cc6dba47dd6e28073a6d0b91898fd390748a4612bbf88d9877a74fc16a184c4a429c621a0422497c1bb362b5')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
