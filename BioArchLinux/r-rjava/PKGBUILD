# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_pkgname=rJava
_pkgver=1.0-11
pkgname=r-${_pkgname,,}
pkgver=1.0.11
pkgrel=1
pkgdesc='Low-Level R to Java Interface'
arch=(x86_64)
url="https://cran.r-project.org/package=${_pkgname}"
license=(LGPL)
depends=(
  r
  openmp
  java-runtime
)
makedepends=(
  make
  clang
  java-environment
  patchelf
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('9ea0ccf5553d86f7de8649a8324766c4f0810f35b7be561640dd87fd37986417')


package() {
  export CC=clang
  export CXX=clang++
  export LD=clang
  export JAVA_HOME=/usr/lib/jvm/default
  export JAR=$JAVA_HOME/bin/jar
  export JAVA=$JAVA_HOME/bin/java
  export JAVAC=$JAVA_HOME/bin/javac
  export JAVA_CPPFLAGS="-I$JAVA_HOME/include -I$JAVA_HOME/include/linux"
  export JAVA_LD_LIBRARY_PATH=$JAVA_HOME/lib/server
  export JAVA_LIBS="-L$JAVA_HOME/lib/server -ljvm"
  export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$JAVA_HOME/lib/server"

  archlinux-java fix
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"

  patchelf --set-rpath "${JAVA_LD_LIBRARY_PATH}" ${pkgdir}/usr/lib/R/library/rJava/libs/rJava.so
}
