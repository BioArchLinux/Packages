# Maintainer: Guoyi Zhang <myname at malacology dot net>
pkgname=idba
pkgver=1.1.3
pkgrel=2
pkgdesc="Basic iterative de Bruijn graph assembler for second-generation sequencing reads \
	https://doi.org/10.1007/978-3-642-12683-3_28 \
	https://doi.org/10.1093/bioinformatics/bts174
	"
arch=('i686' 'x86_64')
url="https://github.com/loneknightpy/idba"
license=('GPL')
depends=('gcc-libs' 'python' 'perl')
makedepends=('make' 'autoconf' 'gcc')
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha256sums=('6b1746a29884f4fa17b110d94d9ead677ab5557c084a93b16b6a043dbb148709')
prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  2to3 -w script/scan.py
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  autoreconf --install
  ./configure --prefix=/usr 
  make
}

check() {
  cd $srcdir/$pkgname-$pkgver
  make unittest
}

package() {
  cd "$srcdir/${pkgname}-$pkgver"
  make DESTDIR="$pkgdir" install

for file in bin/*; do
  if [[ -f "$file" && -x "$file" ]]; then
    install -Dm755 "$file" "$pkgdir/usr/bin/$(basename "$file")"
  fi
done

  mv $pkgdir/usr/bin/{,idba_}test
}
