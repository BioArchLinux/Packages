# Maintainer: Kiri <kiri@vern.cc>
# Contributor: Michael Schubert <mschu.dev at gmail>
# Contributor: Mick Elliot <micke at sfu dot ca>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
pkgname=tcoffee
pkgver=13.46.2.f3896b96
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL2')
pkgdesc="An alignment tool for Protein, DNA and RNA sequences. https://doi.org/10.1006/jmbi.2000.4042"
url="http://www.tcoffee.org"
makedepends=('gcc-fortran')
depends=('gcc-libs')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/cbcrg/tcoffee/archive/refs/tags/Version_${pkgver}.tar.gz")
sha256sums=('acddedbe999a21f3d125bed46f0135384882ad8c1550c786affcf2750e96fe0a')

build() {
  cd "$srcdir/tcoffee-Version_${pkgver}/t_coffee/src"
  make CC="g++ -fpermissive" FCC=gfortran all
}

package() {
  cd "$srcdir/tcoffee-Version_${pkgver}/t_coffee/src"
  install -Dm755 t_coffee "$pkgdir/usr/bin/t_coffee"
}
