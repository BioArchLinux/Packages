# Maintainer:Bipin Kumar <kbipinkumar@pm.me>
pkgbase=mcl
pkgname=('mcl' 'mcl-doc')
pkgver=22.282
_pkgver=${pkgver//./-}
pkgrel=7
pkgdesc='Markov clustering algorithm for weighted or simple networks' 
arch=(x86_64)
url="http://micans.org/mcl/"
license=('GPL2')
makedepends=('quilt' 'cimfomfa' 'zoem')
source=(
        "${pkgbase}-${_pkgver}.tar.gz::http://micans.org/mcl/src/${pkgbase}-${_pkgver}.tar.gz"
        '02-wrong-interpreter-path.patch'
        '05-manual-what-is.patch'
        'spelling.patch'
        'ansic.patch'
        'series'
       )
b2sums=('5886cf3a2118510de7a9b7c0e5923a7c2874e3e14eeca5dd6e5fcf7d4a618305006d965ac7443b85ba1c7c7dfca7ff237f4770b5e8640cbf7d98d902fd91c7d0'
        '465205034d522d4c61927e6f1ab0c90cea139b6fee20d3cee70066ece4931542faf067b9d77a78a03e01ea50415adf9e0b2c49a27d8ecc1f2bad9f0ee38cf6c9'
        '31440b26deb5afa9939ac3643159e229e8838379a61c86f9b3101a13efc4ac9494b7375cf9b7883157615ff5ce1a9d057b42ddcf7bdff87cc32477b6d6fe180a'
        '28be064271e0a897181c64fe5c810611b68b5c9fab7320f22ed2542a5bd4f6ddc92f1f6eea61033c39a29473205e46a0200e8b8647cc39b9c5e1cdca0a9ad915'
        '67c7f50fcc9610c33eb9577e435900d7031217f9aca5f817ae9845aca0a27882f8bfdc820ac3910076923220cf4b4aeb8187f56f57341cb3258313c9f96a7f58'
        '93bca3157a0b392336411c30cea2b1539b09f06e5d0c7327e9c24a7c20ff60a83ae97d06feab1038b4afbf2da033d446cb3313fe04ed809f8b4c5db83e02eda0')

prepare() {
  mkdir -p ${pkgbase}-${_pkgver}/patches
  cp series ${pkgbase}-${_pkgver}/patches
  cp *.patch ${pkgbase}-${_pkgver}/patches
  cd ${pkgbase}-${_pkgver}
  quilt push -a
}

build() {
  cd ${pkgbase}-${_pkgver}
  ./configure --prefix /usr
  make
}

package_mcl() {
  depends=('glibc' 'cimfomfa' 'perl' 'bash')
  pkgdesc='Markov clustering algorithm for weighted or simple networks'
  cd ${pkgbase}-${_pkgver} 
  install -d 755 ${pkgdir}/usr
  make DESTDIR=${pkgdir}/ install-exec 

}

package_mcl-doc() {
  arch=(any)
  pkgdesc='Documentation for Markov clustering algorithm'
  optdepends=('man-db: Read manpages from console'
              'perl: run bundled example perl scripts')
  cd ${pkgbase}-${_pkgver}
  install -d 755 ${pkgdir}/usr
  make DESTDIR=${pkgdir}/ install-data install-am
}
