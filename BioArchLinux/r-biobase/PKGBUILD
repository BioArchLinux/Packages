# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=Biobase
_pkgver=2.70.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Base functions for Bioconductor"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0')
depends=(
  r-biocgenerics
)
optdepends=(
  r-all
  r-biocstyle
  r-golubesets
  r-knitr
  r-limma
  r-runit
  r-tkwidgets
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('1972f4f090998033acd1d3bfdec77961')
b2sums=('66eb1d2f6c3741fd1cc8bcad81bcf88ccad70d9b4f293987e46515395d8109b361f455e173f8f0e47ee68e1a8511e90bfbb9e03ce384fcb86375ad54330a1c09')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
