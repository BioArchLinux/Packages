# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=signal
_pkgver=1.8-1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Signal Processing"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-2.0-only')
depends=(
  r
)
makedepends=(
  gcc-fortran
)
optdepends=(
  r-pracma
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('02fbe007f51ff7ce3b03d7ea5d5ca924')
b2sums=('541ffc19d939ba91c10812fb009666328c248fe93c839eb8aa704554afa577f4e67980d77d9ba0702dcafad70d5f6877c879f89ba6b42e3de46f85ede0168da2')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
