# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=vctrs
_pkgver=0.7.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Vector Helpers"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-cli
  r-glue
  r-lifecycle
  r-rlang
)
optdepends=(
  r-bit64
  r-covr
  r-crayon
  r-dplyr
  r-generics
  r-knitr
  r-pillar
  r-pkgdown
  r-rmarkdown
  r-testthat
  r-tibble
  r-waldo
  r-withr
  r-xml2
  r-zeallot
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('a443790e2d704549b113489b39caaff0')
b2sums=('55bf9bc513c94eb1fb9dfdaf42c73087a531142276bee23760a55ed2298e6aa7df792fc8b63704db953f1746cadcc52adf237e139f825d43980d53c4ab75b098')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
