# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=logitnorm
_pkgver=0.8.38
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Functions for the Logitnormal Distribution"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-2.0-only')
depends=(
  r
)
checkdepends=(
  r-runit
)
optdepends=(
  r-ggplot2
  r-knitr
  r-markdown
  r-reshape2
  r-runit
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('4a1f49249dcd7a212287a1ac6b4e1921')
b2sums=('f29fb662aa2eff0e7238be054fbd0179135746d55b167a92d95b5b91939ee0581a805bd1c4a664b05f9a007dcd8bfba4455be32bbd78e3a2c305a5a936cd8bcb')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" Rscript --vanilla doRUnit.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
