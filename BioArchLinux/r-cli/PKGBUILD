# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=cli
_pkgver=3.6.4
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Helpers for Developing Command Line Interfaces"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r
)
optdepends=(
  r-callr
  r-covr
  r-crayon
  r-digest
  r-glue
  r-htmltools
  r-htmlwidgets
  r-knitr
  r-processx
  r-ps
  r-rlang
  r-rmarkdown
  r-rprojroot
  r-rstudioapi
  r-testthat
  r-tibble
  r-whoami
  r-withr
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('70674a2eef97973efc085dd0b115fe26')
b2sums=('3a7be4602d74fd8dba64a212eb29f8bcf9793b87e6203ecaa08aa3cf69b91a028f54da20347d78cdf45c0b85de48406d621ceab11e1f48aa15231f88baeb04fc')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
