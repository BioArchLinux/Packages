# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=DelayedArray
_pkgver=0.30.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="A unified framework for working transparently with on-disk and in-memory array-like datasets"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0')
depends=(
  r-biocgenerics
  r-iranges
  r-matrixgenerics
  r-s4arrays
  r-s4vectors
  r-sparsearray
)
optdepends=(
  r-airway
  r-biocparallel
  r-biocstyle
  r-delayedmatrixstats
  r-genefilter
  r-hdf5array
  r-knitr
  r-lobstr
  r-rmarkdown
  r-runit
  r-summarizedexperiment
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('bced73ec9d8dabb5b14a3fa236b9a402')
b2sums=('a32e00e9e56d26f94a470d06bbc74960f658f13e61653e8e2886fb1a5e4d8e4e28425154b869f1ae1bbaea3efb9a1c3bc6a33ea32921a93a0c6c3d766a55cc77')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
