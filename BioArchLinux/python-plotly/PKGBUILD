# Maintainer: Grey Christoforo <first name at last name dot net>
# Co-Maintainer: Bert Peters <bert@bertptrs.nl>
# imported from AUR: Bipin Kumar <kbipinkumar@pm.me>

pkgname=python-plotly
pkgver=5.22.0
pkgrel=1
pkgdesc="An open-source, interactive graphing library"
url="https://github.com/plotly/plotly.py"
depends=(
         'python'
         'python-tenacity'
         'python-ipywidgets'
         'python-requests'
         'python-numpy'
         'python-scipy'
         'python-pillow'
         'python-pandas'
         'python-matplotlib'
         'python-xarray'
         'python-statsmodels'
        )
# optdepends based on optional_requirements.txt
optdepends=(
            'ipython'
            'jupyterlab'
            'python-colorcet'
            )

makedepends=(
             'python-setuptools'
             'python-jupyter-core'
             'python-tornado'
             'jupyterlab'
             'jupyter-server'
             'npm'
             'python-wheel'
             'python-build'
             'python-pytest'
            )
license=('MIT')
arch=('any')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/plotly/plotly.py/archive/v${pkgver}.tar.gz"
	"compat-py3.11.patch")
options=(!strip) # strip isn't useful for python files and takes forever
sha256sums=('1f4b007da5b0384291bef634f7a58f0b0eafde2a9dde11c2aa5527243f86624c'
            'af793f902f28aa7ae25abc6e31fa7c4163b0b7f792037baee6b727aede9136c4')

prepare() {
    cd plotly.py-${pkgver}/
#    patch --forward --strip=0 --input="${srcdir}/compat-py3.11.patch"
}

build() {
  cd plotly.py-${pkgver}/packages/python/plotly
  SKIP_NPM=1 python setup.py build
}

check() {
  cd plotly.py-${pkgver}/packages/python/plotly
  pytest plotly/tests/test_core
}

package() {
  cd plotly.py-${pkgver}/packages/python/plotly
  SKIP_NPM=1 python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  mv ${pkgdir}/usr/etc ${pkgdir}
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
