# Maintainer: Kiri <kiri@vern.cc>

pkgname='python-biotite'
_module=${pkgname#python-}
pkgver=0.40.0
pkgrel=1
pkgdesc="A comprehensive library for computational molecular biology"
arch=('x86_64')
url="https://www.biotite-python.org"
license=('BSD')
depends=(python
         python-msgpack
         python-networkx
         python-numpy
         python-requests)
makedepends=(python-build
             python-installer
             python-wheel
             python-setuptools
             python-oldest-supported-numpy
             cython)
source=("${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_module::1}/$_module/${_module}-$pkgver.tar.gz")
sha256sums=('7c24f4efcc315299ad36f5deacef0c65bb61cc59a6b642387b6210988b557261')

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_module}-${pkgver}"
    install -D -m644 LICENSE.rst "${pkgdir}/usr/share/licenses/python-biotite/LICENSE"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
