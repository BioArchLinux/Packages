# Maintainer: Kiri <kiri@vern.cc>

pkgname='python-biotite'
_module=${pkgname#python-}
pkgver=0.38.0
pkgrel=1
pkgdesc="A comprehensive library for computational molecular biology"
arch=('x86_64')
url="https://www.biotite-python.org"
license=('BSD')
depends=(python
         python-msgpack
         python-networkx
         python-numpy
         python-requests)
makedepends=(python-build
             python-installer
             python-wheel
             python-oldest-supported-numpy
             cython)
source=("${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_module::1}/$_module/${_module}-$pkgver.tar.gz")
sha256sums=('6194f2281f9e6e3fb3c3064f851b46add19704fc6c11de5d289cda2f4dcf8213')

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_module}-${pkgver}"
    install -D -m644 LICENSE.rst "${pkgdir}/usr/share/licenses/python-biotite/LICENSE"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
