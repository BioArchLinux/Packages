# import from AUR: bipin kumar <bipin@ccmb.res.in>
# Maintainer: Michael Schubert <mschu.dev at gmail> github.com/mschubert/PKGBUILDs
pkgname=python-numba
_module=${pkgname#python-}
pkgver=0.57.0dev0
pkgrel=1
pkgdesc="NumPy aware dynamic Python compiler using LLVM"
url="https://numba.pydata.org/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('python-llvmlite>=0.39.0' 'python-llvmlite<0.40' 'python-numpy>=1.18' 'python-numpy<1.24' 'intel-tbb>=2021.1' 'python-setuptools')
makedepends=('cython')
optdepends=(
            'python-scipy>=1.0.0'
            'python-jinja' 
            'python-cffi'
            'ipython'
            'python-yaml'
            'python-colorama' 
            'python-pygments'
            'gdb' 
            'graphviz'
            'python-typeguard'
            'python-cuda'
            )
source=($_module-$pkgver.tar.gz::https://github.com/numba/numba/archive/refs/tags/$pkgver.tar.gz)
sha256sums=('cd7db507c8b57c1cf911f83f1199754cb25e4c3bf71985f72a5ab91ae3771cfa')

build() {
  cd "$srcdir/$_module-$pkgver"
  python setup.py build
}

check_disabled() { #ERROR: TypeError None is not callable
  cd "$srcdir/$_module-$pkgver"
  python setup.py test
}

package() {
  cd "$srcdir/$_module-$pkgver"
  python setup.py install --skip-build --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 "$srcdir/$_module-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}
