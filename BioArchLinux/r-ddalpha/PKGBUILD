# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Alex Branham <branham@utexas.edu>

_pkgname=ddalpha
_pkgver=1.3.13
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=3
pkgdesc="Depth-Based Classification and Calculation of Data Depth"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=(GPL2)
depends=(
  r-geometry
  r-rcpp
  r-robustbase
  r-sfsmisc
)
makedepends=(
  gcc-fortran
  r-bh
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('60c444f7f82ed56607c87df9c7caec05')
b2sums=('43f18b76457cc7e1b4ce4d7ad6df9a0ed4161fde1f0353293288200446ef39799cfdf8df6e17764c8a1d7c4b9957f04434a80107b10368468bd9978981353b2d')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
