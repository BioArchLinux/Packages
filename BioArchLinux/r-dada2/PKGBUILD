# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=dada2
_pkgver=1.30.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=4
pkgdesc="Accurate, high-resolution sample inference from amplicon sequencing data"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('LGPL-2.0-only')
depends=(
  onetbb
  r-biocgenerics
  r-biostrings
  r-ggplot2
  r-iranges
  r-rcpp
  r-rcppparallel
  r-reshape2
  r-shortread
  r-xvector
)
optdepends=(
  r-biocstyle
  r-knitr
  r-rmarkdown
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('096baf34b4618af27b1b430945d3a189')
b2sums=('ca212644a29fc7a94a1a46c7ae59937a7cb98501bd3b73ad2213d8a191cb99f6ca8c149d31cc4a1dc0cdb1156683b4709035bd813693e83ab56687215855f18e')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
