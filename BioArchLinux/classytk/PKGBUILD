# Maintainer: Guoyi
pkgbase="classytk"
pkgname=(classytk classytcl)
pkgver=1.1.0
pkgrel=3
pkgdesc="widget set and GUI builder based on ClassyTcl object system"
arch=('x86_64')
url="https://wiki.tcl-lang.org/page/ClassyTk"
license=('BSD')
depends=('tcl'  'tk' 'extral' 'tcl-pkgtools')
source=(
"https://sourceforge.net/projects/classytcl/files/ClassyTk-${pkgver}-src.tar.gz"
"https://sourceforge.net/projects/classytcl/files/ClassyTcl-${pkgver}-src.tar.gz"
)

sha256sums=('0ad607abaa4892e8dd087278c523579ebd47bd78d7001e167df0e3570e3914cf'
            'a0d5d37c1660119887fac7e38f306797dda335b6f69812227e49639be01faeca')
options=(!emptydirs)

prepare(){
  cd ${srcdir}/ClassyTcl/help
  rm -rf 'ClassyTcl.html~'

}


build() {
  cd ${srcdir}/ClassyTcl/build
  ./makedoc.tcl
  cd ${srcdir}/ClassyTk/build
  ./makedoc.tcl

}

package_classytcl() {
  cd ${srcdir}/ClassyTcl/build
  ./install.tcl ${pkgdir}/usr/lib/ClassyTcl
  cd ${pkgdir}/usr/lib/ClassyTcl${pkgver}/
  sed -i "s|${pkgdir}||g" lib/init.tcl pkgIndex.tcl

  install -d ${pkgdir}/usr/share/doc/ClassyTcl${pkgver}/
  mv help/* README  ${pkgdir}/usr/share/doc/ClassyTcl${pkgver}/
  cd ${pkgdir}/usr/lib/ClassyTcl${pkgver}/linux-$CARCH/
  mv *.so ${pkgdir}/usr/lib/
}

package_classytk() {
  arch=('any')
  cd ${srcdir}/ClassyTk/build
 ./install.tcl ${pkgdir}/usr/lib/ClassyTk
}
