# Maintainer: Guoyi
pkgbase="classytk"
pkgname=(classytk classytcl)
pkgver=1.1.0
pkgrel=1
pkgdesc="widget set and GUI builder based on ClassyTcl object system"
arch=('x86_64')
url="https://wiki.tcl-lang.org/page/ClassyTk"
license=('BSD')
depends=('tcl'  'tk' 'extral' 'tcl-pkgtools')
source=(
"https://sourceforge.net/projects/classytcl/files/ClassyTk-${pkgver}-src.tar.gz"
"https://sourceforge.net/projects/classytcl/files/ClassyTcl-${pkgver}-src.tar.gz"
)

sha256sums=('0ad607abaa4892e8dd087278c523579ebd47bd78d7001e167df0e3570e3914cf'
            'a0d5d37c1660119887fac7e38f306797dda335b6f69812227e49639be01faeca')
options=(!emptydirs)

prepare(){
  cd ${srcdir}/ClassyTcl/help
  rm -rf 'ClassyTcl.html~'

}


build() {
  cd ${srcdir}/ClassyTcl/build
  ./configure --prefix=/usr --with-tcl=/usr/lib --with-tclinclude=/usr/include
  make
  cd ${srcdir}/ClassyTk/build
  ./configure --prefix=/usr --with-tcl=/usr/lib --with-tclinclude=/usr/include
  make

}

package_classytcl() {
  cd ${srcdir}/ClassyTcl/build
  make install DESTDIR="$pkgdir"
  cd ${pkgdir}/usr/lib/Class1.0/
  sed -i "s|${pkgdir}||g" lib/init.tcl pkgIndex.tcl

  install -d ${pkgdir}/usr/share/doc/Class1.0/
  mv help/* README  ${pkgdir}/usr/share/doc/Class1.0/
}

package_classytk() {
  cd ${srcdir}/ClassyTk/build
  make install DESTDIR="$pkgdir"
  # fix broken links
  cd ${pkgdir}/usr/bin
  for bin in $(ls);
  do
    ln -sf ../lib/ClassyTk1.0/bin/$bin ${pkgdir}/usr/bin/$bin
  done;
  ln -sf ../lib/ClassyTk1.0/apps/cwww/cwww.tcl ${pkgdir}/usr/bin/cwww

  cd ${pkgdir}/usr/lib/ClassyTk1.0/apps/cwww/
  sed -i 's|ClassyTk 0.4|ClassyTk 1.0|g' cwww.tcl
}
