# Maintainer:  Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: TDY <tdy@archlinux.info>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=udunits
pkgver=2.2.28
pkgrel=5
pkgdesc="A tool for calculations of physical quantities"
arch=(x86_64)
url="http://www.unidata.ucar.edu/software/udunits/"
license=(custom)
depends=(expat)
source=("https://artifacts.unidata.ucar.edu/repository/downloads-udunits/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('590baec83161a3fd62c00efa66f6113cec8a7c461e3f61a5182167e0cc5d579e')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYRIGHT
  cd "$pkgdir/usr/lib"
  local _lib
  for _lib in libudunits2.so*; do
    ln -s "$_lib" "${_lib/#libudunits2/libudunits}"
  done
}
