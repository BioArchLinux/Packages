# system requirements: libbz2 & liblzma & libcurl (with header files), GNUmake
# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=Rhtslib
_pkgver=2.0.0
pkgname=r-${_pkgname,,}
pkgver=2.0.0
pkgrel=1
pkgdesc='HTSlib high-throughput sequencing library as an R package'
arch=('x86_64')
url="https://bioconductor.org/packages/${_pkgname}"
license=('LGPL')
options=(!lto staticlibs)
depends=(
  r
  r-zlibbioc
  bzip2
  xz
  curl
)
optdepends=(
  r-biocstyle
  r-knitr
  r-rmarkdown
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('5098bf9f4e8bbbe7eea3d63480c5c970af87b68998d625a2ec0f12b2393727e6')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
}
# vim:set ts=2 sw=2 et:
