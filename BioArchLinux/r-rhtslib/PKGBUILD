# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=Rhtslib
_pkgver=2.4.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=4
pkgdesc="HTSlib high-throughput sequencing library as an R package"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('LGPL-2.0-or-later')
depends=(
  bzip2
  curl
  r-zlibbioc
  xz
  zlib
)
optdepends=(
  r-biocstyle
  r-knitr
  r-rmarkdown
)
options=(staticlibs)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('b7d295a694e865f4f4060c94c4bca446')
b2sums=('003d61f9455981794724ab4e65eaf7f557d2bd394b58ba4e61eb895fbceb8117cb8a8fc6b99f7be71f2ec1b1641ea71867c6a083deba29991c630716de48e82c')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
