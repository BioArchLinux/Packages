# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=Rhtslib
_pkgver=3.4.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="HTSlib high-throughput sequencing library as an R package"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('LGPL-2.0-or-later')
depends=(
  bzip2
  curl
  r
  xz
  zlib
)
optdepends=(
  r-biocstyle
  r-knitr
  r-rmarkdown
)
options=(staticlibs)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('5f9e5b7a15bb67e60351b5896254cfec')
b2sums=('6f5288c03064e9e711136fcbeffac3db2253b88ae0f2f71ec89aa32f8c3a8da56a6b29abf55be2edaf35565d1211ac7a78424f4bb4ecb3c6d3582bbb16e01d74')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
