# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=Rhtslib
_pkgver=3.2.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="HTSlib high-throughput sequencing library as an R package"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('LGPL-2.0-or-later')
depends=(
  bzip2
  curl
  r
  xz
  zlib
)
optdepends=(
  r-biocstyle
  r-knitr
  r-rmarkdown
)
options=(staticlibs)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('1d8eabf0934373b61aca2dc20cab712b')
b2sums=('c3dac27122de6be55135fbcf550bb13acfc6dc1c78a05d753548569627eb1adda7ea1902922440ad9cc44b8d55267b4dff692e94ac4c4c81eca97ebd13967b33')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
