# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=GenomeInfoDb
_pkgver=1.40.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Utilities for manipulating chromosome names, including modifying them to follow a particular naming style"
arch=(any)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0')
depends=(
  r-biocgenerics
  r-genomeinfodbdata
  r-iranges
  r-s4vectors
  r-ucsc.utils
)
optdepends=(
  r-biocstyle
  r-bsgenome
  r-bsgenome.celegans.ucsc.ce2
  r-bsgenome.hsapiens.ncbi.grch38
  r-bsgenome.scerevisiae.ucsc.saccer2
  r-data.table
  r-genomicalignments
  r-genomicfeatures
  r-genomicranges
  r-knitr
  r-r.utils
  r-rsamtools
  r-runit
  r-txdb.dmelanogaster.ucsc.dm3.ensgene
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('6b6c8b47d8b4c8c8af76bdf534570bd5')
b2sums=('4bfdfa0a294c07607cc97a52e8269ce303ab3aaf1df0d414b28bbab2e5752f3c82930a91d64a3e7474dc3885a80ede66ba1c9935809fb01d66a5dc34c80bfbff')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
