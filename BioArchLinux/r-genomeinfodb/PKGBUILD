# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=GenomeInfoDb
_pkgver=1.38.5
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Utilities for manipulating chromosome names, including modifying them to follow a particular naming style"
arch=(any)
url="https://bioconductor.org/packages/$_pkgname"
license=(Artistic2.0)
depends=(
  r-biocgenerics
  r-genomeinfodbdata
  r-iranges
  r-rcurl
  r-s4vectors
)
optdepends=(
  r-biocstyle
  r-bsgenome
  r-bsgenome.celegans.ucsc.ce2
  r-bsgenome.hsapiens.ncbi.grch38
  r-bsgenome.scerevisiae.ucsc.saccer2
  r-data.table
  r-genomicalignments
  r-genomicfeatures
  r-genomicranges
  r-knitr
  r-r.utils
  r-rsamtools
  r-runit
  r-txdb.dmelanogaster.ucsc.dm3.ensgene
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('397b4c20b38a4c2bf38198efc9d3beeb')
b2sums=('def2293b3f6dec71e873df568d0fd6b2d52eae0b29389c36dd93de13f21f7a0145661a1316166435bd0b74eef605fded8b7fea97c151442f7e02092e81f33831')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
