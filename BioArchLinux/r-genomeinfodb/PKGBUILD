# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=GenomeInfoDb
_pkgver=1.38.6
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Utilities for manipulating chromosome names, including modifying them to follow a particular naming style"
arch=(any)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0')
depends=(
  r-biocgenerics
  r-genomeinfodbdata
  r-iranges
  r-rcurl
  r-s4vectors
)
optdepends=(
  r-biocstyle
  r-bsgenome
  r-bsgenome.celegans.ucsc.ce2
  r-bsgenome.hsapiens.ncbi.grch38
  r-bsgenome.scerevisiae.ucsc.saccer2
  r-data.table
  r-genomicalignments
  r-genomicfeatures
  r-genomicranges
  r-knitr
  r-r.utils
  r-rsamtools
  r-runit
  r-txdb.dmelanogaster.ucsc.dm3.ensgene
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('b739753a88d5a331b38744b1ad14f416')
b2sums=('bfa7308df102698ba1d2530e2ae6170a7523ff60f2d168b44a24bc4e093a9d01d84c7d3ec496f42850984a6bb6c5e0e74e9e65e7211a84d4905bcfdfb6f7481b')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
