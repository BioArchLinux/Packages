# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=ggplot2
_pkgver=3.4.2
pkgname=r-${_pkgname,,}
pkgver=3.4.2
pkgrel=1
pkgdesc='Create Elegant Data Visualisations Using the Grammar of Graphics'
arch=('any')
url="https://cran.r-project.org/package=${_pkgname}"
license=('MIT')
depends=(
  r
  r-cli
  r-glue
  r-gtable
  r-isoband
  r-lifecycle
  r-rlang
  r-scales
  r-tibble
  r-vctrs
  r-withr
)
optdepends=(
  r-covr
  r-dplyr
  r-ggplot2movies
  r-hexbin
  r-hmisc
  r-knitr
  r-lattice
  r-mapproj
  r-maps
  r-maptools
  r-multcomp
  r-munsell
  r-nlme
  r-profvis
  r-quantreg
  r-ragg
  r-rcolorbrewer
  r-rgeos
  r-rmarkdown
  r-rpart
  r-sf
  r-sp
  r-svglite
  r-testthat
  r-vdiffr
  r-xml2
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('70230aa70a2c6f844fc41dd93e5f62af6859dfed390026ae58f223637e5283ca')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
  install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
