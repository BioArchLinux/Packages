# Maintainer: flying-sheep <flying-sheep@web.de>
# Contributor: bipin kumar <kbipinkumar@pm.me>
pkgname=kallisto
pkgver=0.51.0
pkgrel=1
pkgdesc='Quantify abundances of transcripts from RNA-Seq data. doi:10.1038/nbt.3519'
url="http://pachterlab.github.io/$pkgname/"
license=(BSD)
arch=('x86_64')
depends=('hdf5' 'zlib' 'glibc' 'gcc-libs')
makedepends=('cmake' 'git')
optdepends=('bustools')
source=($pkgname-$pkgver.tar.gz::"https://github.com/pachterlab/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('efeb0191c1a6a0d6de69111fb66f4bda51ff31fb40c513280f072bd44556f80d')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"

	# https://github.com/pachterlab/kallisto/issues/303#issuecomment-884612169
	pushd ext/htslib
	sed -i '/AC_PROG_CC/a \
AC_CANONICAL_HOST \
AC_PROG_INSTALL \
' configure.ac
	autoreconf -i
	autoheader
	autoconf
	popd

	# add missing header
	sed -i '/#include <algorithm>/a #include <limits>' src/MinCollector.cpp
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	rm -rf build
	cmake -B build \
        -DCMAKE_BUILD_TYPE='Release' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
		-DUSE_HDF5=ON \
		-DUSE_BAM=ON \
        -Wno-dev
    MAKEFLAGS=-j1 cmake --build build
}

package() {
	cd "$srcdir/$pkgname-$pkgver/"
	DESTDIR="$pkgdir" cmake --install build
	install -Dm644 "$srcdir/$pkgname-$pkgver/license.txt" -t "$pkgdir/usr/share/licenses/$pkgname"

}
