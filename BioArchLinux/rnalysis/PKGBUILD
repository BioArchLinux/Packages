# Maintainer: Bipin Kumar <kbipinkumar@pm.me>

pkgname=rnalysis
_name=RNAlysis
pkgver=3.4.2
pkgrel=1
pkgdesc='Python-based software for analyzing RNA sequencing data. https://doi.org/10.1101/2022.11.25.517851'
_pkgdesc='Python-based software with GUI for analyzing RNA sequencing data'
arch=(any)
url=https://github.com/GuyTeichman/RNAlysis
license=(MIT)
depends=(
         'python'
         'cython'
         'python-numpy=1.23.5' 
         'python-pandas'
         'python-scipy'
         'python-matplotlib'
         'python-requests'
         'python-scikit-learn'
         'python-seaborn'
         'python-statsmodels'
         'python-joblib'
         'python-tqdm'
         'python-appdirs'
         'python-yaml'
         'python-typing_extensions'
         'python-pyqt5'
         'python-qdarkstyle'
         'r'
         'graphviz'
         'python-graphviz'
         'python-numba'
         'python-hdbscan'
         'python-matplotlib-venn'
         'kallisto'
         'python-scikit-learn-extra'
         'python-grid-strategy'
         'python-upsetplot'
         'python-xlmhg'
         'python-plotly'
         'python-pairwisedist'
         'cutadapt'
         'python-defusedxml'
         'qt5-imageformats'
        )

makedepends=(
             'python-pip'
             'gendesk'
             'python-setuptools'
             'python-setuptools-scm'
             'python-sphinx'
             'python-wheel'
             'python-installer'
             'python-build'
            )

checkdepends=(
            'python-pytest-runner'
            'python-pytest'
            'python-anyio'
            'python-pytest-xvfb'
            'python-pytest-qt'
             )

source=(${_name}-${pkgver}.tar.gz::https://github.com/GuyTeichman/RNAlysis/archive/refs/tags/V"${pkgver}".tar.gz
       )

sha256sums=('593e766277f919cd4c8529645ad2b6afc273e69e1a4e9d95b30351245ad9930e')

prepare() {
    cd "$_name-$pkgver"
    gendesk --exec='/usr/bin/rnalysis-gui' --pkgname "$_name" --pkgdesc "$_pkgdesc" -n --icon=RNAlysis --categories=Science
}

build() {
    cd "$_name-$pkgver"
    export JUPYTER_PLATFORM_DIRS=1
    python -m build --wheel --no-isolation
}

check(){

    export QT_DEBUG_PLUGINS=1
    export JUPYTER_PLATFORM_DIRS=1
    cd "$_name-$pkgver"
    python setup.py pytest | tee check.log
}

package() {

    cd "$_name-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
    install -Dm644 RNAlysis.desktop -t "$pkgdir"/usr/share/applications/
    install -Dm644 docs/source/RNAlysis.svg -t "$pkgdir"/usr/share/pixmaps/
}

