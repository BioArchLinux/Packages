# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=flexsurv
_pkgver=2.2.2
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Flexible Parametric Survival and Multi-State Models"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-2.0-or-later')
depends=(
  r-assertthat
  r-desolve
  r-dplyr
  r-generics
  r-ggplot2
  r-magrittr
  r-mstate
  r-muhaz
  r-mvtnorm
  r-numderiv
  r-purrr
  r-quadprog
  r-rcpp
  r-rlang
  r-rstpm2
  r-statmod
  r-tibble
  r-tidyr
  r-tidyselect
)
checkdepends=(
  r-testthat
)
optdepends=(
  r-colorspace
  r-eha
  r-flexsurvcure
  r-knitr
  r-lubridate
  r-msm
  r-rmarkdown
  r-survminer
  r-testthat
  r-th.data
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('a86c5647062c60b2bebb5ffb8165604f')
b2sums=('f866caeba0cb39bf8ef40dbb0c7d3df6589a2e449d8bd1abaf5da1d3804a621b44cb275f68d0b274fb1f4e28a469a7f5994e449b0d857028bbec42e369eb823d')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla test_base.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
