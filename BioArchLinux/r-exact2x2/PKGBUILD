# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Kiri <kiri@vern.cc>

_pkgname=exact2x2
_pkgver=1.6.8
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=4
pkgdesc="Exact Tests and Confidence Intervals for 2x2 Tables"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-3.0-only')
depends=(
  r-exactci
  r-ssanv
)
checkdepends=(
  r-exact
  r-testthat
)
optdepends=(
  r-exact
  r-ggplot2
  r-gridextra
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('8ad7aa8674eb114e77d9c3c811cf238c')
b2sums=('6199b018d1d036c9b6482b2933481c7c0616edebcf17e087d761c014b654ce05f60f83e5843806904267ad7f0607b04c9a305d7d1cfd3e01ae174ca48ee589e6')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
