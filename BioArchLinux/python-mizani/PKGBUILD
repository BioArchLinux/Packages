# Maintainer: Kiri <kiri@vern.cc>

pkgname='python-mizani'
_module=${pkgname#python-}
pkgver=0.12.2
pkgrel=1
pkgdesc="Scales for Python"
arch=('x86_64')
url="https://mizani.readthedocs.io/en/stable"
license=('BSD')
depends=(python
         python-matplotlib
         python-numpy
         python-pandas
         python-scipy)
makedepends=(python-build
             python-installer
             python-setuptools-scm
             python-wheel)
source=("${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_module::1}/$_module/${_module}-$pkgver.tar.gz")
sha256sums=('b44ab57a57794c6fdabcc2c423860fa042c7e6410f073c9b2959e0e768e0b7a8')

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_module}-${pkgver}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python-mizani/LICENSE"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
