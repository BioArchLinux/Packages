# Maintainer: Kiri <kiri@vern.cc>

pkgname='python-mizani'
_module=${pkgname#python-}
pkgver=0.11.4
pkgrel=1
pkgdesc="Scales for Python"
arch=('x86_64')
url="https://mizani.readthedocs.io/en/stable"
license=('BSD')
depends=(python
         python-matplotlib
         python-numpy
         python-pandas
         python-scipy)
makedepends=(python-build
             python-installer
             python-setuptools-scm
             python-wheel)
source=("${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_module::1}/$_module/${_module}-$pkgver.tar.gz")
sha256sums=('9fdb79e88602ca29132613b428d6c1e8af436e88e37d55a0f8eb99fe044c9bc2')

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_module}-${pkgver}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python-mizani/LICENSE"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
