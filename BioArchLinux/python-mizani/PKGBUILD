# Maintainer: Kiri <kiri@vern.cc>

pkgname='python-mizani'
_module=${pkgname#python-}
pkgver=0.13.5
pkgrel=1
pkgdesc="Scales for Python"
arch=('x86_64')
url="https://mizani.readthedocs.io/en/stable"
license=('BSD')
depends=(python
         python-matplotlib
         python-numpy
         python-pandas
         python-scipy)
makedepends=(python-build
             python-installer
             python-setuptools-scm
             python-wheel)
source=("${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_module::1}/$_module/${_module}-$pkgver.tar.gz")
sha256sums=('99c3306fc3217d3840dfd3468feadd9a7664935a07ae4f7e8b3889e46b48ac73')

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_module}-${pkgver}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python-mizani/LICENSE"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
