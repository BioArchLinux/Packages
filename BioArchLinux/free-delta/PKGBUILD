# Maintainer: Malacology <guoyizhang at malacology dot net>
# Contributor: Sergej Pupykin <sergej@p5n.pp.ru> 
# Contributor: FabioLolix

pkgname=free-delta
pkgver=2.9.2
pkgrel=4
pkgdesc="An Open Source System for Processing Taxonomic Descriptions"
arch=('x86_64')
url="http://freedelta.sourceforge.net/"
#url="https://www.delta-intkey.com/"
license=('GPL2')
depends=(gtk2 classic-delta)
makedepends=(lazarus imagemagick git)
source=("git+https://github.com/maurobio/freedelta.git#tag=v$pkgver"
	"Free_Delta_Editor.desktop"
	"fde.patch")
sha256sums=('SKIP'
            '51bc24fd50441993bf171da7074da692ef2ab8471b67a2294fb14c5073567f0c'
            '025d7782b3edffbebde90d709c4f70a28bac56dd5bfd3802d65afeec7dd1a05a')
prepare () {
  cd $srcdir/freedelta
  patch -N -i ${srcdir}/fde.patch
}
build () {
  cd freedelta

  # binary
  lazbuild -r fde.lpi

  # desktop png
  convert "fde.ico" -thumbnail 256x256 -alpha on -background none -flatten "fde.png"
}
package() {
  install -dDm755 "$pkgdir"/usr/{bin,share/$pkgname}

  install -Dm 755 Free_Delta_Editor.desktop $pkgdir/usr/share/applications/Free_Delta_Editor.desktop
  
  cd freedelta

for dire in languages vocabulary
do
  cp -r $dire $pkgdir/usr/share/$pkgname/
done

  install -Dm 755 fde $pkgdir/usr/share/$pkgname/fde
  install -Dm 644 fde.png $pkgdir/usr/share/pixmaps/fde.png
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  ln -s /usr/share/$pkgname/fde $pkgdir/usr/bin/fde

# add function of free-delta
for prog in dist key confor
do
  ln -s /usr/bin/$prog $pkgdir/usr/share/$pkgname/$prog
done	
}
