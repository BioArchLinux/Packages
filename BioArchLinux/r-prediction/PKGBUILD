# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=prediction
_pkgver=0.3.14
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=3
pkgdesc="Tidy, Type-Safe 'prediction()' Methods"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-data.table
)
checkdepends=(
  r-aer
  r-aod
  r-betareg
  r-biglm
  r-brglm
  r-caret
  r-crch
  r-e1071
  r-earth
  r-gam
  r-gee
  r-glmnet
  r-glmx
  r-kernlab
  r-lme4
  r-mclogit
  r-mda
  r-mnp
  r-ordinal
  r-plm
  r-pscl
  r-quantreg
  r-sampleselection
  r-speedglm
  r-survey
  r-testthat
  r-truncreg
)
optdepends=(
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('f27a713e1265ac86ab47b06a32154e44')
b2sums=('971135aed1153864f6c72f47da2b45a8df0f16f9d7154d81334df6515d77d367c5faae88887feddda1fb1848ed4527fc7f3d2254c3af78c0ba6ca875e6bdf959')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat-prediction.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
