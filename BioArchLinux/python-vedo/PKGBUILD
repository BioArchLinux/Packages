# Maintainer: Christian Holme (Nordwin) <hcmh [at] mailbox [dot] org>
_base=vedo
pkgname=python-${_base}
pkgver=2026.6.1
pkgrel=1
pkgdesc="Python module for scientific analysis and visualization of 3d objects"
arch=(any)
url="https://github.com/marcomusy/vedo"
license=(MIT)
depends=(python-numpy vtk python-typing_extensions python-pygments)
makedepends=(python-build python-installer python-wheel python-setuptools)
source=(${_base}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz)
sha512sums=('f812f63591f59edebb264ae24bb0e9e650f17be8c9807f9834193f1fb37867c3971b34e8fdd6619bf68eab0bab44aa27bed76129efe51287720017487ed4c5c9')

build() {
  cd ${_base}-${pkgver}
  python -m build --wheel --no-isolation
}


package() {
  cd ${_base}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
