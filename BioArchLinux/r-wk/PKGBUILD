# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=wk
_pkgver=0.9.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Lightweight Well-Known Geometry Parsing"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r
)
optdepends=(
  r-readr
  r-sf
  r-testthat
  r-tibble
  r-vctrs
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('347bbbd47d5c419d0238f034a942ec22')
b2sums=('d0dca10832b6da9b2bd2eebf096e11424e90feae14e22567823d95c18f2184ede063bf83a74049cd639b403da1fba5c0a9cbbe7217d3990dfd41f215f1ca807f')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
