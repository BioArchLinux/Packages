# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=wk
_pkgver=0.9.5
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Lightweight Well-Known Geometry Parsing"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r
)
optdepends=(
  r-readr
  r-sf
  r-testthat
  r-tibble
  r-vctrs
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('2f70ad0274a5d83a8a9de1dfe85da0ea')
b2sums=('1e033f89bb9e03a4b8c730ae21155299b262247a98c19323d5d8fa36204e52d1ce1e409f1fc4966b0d8b6093ed3ef1ec24a2736655c79e7efc5a7f50b6c29613')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
