# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=prismatic
_pkgver=1.1.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Color Manipulation Tools"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-farver
)
checkdepends=(
  r-testthat
)
optdepends=(
  r-covr
  r-crayon
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('9b1eb9811f9c3a938f854e5a41c5f60b')
b2sums=('3021e29f9ac38003cdb6eba8d3ce46b497d5283a534dbbf8e76e48a82b6da7a4767f1ed31ca918e3c90b4addba57663da5cafdf3b1b93060b45bb6e424bae06f')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
