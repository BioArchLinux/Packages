# Maintainer: Kiri <kiri@vern.cc>

pkgname='python-plotnine'
_module=${pkgname#python-}
pkgver=0.14.4
pkgrel=2
pkgdesc="A Grammar of Graphics for Python"
arch=('x86_64')
url="https://plotnine.readthedocs.io/en/stable"
license=('MIT')
depends=(python
         python-matplotlib
         python-mizani
         python-numpy
         python-pandas
         python-patsy
         python-scipy
         python-statsmodels
         python-setuptools-scm
         python-wheel)
makedepends=(python-build
             python-installer)
source=("${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_module::1}/$_module/${_module}-$pkgver.tar.gz")
sha256sums=('634d7168bf6f5c97e810083718aaa4330fb10f32e0e3828746a678cbd461305a')

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_module}-${pkgver}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python-plotnine/LICENSE"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
