# Maintainer: sukanka <su975853527@gmail.com>

_pkgname=Seqinfo
_pkgver=1.0.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//[:-]/.}
pkgrel=1
pkgdesc='A simple S4 class for storing basic information about a collection of genomic sequences'
arch=('any')
url="https://bioconductor.org/packages/${_pkgname}"
license=('Artistic2.0')
depends=(
  r
  r-biocgenerics
  r-iranges
  r-s4vectors
)
optdepends=(
  r-biocstyle
  r-bsgenome
  r-bsgenome.celegans.ucsc.ce2
  r-bsgenome.hsapiens.ucsc.hg38
  r-genomeinfodb
  r-genomicfeatures
  r-genomicranges
  r-knitr
  r-rmarkdown
  r-runit
  r-txdb.dmelanogaster.ucsc.dm3.ensgene
  r-txdb.hsapiens.ucsc.hg38.knowngene
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('10585f976146cbcf32a4ce7d46a18709')
sha256sums=('d72c95a5593ef0c5e343baa7d935583540abdc30c6f4d4c9178ecc8d89234d82')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
}
# vim:set ts=2 sw=2 et:
