# guoyi

pkgbase=gatb-core
pkgname=(gatb-core gatb-core-doc)
_pkgname=gatb-core
pkgver=1.4.2
pkgrel=1
pkgdesc="The Genome Analysis Toolbox with de-Bruijn graph"
arch=('x86_64')
url="https://github.com/GATB/gatb-core/tree/master"
license=('AGPL')
depends=('gcc-libs')
makedepends=('gcc' 'make' 'cmake' 'doxygen' 'hdf5' 'boost' 'graphviz')

source=(${_pkgname}-${pkgver}.tar.gz::https://github.com/GATB/gatb-core/archive/refs/tags/v${pkgver}.tar.gz
"gatb.patch"
)
sha256sums=('824c84a3712973746b977a9d49923fd499021a894225231100eaad1a66e9742d'
            '8d5d82352cbd7680c2c560d9de92009815ea13aa05c10ae99b9bfac13d0a7f8d')

prepare(){
  cd ${srcdir}/gatb-core-${pkgver}/gatb-core/src
  mkdir build src
  cp -rf ../thirdparty/{BooPHF,json} src
  cp -rf  gatb src
  rm -rf gatb
  patch -p1 < ${srcdir}/gatb.patch
  # fix for hdf5
  find src -type f -print0 | xargs -0 sed -i "s|hdf5/hdf5.h|hdf5.h|g"
}
build() {
  cd ${srcdir}/gatb-core-${pkgver}/gatb-core/src/build
  cmake .. -DINT128_FOUND=0 -Dgatb-core-version=${pkgver} \
    -Dgatb-core-date=$(date -I) -Dgatb-core-flags="-std=c++11 -Wno-invalid-offsetof" \
    -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make

  # doc
  cd ${srcdir}/gatb-core-${pkgver}/gatb-core/doc
  cmake . -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr/share/doc/${pkgbase}
  make doc
}

package_gatb-core() {
  cd ${srcdir}/gatb-core-${pkgver}/gatb-core/src/build
  make install DESTDIR=${pkgdir}
}
package_gatb-core-doc() {
  arch=('any')
  pkgdesc+=" Documentation"
  depends=()

  cd ${srcdir}/gatb-core-${pkgver}/gatb-core/doc
  # fix bugs for CMakelists.txt
  mkdir doc
  mv html doc

  make install DESTDIR=${pkgdir}
  cd ${pkgdir}/usr/share/doc/${pkgbase}
  find doc/html -type f -print0 | xargs -0 sed -i "s|$srcdir||g"
  mv doc/html ./
  rmdir doc
}
