# Contributor: Bipin Kumar <kbipinkumar@pm.me>
# Maintainer: Ronny Lorenz <ronny at tbi dot univie dot ac dot at>

pkgname=infernal
pkgver=1.1.5
pkgrel=3
pkgdesc="Tool for searching DNA sequence databases for RNA structure and sequence similarities"
arch=('x86_64')
url="http://eddylab.org/infernal/"
license=('BSD')
depends=('glibc' 'gsl' 'openmpi')
makedepends=('python')
provides=('easel')
source=("$pkgname-$pkgver.tar.gz::http://eddylab.org/infernal/infernal-$pkgver.tar.gz")
sha256sums=('ad4ddae02f924ca7c85bc8c4a79c9f875af8df96aeb726702fa985cbe752497f')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix="/usr" --with-gsl --enable-mpi --pdfdir=/usr/share/doc/${pkgname}
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check

}
package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -Dm644 LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}
  cp Userguide.pdf "$pkgdir"/usr/share/doc/infernal
  cd easel
  make DESTDIR="$pkgdir/" install
}
