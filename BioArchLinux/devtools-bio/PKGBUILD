pkgname=devtools-bio
pkgver=2022.06.11
pkgrel=1
pkgdesc='Tools for BioArchLinux Community package maintainers'
arch=('any')
url='https://github.com/BioArchLinux/devtools-bio'
license=('GPL')
depends=('devtools')
makedepends=('cmake' 'git')
provides=('devtools-bio')
conflicts=('devtools-bio')
source=('git+https://github.com/BioArchLinux/devtools-bio.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/devtools-bio"
  printf $(TZ=UTC git log --no-walk --pretty="%cd" --decorate=full --date=format-local:%Y.%m.%d | head -n 1)
}

build() {
  cd devtools-bio
  mkdir -p build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd devtools-bio/build

  make DESTDIR="${pkgdir}" install
}
