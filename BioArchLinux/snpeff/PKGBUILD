# Maintainer: Bipin Kumar <bipin@ccmb.res.in>
# Previous Maintainer: Clint Valentine <valentine.clint@gmail.com>
pkgname=snpeff
_pkgname=SnpEff
_pkgname2=SnpSift
pkgver=5.1
pkgrel=1
pkgdesc="Genetic variant annotation and effect prediction toolbox. doi:10.4161/fly.19695"
arch=('any')
url="https://pcingola.github.io/SnpEff/"
license=('MIT')
depends=('java-runtime>=12' 'python' 'perl' 'antlr4')
makedepends=('maven' 'java-environment>=12')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/pcingola/SnpEff/archive/refs/tags/v$pkgver.tar.gz"
        "$_pkgname2-$pkgver.tar.gz::https://github.com/pcingola/SnpSift/archive/refs/tags/v$pkgver.tar.gz"
        "snpSift.sh"
        "snpEff.sh"
       )
sha512sums=('eb12a126f71536eb04132e40b5f3e0bea33b6512dce999a70e5c170d2839d739ea8b52e5b38822e83c544693aec7ece20f13674634ae3971a6f08659dc7b9640'
            '074139695584227d3e523962b81ed1cd1687a146b973ec761e5c42fccf865b6f01bd8fc1280a1f73e7c9ab3292bfc3358d56a572f0795bd05fa879011a4ad950'
            '9ac75d90d8005534ea67ad0cecc4babba69b6c0db719964393d8524dd2867d6fb51082ab1b12ee8cd148e9e89d31c26266f36d9856070df431a7ecd3f8992962'
            'b0ea0b205a24d0a90775a936d52cef0f55feaed4cc1038ef9a5233353c0589dce1af2a298d28dc05057a3ffe0a28cdc1f615af9dc6735ed2f2583f9fc4b0f640')
build() {
	# Build SnpEff
    cd "${srcdir}/$_pkgname-$pkgver"
    mvn -e clean compile assembly:single jar:jar
    
    # Install SnpEff.jar file in local Maven repo as dependency for building SnpSift
    mvn install:install-file \
	 -Dfile=target/SnpEff-$pkgver.jar \
	 -DgroupId=org.snpeff \
	 -DartifactId=SnpEff \
	 -Dversion=$pkgver \
	 -Dpackaging=jar \
	 -DgeneratePom=true \
	 --quiet

    # Build SnpSift
    cd "${srcdir}/$_pkgname2-$pkgver"
    mvn -e clean compile assembly:single jar:jar
    
}

package() {
    install -Dm644 ${srcdir}/$_pkgname-$pkgver/target/SnpEff-$pkgver-jar-with-dependencies.jar  "${pkgdir}"/usr/share/java/snpEff/snpEff.jar
    install -Dm644 ${srcdir}/$_pkgname2-$pkgver/target/SnpSift-$pkgver-jar-with-dependencies.jar  "${pkgdir}"/usr/share/java/snpEff/snpSift.jar
    install -Dm644 ${srcdir}/$_pkgname-$pkgver/config/snpEff.config "${pkgdir}"/usr/share/java/snpEff/snpEff.config
    install -Dm644 ${srcdir}/$_pkgname-$pkgver/LICENSE.md "${pkgdir}"/usr/share/licenses/snpEff/LICENSE.md
    install -Dm755 snpEff.sh "${pkgdir}"/usr/bin/snpEff
    install -Dm755 snpSift.sh "${pkgdir}"/usr/bin/snpSift
}
