# Maintainer: Michael Schubert <mschu.dev at gmail> github.com/mschubert/PKGBUILDs
# Contributor: Mick Elliot <micke at sfu dot ca>
pkgname=ncbi-tools++
pkgver=29.2.0
_pkgver=${pkgver//./_}
pkgrel=4
pkgdesc="Applications, scripts and C++ libraries for the analysis of biological data"
arch=('i686' 'x86_64')
url="https://ncbi.github.io/cxx-toolkit/"
license=('NCBI-PD')
depends=('python' 'pcre' 'sqlite' 'lzo' 'libtiff' 'giflib' 'libxpm' 'glibc' 'gcc-libs'
         'mariadb-libs' 'libpng' 'gnutls' 'libxml2' 'libxslt' 'lmdb' 'gnutls' 'openssl' 'krb5' 'mbedtls' 'hdf5' 'bzip2' 'zlib' 'lzo' 'zstd' 'bash' 'libjpeg-turbo' 'e2fsprogs' 'perl')
optdepends=('fltk' 'wxgtk' 'fcgi')
makedepends=('autoconf' 'cpio')
provides=('blast+-bin' 'blast+')
conflicts=('blast+-bin' 'blast+')
source=(https://github.com/ncbi/ncbi-cxx-toolkit-public/archive/refs/tags/release/${pkgver}.tar.gz
        LICENSE)
sha256sums=('4bb2701b2cc8b4b29b5cde10088142d3c71c0ffd59e5f3402a1a6eec3d07db7e'
            '78bbf3f310ff43f1b5f711e7221d51da1e6f055831bd6c6941e0650bf1261df2')

build() {
  # https://ncbi.github.io/cxx-toolkit/pages/ch_config#ch_config.UNIX
  FLAGS="\
    --without-debug \
    --with-bin-release \
    --with-dll \
    --without-static \
    --with-optimization \
    --with-autodep \
    --with-mt \
    --prefix=/usr \
    --with-build-root=$srcdir/build
    --without-bdb \
    --without-internal \
    --with-lmdb=/usr/lib \
    --with-hdf5=/usr/lib \
    --with-libxslt=/usr/lib \
    --with-z=/usr/lib \
    --with-bz2=/usr/lib \
    --with-lzo=/usr/lib \
    --with-zstd=/usr/lib \
    --with-pcre=/usr/lib \
    --with-gnutls=/usr/lib \
    --with-openssl=/usr/lib \
    --with-krb5=/usr/lib \
    --without-boost" # current version in repo not compatible

  if [ $CARCH = "x86_64" ]; then
    FLAGS="$FLAGS --with-64"
  fi

  cd "$srcdir/ncbi-cxx-toolkit-public-release-${pkgver}"
  rm -f "$srcdir"/build/inc/ncbiconf_unix.h # in case of previous build
  ./configure $FLAGS
  make
}

package() {
  cd "$srcdir/ncbi-cxx-toolkit-public-release-${pkgver}"
  install -Dm644 "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/license
  make prefix="$pkgdir"/usr libdir="$pkgdir"/usr/lib install
  #make DESTDIR="$pkgdir" install
  find "$pkgdir"/usr/bin -name "*test*" -delete
  # Remove all empty symlnks to static libs
  find ${pkgdir}/usr/lib/ -type l ! -exec test -e {} \; -delete
  # avoid conflict with proj
  rm ${pkgdir}/usr/lib/libproj.so
}
