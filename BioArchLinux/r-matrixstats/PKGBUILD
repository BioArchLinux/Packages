# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=matrixStats
_pkgver=1.4.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Functions that Apply to Rows and Columns of Matrices (and to Vectors)"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('Artistic-2.0')
depends=(
  r
)
optdepends=(
  r-base64enc
  r-ggplot2
  r-knitr
  r-markdown
  r-microbenchmark
  r-r.devices
  r-r.rsp
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('4f04ea3fe4621f5c9e0fee8544289ecf')
b2sums=('e9c11c980d2d6aab795643257fef34d0cdc7a2fec7a74d430b9723d8738c61ae368509a02aef6b146242f33ccff58fb996bed7408edc3d88956e60e06d9c78b1')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
