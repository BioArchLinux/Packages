# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=matrixStats
_pkgver=1.2.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Functions that Apply to Rows and Columns of Matrices (and to Vectors)"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('Artistic-2.0')
depends=(
  r
)
optdepends=(
  r-base64enc
  r-ggplot2
  r-knitr
  r-markdown
  r-microbenchmark
  r-r.devices
  r-r.rsp
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('2b68a4df8f1d2199ad9feb9b5a73d219')
b2sums=('847f435794c8e78d6cc82ae0079bd61fd6d52a82102b792ea22b19e2945afd7c8691388a866ce0c456a5b7e16e119298fa153067106a88e410b85b91ef537748')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
