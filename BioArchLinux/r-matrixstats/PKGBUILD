# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=matrixStats
_pkgver=0.62.0
pkgname=r-${_pkgname,,}
pkgver=0.62.0
pkgrel=1
pkgdesc='Functions that Apply to Rows and Columns of Matrices (and to Vectors)'
arch=('x86_64')
url="https://cran.r-project.org/package=${_pkgname}"
license=('Artistic2.0')
depends=(
  r
)
optdepends=(
  r-base64enc
  r-ggplot2
  r-knitr
  r-markdown
  r-microbenchmark
  r-r.devices
  r-r.rsp
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('85e2016b6dd20cbfe32d38a2ef2578ae80e688d9a3590aefd1d2f4bf4bd44eca')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
}
# vim:set ts=2 sw=2 et:
