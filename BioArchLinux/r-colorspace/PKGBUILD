# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>
# Contributor: Alex Branham <branham@utexas.edu>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

_pkgname=colorspace
_pkgver=2.1-0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=5
pkgdesc="A Toolbox for Manipulating and Assessing Colors and Palettes"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('BSD-3-Clause')
depends=(
  r
)
optdepends=(
  r-dplyr
  r-ggplot2
  r-jpeg
  r-kernlab
  r-knitr
  r-mvtnorm
  r-png
  r-rcartocolor
  r-rcolorbrewer
  r-rmarkdown
  r-scales
  r-scico
  r-shiny
  r-shinyjs
  r-vcd
  r-viridis
  r-wesanderson
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('e386fecdf186c037558e2e73b1f151e8')
b2sums=('942cadbdf54545aa596a6aeedc520bb7c1738bd663c1e172f4e1336bb778f6a167f01d74950ece04823490437f9bdae930e92946c9c0ec5dbc04ab8e35c981b3')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
