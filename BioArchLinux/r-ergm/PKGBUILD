# Maintainer: Shun Wang <shuonwang at gmail dot com>
# Contributor: Shun Wang <shuonwang at gmail dot com>

_pkgname=ergm
_pkgver=4.11.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Fit, Simulate and Diagnose Exponential-Family Models for Networks"
arch=('x86_64')
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-3.0-only')
depends=(
  r-network
  r-robustbase
  r-coda
  r-trust
  r-lpsolveapi
  r-statnet.common
  r-rle
  r-purrr
  r-rlang
  r-memoise
  r-tibble
  r-magrittr
  r-rdpack
  r-knitr
  r-stringr
)
optdepends=(
  r-latticeextra
  r-sna
  r-rmarkdown
  r-testthat
  r-ergm.count
  r-withr
  r-covr
  r-rglpk 
  r-slam
  r-networklite
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('2fa5e60730e170ebc89cacff944dedb9')
b2sums=('1989b5842ee771baebaa38d779eaab90ba42f790571151f745e2544e27afd33d8eed797e190656b1547e0d417df80a2c73284bcbd7d6accc3c902308c937a617')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENCE" "$pkgdir/usr/share/licenses/$pkgname"
}
