# Maintainer: Shun Wang <shuonwang at gmail dot com>
# Contributor: Shun Wang <shuonwang at gmail dot com>

_pkgname=ergm
_pkgver=4.7.5
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Fit, Simulate and Diagnose Exponential-Family Models for Networks"
arch=('x86_64')
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-3.0-only')
depends=(
  r-network
  r-robustbase
  r-coda
  r-trust
  r-lpsolveapi
  r-statnet.common
  r-rle
  r-purrr
  r-rlang
  r-memoise
  r-tibble
  r-magrittr
  r-rdpack
  r-knitr
  r-stringr
)
optdepends=(
  r-latticeextra
  r-sna
  r-rmarkdown
  r-testthat
  r-ergm.count
  r-withr
  r-covr
  r-rglpk 
  r-slam
  r-networklite
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('a3f51a8588d2bcec0ba6339a76be2cd2')
b2sums=('58f9e38782261b7162493a8013d56dc95d183d21bb775bac28a4a8e11db4bd243352d81ce10c236f7b918aa6e0170f2a8b75984c9356888744d78e8c9cf42bcb')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENCE" "$pkgdir/usr/share/licenses/$pkgname"
}
