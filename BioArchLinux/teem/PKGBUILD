# Maintainer of this PKGBUILD file: Martino Pilia <martino.pilia@gmail.com>
# Contributor: bartus <szczepaniak.bartek+github@gmail.com>
pkgname=teem
pkgver=1.11.0
pkgrel=6
pkgdesc='Group of libraries for processing scientific raster data.'
arch=('x86_64')
url='http://teem.sourceforge.net/index.html'
license=('LGPL-2.1-or-later')
depends=('libpng')
makedepends=('cmake' 'make')
source=(
    "https://sourceforge.net/projects/teem/files/teem/${pkgver}/teem-${pkgver}-src.tar.gz/download"
    "cmake.patch"
    "https://github.com/Kitware/CMake/releases/download/v3.31.6/cmake-3.31.6-linux-x86_64.tar.gz"
)
sha256sums=('a01386021dfa802b3e7b4defced2f3c8235860d500c1fa2f347483775d4c8def'
            'e7dac798204c7a832839f52a7e339d46ed3207a6c762c2e4c8b12e842685c66c'
            '5a1133ff103c71eb5120e2cc3de922733e7d8a26a98ae716397e8676adb367bf')
prepare() {
    cd "$srcdir/$pkgname-$pkgver-src"

    # Fix CMake config to install CMake files in the right place
    patch -Np1 -i "$srcdir"/cmake.patch

    mkdir build || :
    cd build

    export PATH=${srcdir}/cmake-3.31.6-linux-x86_64/bin:${PATH}

    cmake \
        -DCMAKE_INSTALL_PREFIX:PATH="/usr" \
        -DCMAKE_BUILD_TYPE:STRING=Release \
        -DBUILD_SHARED_LIBS:BOOL=ON \
	-DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
        ..
}

build() {
    cd "$srcdir/$pkgname-$pkgver-src/build"
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver-src/build"
    make install DESTDIR="$pkgdir"

    install -D -m644 \
        "${srcdir}/$pkgname-$pkgver-src/LICENSE.txt" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

