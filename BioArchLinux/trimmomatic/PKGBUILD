# Maintainer: ZJLIN <zjin841126@gmail.com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>

pkgname=trimmomatic
_pkgname=Trimmomatic
pkgver=0.39
pkgrel=8
pkgdesc='A flexible read trimming tool for Illumina NGS data'
arch=('any')
url="http://www.usadellab.org/cms/?page=trimmomatic"
license=('GPL3')
depends=('java-runtime')
makedepends=('java-environment=11' 'ant')
source=("https://github.com/usadellab/Trimmomatic/archive/refs/tags/v${pkgver}.tar.gz"
        "$pkgname"
	"$pkgname.patch"
        )
md5sums=('2f80c01dd0876f7761b33ed77768a76c'
         '0716cd4695be3e8ea6d4a5aa78f7eb85'
         '4612f0187c29f7be68433778f884a067')
prepare() {
  cd $_pkgname-$pkgver
  patch -N -i $srcdir/$pkgname.patch
}
build() {
  cd $_pkgname-$pkgver
  ant
}
package() {
  install -Dm 755 $srcdir/$_pkgname-$pkgver/dist/jar/$pkgname-$pkgver.jar $pkgdir/usr/share/java/$pkgname/$pkgname.jar
  install -Dm 755 $srcdir/$pkgname -t  $pkgdir/usr/bin

  mkdir -p $pkgdir/usr/share/java/$pkgname/adapters
  install -Dm 755 -t $pkgdir/usr/share/java/$pkgname/adapters $srcdir/$_pkgname-$pkgver/adapters/*
}
