# Maintainer: Lorenzo Gaifas <brisvag at gmail dot com>

_name=pymol-open-source
pkgname=pymol
pkgver=2.5.0
pkgrel=4
pkgdesc="Molecular visualization system on an Open Source foundation"
arch=('x86_64')
url="https://pymol.org/"
license=('custom')
depends=('freetype2' 'glew' 'glm' 'glut' 'python-numpy' 'tcsh')
makedepends=('desktop-file-utils' 'gendesk' 'libmmtf' 'msgpack-c' 'netcdf' 'git')
optdepends=('python-pmw: pmw based UI'
            'python-pyqt5: Qt5 based UI'
			'ffmpeg: MPEG encoding')
conflicts=('pymol')
provides=("${_name}=${pkgver}")
source=("https://github.com/schrodinger/pymol-open-source/archive/refs/tags/v${pkgver}.zip"
	${pkgname}.png::"https://c.fsdn.com/allura/p/pymol/icon")
md5sums=('233dec168414cff2d7ae89ae7d8fb875'
         'a6b62ae41658a772f75b3b123a8e5c0b')

prepare() {
  # create desktop file
  gendesk -f -n --pkgname "${_name}" --pkgdesc "${pkgdesc}" \
          --name="PyMOL Molecular Graphics System" \
          --categories="Science;Chemistry"

  # suppress non-zero exit code that breaks makepkg
  sed -i '/sys.exit/ s,2,0,' "${srcdir}/${_name}-${pkgver}/setup.py"
}

build() {
  cd "${srcdir}/${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_name}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/pymol-open-source.desktop" "${pkgdir}/usr/share/applications/pymol.desktop"
  install -Dm644 "${srcdir}/pymol.png" "${pkgdir}/usr/share/pixmaps/pymol.png"
}
