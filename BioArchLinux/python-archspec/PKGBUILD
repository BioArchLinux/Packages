# Maintainer: arielzn <arielzn@riseup.net>

pkgbase='python-archspec'
pkgname=('python-archspec')
_module='archspec'
pkgver='0.2.1'
pkgrel=1
pkgdesc="A library for detecting, labeling, and reasoning about microarchitectures"
url="https://github.com/archspec/archspec"
depends=('python' 'python-setuptools'
         'python-click>=8.0' 'python-click<9.0'
         'python-six>=1.13.0' 'python-six<2.0.0')
makedepends=('cython' 'python-build' 'python-installer' 'python-wheel' 'python-poetry')
license=('MIT')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_module::1}/$_module/$_module-$pkgver.tar.gz")
sha256sums=('0974a8a95831d2d43cce906c5b79a35d5fd2bf9be478b0e3b7d83ccc51ac815e')

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_module}-${pkgver}"
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm644 LICENSE-MIT "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
