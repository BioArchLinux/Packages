# Maintainer: guoyi
pkgname=bioarchlinux-packages
_pkgname=archlinuxcn-packages
pkgver=0.2.7
pkgrel=2
pkgdesc="build status site"
arch=('x86_64')
url="https://github.com/BioArchLinux/bioarchlinux-packages"
depends=()
epoch=1
makedepends=('rust' 'git')
source=(git+https://github.com/imlonghao/archlinuxcn-packages.git#tag=v$pkgver
	$pkgname.service
	cargo.patch
	current_html.patch
	index_html.patch
	log_html.patch
	rust.patch)
sha256sums=('SKIP'
            'dd2a46ad3b1e7d532b53f10ced77fbd74924c8635ff3778d62a42f0606295d2c'
            '06336c1294cea451281abe8965767d369b6d36738cabea29e490f100740548f3'
            '9688e901488b8c42a71153374c2e8b8dfbc2fb6333df1bb0962f880a98ac3b4f'
            '3e9cad1eecc70b66acc72aeb4886e0ed6df55aa66e60031807946652a8e0a01a'
            'cf8dd25936b59daceea24f867fdf0c3fe5681ce84f7248ed867c245385211576'
            '9e610a2eea82854413685027c4fee3eac8e0a2593d825577e005789c205e8b45')

prepare() {
  cd $_pkgname
for pat in cargo  current_html  index_html  log_html  rust 
do
  patch -p1 < $srcdir/$pat.patch
done
}

build() {
  cd "$_pkgname"/src
  cargo build --release --all-features
}

package() {
  install -Dm755 $pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
  cd "$_pkgname"
  install -Dm755 "target/release/$pkgname" $pkgdir/usr/bin/$pkgname
  install -dm755 "$pkgdir"/usr/share/webapps/${pkgname}
  cd html
  cp *.html $pkgdir/usr/share/webapps/$pkgname/
}

