# Maintainer:  Mick Elliot <m.g.elliot@rug.nl>
# Contributor: Mick Elliot <m.g.elliot@rug.nl>

pkgname=armadillo
sfname=arma
pkgver=14.0.2
pkgrel=2
pkgdesc="C++ library for linear algebra & scientific computing"
arch=('x86_64')
url="http://${sfname}.sourceforge.net/"
license=('Apache-2.0')
depends=(openblas lapack)
makedepends=(cmake)
source=("http://sourceforge.net/projects/${sfname}/files/${pkgname}-${pkgver}.tar.xz")

sha256sums=('248e2535fc092add6cb7dea94fc86ae1c463bda39e46fd82d2a7165c1c197dff')

build() {
  cd "${pkgname}-${pkgver}"
  cmake . -DCMAKE_INSTALL_PREFIX="/usr"
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
