# Maintainer: Sedeer el-Showk <s.elshowk@netherrealm.net>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>

pkgname=fastqc
_pkgname=FastQC
pkgver=0.11.9
pkgrel=7
pkgdesc='A quality control tool for high throughput sequence data.'
arch=('any')
url="http://www.bioinformatics.babraham.ac.uk/projects/fastqc"
license=('GPL3')
depends=('perl' 'java-runtime' 'ttf-dejavu')
makedepends=('ant' 'java-environment')
source=("$pkgname-$pkgver::https://github.com/s-andrews/FastQC/archive/refs/tags/v${pkgver}.tar.gz")
md5sums=('f283dc975895766c5c74e18e2e8c25fa')
build() {
  cd $_pkgname-$pkgver
  ant
}
package() {
  mkdir -p $pkgdir/usr/share/$pkgname/
  cp -r $srcdir/$_pkgname-$pkgver/bin/* $pkgdir/usr/share/$pkgname/

  mkdir -p "${pkgdir}/usr/bin/"
  chmod 755 $pkgdir/usr/share/$pkgname/fastqc
  ln -s "/usr/share/$pkgname/fastqc" "${pkgdir}/usr/bin/"
}
