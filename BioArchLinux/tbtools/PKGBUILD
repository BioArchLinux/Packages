# Maintainer: Kiri <kiri@vern.cc>
# Contributor: wangzhichao <wangzhichao1990@126.com>

pkgname=tbtools
_pkgname=TBtools
pkgver=2.005
pkgrel=1
epoch=1
pkgdesc='GUI/CommandLine Tool Box for biologistists to utilize NGS data. https://doi.org/10.1016/j.molp.2020.06.009'
arch=('x86_64')
url='https://github.com/CJ-Chen/TBtools'
license=('unknown')
depends=('java-runtime'
         'bash')
makedepends=('unzip'
             'gendesk')
optdepends=('blast+: BLAST support'
            'diamond: quick blast(needed in quick MCScanx scan) support'
            'mcscanx: collinear scan support'
            'hmmer: HMM Search support'
            'muscle: align support'
            'trimal: large scale align support'
            'iqtree: maximum likelihood phylogenomic tree support'
            'kaks_calculator: kaks calculate support')
source=("https://github.com/CJ-Chen/TBtools/releases/download/${pkgver}/TBtools_JRE1.6.jar"
        "LICENSE")
sha256sums=('773660edb7de09dee7967486ed539617650ad30cc5232a74ed2c8f130ab733af'
            'ea019d305df02a42515d8fdd378158503fb49349e3c10aa94569e7f301f72c1f')

prepare() {
    # generate /usr/bin file
    printf '#!/usr/bin/sh\nexec java -jar /usr/share/tbtools/tbtools*.jar "$@"\n'>${srcdir}/${pkgname}
    # generate desk entry
    gendesk -f -n --pkgname "${pkgname}" --pkgdesc "${pkgdesc}" --exec="${pkgname}" --name="Tool Box for biologistists" --categories="Science"
}

package() {
    install -Dm644 ${srcdir}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm 755 ${srcdir}/${pkgname} ${pkgdir}/usr/bin/${pkgname}
    install -Dm 644 ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
    install -Dm 755 ${srcdir}/TBtools*.jar ${pkgdir}/usr/share/${pkgname}/${pkgname}.jar
    install -Dm 644 ${srcdir}/Icons/TBtoolsLogo.png ${pkgdir}/usr/share/icons/hicolor/720x720/apps/${pkgname}.png
    install -Dm 644 ${srcdir}/Icons/TBtoolsLogo16x16.png ${pkgdir}/usr/share/icons/hicolor/16x16/apps/${pkgname}.png
}
