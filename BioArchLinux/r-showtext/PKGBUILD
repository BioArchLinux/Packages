# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Alexander Bocken <alexander@bocken.org>

_pkgname=showtext
_pkgver=0.9-7
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Using Fonts More Easily in R Graphs"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=(Apache)
depends=(
  freetype2
  r-showtextdb
  r-sysfonts
)
optdepends=(
  r-curl
  r-jsonlite
  r-knitr
  r-prettydoc
  r-rmarkdown
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('b0ace7cd37d2fda8abeba7055be73c39')
b2sums=('8b2daf5997041a3744460582a0709c6ab72fc1ea9ad624e7784e430088630a9ffcfd3f25c386fa02a43850294fad98b1e9410e42c7f8482f48afa449592edfac')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
