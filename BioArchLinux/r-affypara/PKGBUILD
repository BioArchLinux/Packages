# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>
_pkgname=affyPara
_pkgver=1.54.0
pkgname=r-${_pkgname,,}
pkgver=1.54.0
pkgrel=6
pkgdesc='Sample size for RNAseq studies'
arch=('any')
url="https://bioconductor.org/packages/3.14/${_pkgname}"
license=('GPL3')
depends=(
  r
  r-affy
  r-snow
  r-vsn
  r-aplpack
  r-affyio
  tk
)
makedepends=(
  git
  tar
)
optdepends=(
  r-affy
  r-affydata
)
source=("git+https://git.bioconductor.org/packages/${_pkgname}.git")
sha256sums=('SKIP')

prepare(){
  # remove 22c `assign(".affyParaInternalEnv", .affyParaInternalEnv, envir=topenv(parent.frame()))`
  # adapted from
  # https://github.com/tidyverse/ggplot2/blob/7ebb6bd9d664f58794078afdb5a8ff71d5edf96d/R/ggplot-global.R#L4
  sed -i '/assign(".affyParaInternalEnv/d' ${_pkgname}/R/zzz.R

  # mv .affyParaInternalEnv <- new.env(parent=emptyenv()) to top of the file.
  grep -E '^\s+.affyParaInternalEnv' ${_pkgname}/R/zzz.R  > tmpzzz.R
  grep -vE '^\s+.affyParaInternalEnv' ${_pkgname}/R/zzz.R >> tmpzzz.R
  mv tmpzzz.R ${_pkgname}/R/zzz.R
}

build() {
  tar -zcvf ${_pkgname}.tar.gz  ${_pkgname}
  R CMD INSTALL ${_pkgname}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
}
# vim:set ts=2 sw=2 et:
