# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=fabricatr
_pkgver=1.0.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=3
pkgdesc="Imagine Your Data Before You Collect It"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=(MIT)
depends=(
  r-rlang
)
checkdepends=(
  r-testthat
)
optdepends=(
  r-data.table
  r-extradistr
  r-mvnfast
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('9a30e41e08bd0098b6183dcbfafaeed8')
b2sums=('3beac1ea9f5ce30e0018e23c0f5d2a3de8e1eebf8eaf162942fb07466263771c3aa81bac6796fc9f7ba9336e9077a9c969f0183306a8622ac689373d61627057')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
