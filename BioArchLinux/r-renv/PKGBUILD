# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=renv
_pkgver=1.0.11
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Project Environments"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r
)
optdepends=(
  r-biocmanager
  r-cli
  r-covr
  r-cpp11
  r-devtools
  r-gitcreds
  r-jsonlite
  r-jsonvalidate
  r-knitr
  r-miniui
  r-modules
  r-packrat
  r-pak
  r-r6
  r-remotes
  r-reticulate
  r-rmarkdown
  r-rstudioapi
  r-shiny
  r-testthat
  r-uuid
  r-waldo
  r-webfakes
  r-yaml
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('5d143e5f534dc987a657762cc48f09c5')
b2sums=('b9645a05c1e27d8c50818929fc09d3936e88edb50abfbe203cfc43efdea743bd5780427aa4db83abadeaf4dfb762206ed85db83fbe3702361a72b6f9a4ca2d4b')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
