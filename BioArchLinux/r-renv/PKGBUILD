# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=renv
_pkgver=1.1.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Project Environments"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r
)
optdepends=(
  r-biocmanager
  r-cli
  r-covr
  r-cpp11
  r-devtools
  r-gitcreds
  r-jsonlite
  r-jsonvalidate
  r-knitr
  r-miniui
  r-modules
  r-packrat
  r-pak
  r-r6
  r-remotes
  r-reticulate
  r-rmarkdown
  r-rstudioapi
  r-shiny
  r-testthat
  r-uuid
  r-waldo
  r-webfakes
  r-yaml
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('15bbee6925eae3157154ef3537fc3747')
b2sums=('853e493f3b20eca80d60b81247c232979334c1afc69037500a8e72cba3ccef1d5bedd0220b2292ed61e7cf8400967d77476614e0db5d46e2604748997794b475')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
