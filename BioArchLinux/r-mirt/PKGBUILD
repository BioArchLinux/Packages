# Maintainer: Shun Wang <shuonwang at gmail dot com>

_pkgname=mirt
_pkgver=1.45.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Multidimensional Item Response Theory"
arch=('x86_64')
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-3.0-or-later')
depends=(
  r-gparotation
  r-gridextra
  r-rcpp
  r-vegan
  r-deriv
  r-pbapply
  r-dcurver
  r-simdesign
)
makedepends=(
  r-rcpparmadillo
)
optdepends=(
  r-latticeextra
  r-directlabels
  r-shiny
  r-knitr
  r-markdown
  r-rsolnp
  r-nloptr
  r-sirt
  r-plink
  r-mirtcat
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('2ea5b1110b34a9c9404baa452385b6b7')
b2sums=('62eb1a1b9d7ac9b5decfab39b01bfd9f93cbe2247bc46fdf1d83908db3ac8e1de00f12b9bb6d9294b7a5f406735fdba8d60547cc81aa957b83f2a4303f28f41f')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
