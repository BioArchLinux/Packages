# Maintainer: PumpkinCheshire <sollyonzou at gmail dot com>

pkgname=bwa
pkgver=0.7.18.r0.g79b230d
pkgrel=1
pkgdesc="Burrows-Wheeler Aligner maps low-divergent sequences against a large reference genome. https://doi.org/10.1093/bioinformatics/btp324"
arch=('x86_64')
url="http://bio-bwa.sourceforge.net/"
license=('GPL3')
depends=('perl' 'zlib')
makedepends=('git' 'make')
source=('git+https://github.com/lh3/bwa.git')
sha256sums=('SKIP')

pkgver() {
    git -C ${pkgname} describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/${pkgname}"
    sed -i "/CFLAGS=/ s|$| -fPIC|" Makefile
    make
    install -Dm644 libbwa.a lib/libbwa.a
    cd lib
    ar -x libbwa.a
    gcc -shared *.o -o libbwa.so
}

package() {
    install -Dm755 -t "${pkgdir}/usr/bin" ${srcdir}/${pkgname}/bwa
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" ${pkgname}/COPYING
    cd $srcdir/${pkgname}
    install -Dm644 *.h -t "${pkgdir}/usr/include/bwa"
    install -Dm755 lib/libbwa.so -t "${pkgdir}/usr/lib"
}
