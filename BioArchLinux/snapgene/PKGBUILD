# Based on snapgene-viewer package by Antony Lee <anntzer dot lee at gmail dot com>
# Maintainer: Bitals <me at bitals dot xyz>
# Contributor: Matthijs Tadema <M dot J dot Tadema at gmail dot com>
# Contributor: Lorenzo Gaifas <brisvag at gmail dot com>
pkgname=snapgene
pkgver=8.2.2
_pkgver_major=${pkgver%%.*}
_pkgver_major_middle=${pkgver%.*}
pkgrel=1
pkgdesc='Software for plasmid mapping, primer design, and restriction site analysis (includes free Viewer mode)'
arch=('x86_64')
url='https://www.snapgene.com/about'
license=('custom')
depends=('qt6-base'
         'qt6-webchannel'
         'qt6-5compat'
         'qt6-declarative'
         'qt6-positioning'
         'qt6-svg'
         'qt6-webengine'
         'libsm'
         'fontconfig'
         'nspr'
         'xz'
         'expat'
         'glibc'
         'gcc-libs'
         'libc++'
         'libc++abi'
         'dbus'
         'krb5'
         'libxcb'
         'libxkbcommon-x11'
         'xcb-util-image'
         'xcb-util-wm'
         'libx11'
         'libxkbfile'
         'xcb-util-keysyms'
         'xcb-util-renderutil'
         'nss'
         'hicolor-icon-theme'
         'libice'
         'libglvnd'
         'libxext'
         'openssl-1.1'
         'zlib'
         'qca-qt6'
         'freetype2'
         'libxkbcommon'
         'sh')

makedepends=('patchelf')
# A valid licence is required to use the full version of snapgene
source=("${pkgname}-${pkgver}.deb::https://cdn.snapgene.com/downloads/SnapGene/"$_pkgver_major".x/"$_pkgver_major_middle"/"$pkgver"/"$pkgname"_"$pkgver"_linux.deb"
        "LICENSE.html::https://cdn.snapgene.com/downloads/licenseAgreement.html"
        "snapgene.sh")
sha512sums=('e49f2b9897249cf671497da948cc90ebfe48e3be128da4d9e373062ce213cf24fdcb8b70151eaeed698ee95dda9703f2eca8f9755fccbc76a329a65b60c949d7'
            'c4c8cf8dd78715be9cc9c2c3ec1ce41f12029f6167b48277bdf3a5e95a2a546ec25e467e6120ed872b3a4d80e935b88f5c36e1e673289110daf61f4ba74464fe'
            'b6e85e1a3e1ed9d07159b58abf6fecfb76bfd19752b67fbf156baaab696df83aa8b0fcfc49c93263dd56ea2f47926ddc4dac0b4b011f53112426e8545d1197be')

package() {
    cd "${srcdir}"
    tar -xf data.tar.xz -C "${pkgdir}"
    
    cd "$pkgdir"
    install -Dm 755 "${srcdir}/snapgene.sh" "${pkgdir}/usr/bin/snapgene"
    install -Dm 644 "${srcdir}/LICENSE.html" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.html"

    sed -i 's|${INSTALLED_DIR}/snapgene "$@"|QT_QPA_PLATFORM="xcb" ${INSTALLED_DIR}/snapgene "$@"|' "$pkgdir/opt/gslbiotech/snapgene/snapgene.sh"

    # Fix rpath which cause "Insecure RUNPATH" or "outside of a valid path"
    # Fix .so files using patchelf
    cd "${pkgdir}/opt/gslbiotech/snapgene"
    for file in *.so*; do
        PATCHELF_OUTPUT=$(patchelf --print-rpath $file)
        echo "Checking $file: $PATCHELF_OUTPUT"
    # Skip file if PATCHELF_OUTPUT does not contain CURRENT_DIR
        if [[ ! $PATCHELF_OUTPUT =~ "/home" ]]; then
            echo "Skipping $file"
            continue
        fi
    # Remove rpath if it not contain ORIGIN before
        if [[ ! $PATCHELF_OUTPUT =~ 'ORIGIN' ]]; then
            echo "Removing rpath for $file"
            patchelf --remove-rpath $file
            continue
        fi
    # Set rpath to ORIGIN if it contain ORIGIN before
        echo "Setting ORIGIN rpath for $file"
        patchelf --set-rpath '$ORIGIN' $file
    done

    # Remove rpath of snapgene
    echo "Removing rpath for snapgene"
    patchelf --remove-rpath snapgene
}
