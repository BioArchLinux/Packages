# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=nanopolish
pkgver=0.14.0
pkgrel=8
pkgdesc="Provide signal-level analysis of Oxford Nanopore sequencing data"
arch=('x86_64')
url="https://github.com/jts/nanopolish"
license=('MIT')
depends=('glibc' 'hdf5' 'htslib' 'zlib')
makedepends=('git' 'eigen' 'wget')
source=("git+https://github.com/jts/nanopolish.git#tag=v$pkgver")
sha256sums=('4b3586040cd2a7586aed98420609299f73ddbb5ea626b473b37f572c86cd6b76')

prepare() {
  cd "nanopolish"

  git submodule update --init --recursive --remote

#  patch -Np1 -i "$srcdir/0001-Fix-include-path-for-system-installed-eigen-library.patch"
#  patch -Np1 -i "$srcdir/0002-Allow-users-to-supply-htslib-include-path-from-comma.patch"
}

build() {
  cd "nanopolish"

  make \
    EIGEN=noinstall \
    HDF5=noinstall \
    HTS=noinstall \
    EIGEN_INCLUDE="-I/usr/include/eigen3" \
    H5_INCLUDE="-I/usr/include" \
    HTS_INCLUDE="-I/usr/include/htslib"
}

check() {
  cd "nanopolish"

  make \
    EIGEN=noinstall \
    HDF5=noinstall \
    HTS=noinstall \
    EIGEN_INCLUDE="-I/usr/include/eigen3" \
    H5_INCLUDE="-I/usr/include" \
    HTS_INCLUDE="-I/usr/include/htslib" \
    test
}

package() {
  cd "nanopolish"

  install -Dm755 "nanopolish" -t "$pkgdir/usr/bin"
  install -Dm644 "LICENSE" -t "$pkgdir/usr/share/licenses/nanopolish"
}
