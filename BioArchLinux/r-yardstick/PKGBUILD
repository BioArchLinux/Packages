# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=yardstick
_pkgver=1.2.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Tidy Characterizations of Model Performance"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-cli
  r-dplyr
  r-generics
  r-hardhat
  r-lifecycle
  r-rlang
  r-tibble
  r-tidyselect
  r-vctrs
)
checkdepends=(
  r-ggplot2
  r-testthat
  r-tidyr
)
optdepends=(
  r-covr
  r-crayon
  r-ggplot2
  r-knitr
  r-probably
  r-rmarkdown
  r-testthat
  r-tidyr
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz"
        "$_pkgname-correct-expected-result.patch::https://github.com/tidymodels/yardstick/pull/429.patch")
md5sums=('d16dbb6e478b5e561cd6b6c29e0704c8'
         '386aabd0b1dfcc7ccf4e9b3109ee6030')
b2sums=('88ec0f9a6ba98e07c2fc7123c0bff7f94805ad4839f0ae8c285a50852f9d8a9bf0ab0a74c11d2269fb0edc8d2ed57970d316a5241bd912c3bebcc5fdc79c8dae'
        '907db4eb4583069f83a60defbcc994c4acad179cf7c8e6c99b38ffa09e9a5a9e6729ec316d325ee83a73da22ca18e3d20fdfab6bd5e990b6de06a16fd3e5f702')

prepare() {
  # fix tests
  patch -Np1 -d "$_pkgname" < "$_pkgname-correct-expected-result.patch"
}

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
