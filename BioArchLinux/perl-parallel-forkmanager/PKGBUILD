pkgname=perl-parallel-forkmanager
_dist='Parallel-ForkManager'
pkgver=2.02
pkgrel=1
pkgdesc='A simple parallel processing fork manager'
url='https://metacpan.org/dist/Parallel-ForkManager'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl'
)

source=("https://cpan.metacpan.org/authors/id/Y/YA/YANICK/Parallel-ForkManager-${pkgver}.tar.gz")
b2sums=('7da8a88fc8b9314d5e3e27c50267171f0503c773921ca9cbf512d5ea69d3e244fab6f4e4415c80f3aa169478492ff4f45b82c3ab317ee836d8c5f3feb8056c9e')

build() {
  cd "$srcdir/$_dist-$pkgver" || exit

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT

  export PERL_MM_USE_DEFAULT=1 \
    PERL_AUTOINSTALL=--skipdeps \
    MODULEBUILDRC=/dev/null

  /usr/bin/perl Makefile.PL
  make
}


package() {
  cd "$srcdir/$_dist-$pkgver" || exit

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT

  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
