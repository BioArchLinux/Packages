# Maintainer: Kiri <kiri@vern.cc>

pkgname='python-opentsne'
_module=openTSNE
pkgver=1.0.1
pkgrel=2
pkgdesc="Extensible, parallel implementations of t-SNE"
arch=('x86_64')
url="https://github.com/pavlin-policar/openTSNE"
license=('custom:OSI Approved')
depends=(python
         python-numpy
         python-scikit-learn
         python-scipy)
makedepends=(python-build
             python-installer
             cython
             python-oldest-supported-numpy
             python-wheel
             python-setuptools)
source=("${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_module::1}/$_module/${_module}-${pkgver}.tar.gz")
sha256sums=('51f4dffaa3366ee4a480dd21d5f64eb0fa677248a0c99490aeb8bf311124368c')

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_module}-${pkgver}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python-opentsne/LICENSE"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
