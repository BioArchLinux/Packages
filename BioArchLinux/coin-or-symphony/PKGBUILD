# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Carlos Aznar√°n <caznaranl@uni.pe>
# Contributor: Felix Golatofski <felix.golatofski@gmx.de>
# Contributor: Daniel YC Lin <dlin.tw at gmail>

_base=SYMPHONY
pkgname=coin-or-${_base,,}
pkgver=5.7.2
pkgrel=1
pkgdesc="COIN-OR solver for mixed-integer linear programs (MILPs)"
arch=(x86_64)
url="https://github.com/coin-or/$_base"
license=(EPL)
depends=(
  coin-or-cgl
  coin-or-clp
  coin-or-coinutils
  coin-or-osi
)
source=("$_base-releases-$pkgver.tar.gz::$url/archive/releases/$pkgver.tar.gz")
sha512sums=('167a8fd35d6f8fb809eeab5d986f8b3acb85d8b2094c57bfea5e060e28fd2d67e8221f779176991564bcaae79a66ed03a71aa98b61d2f89a64dc6f313ddc18fa')

build() {
  cd "$_base-releases-$pkgver/$_base"
  COIN_SKIP_PROJECTS="Sample" \
    ./configure --prefix=/usr \
    --with-osi-lib="$(pkg-config --libs osi)" \
    --with-osi-incdir="/usr/include/coin/" \
    --with-clp-lib="$(pkg-config --libs clp)" \
    --with-clp-incdir="/usr/include/coin/" \
    --with-cgl-lib="$(pkg-config --libs cgl)" \
    --with-cgl-incdir="/usr/include/coin/" \
    --with-coinutils-lib="$(pkg-config --libs coinutils)" \
    --with-coinutils-incdir="/usr/include/coin/" \
    --enable-dependency-linking \
    --enable-cbc-parallel
  # don't link with unused shared libraries
  sed -E '/SYMPHONYLIB_LIBS = / s/ -l(Cgl|OsiClp|ClpSolver|Clp|cholmod|amd|coinasl|bz2|z|glpk|lapack|blas|m)//g' \
      -i src/OsiSym/Makefile
  sed -E '/SYMPHONYLIB_LIBS = / s/ -l(ClpSolver|cholmod|amd|coinasl|bz2|z|glpk|lapack|blas)//g' \
      -i src/Makefile
  make
}

package() {
  cd "$_base-releases-$pkgver/$_base"
  make DESTDIR="$pkgdir" install
}
