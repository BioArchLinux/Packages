# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=DBItest
_pkgver=1.7.3
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Testing DBI Backends"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=(LGPL)
depends=(
  r-blob
  r-callr
  r-dbi
  r-desc
  r-hms
  r-lubridate
  r-palmerpenguins
  r-r6
  r-rlang
  r-testthat
  r-vctrs
  r-withr
)
optdepends=(
  r-clipr
  r-dblog
  r-debugme
  r-devtools
  r-knitr
  r-lintr
  r-rmarkdown
  r-rsqlite
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('a43445d47911fa2d62ec63c653522354')
b2sums=('91f6845cfe4935e9050b90d65ce24537c19823c62dd3e24b9a877ea0f080373bf8b972c8a87d435de4ac941a1e9019fd62bebf6f840c7ea7e5ab4d3a8570a0a5')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
