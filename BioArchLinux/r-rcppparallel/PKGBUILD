# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: peippo <christoph+aur@christophfink.com>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=RcppParallel
_pkgver=5.1.8
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Parallel Programming Tools for 'Rcpp'"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-3.0-or-later')
depends=(
  onetbb
  r
)
checkdepends=(
  r-rcpp
  r-runit
)
optdepends=(
  r-knitr
  r-rcpp
  r-rmarkdown
  r-runit
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz"
        "fix-build.patch")
md5sums=('674f12ea164645734c2635ebc499a429'
         '574c2ee668e212c59d9a269b5075e7ad')
b2sums=('b7cee36c6b5f4bf93f14926780df2c2636b0149daa3e6402c9c351ecb3e28c58d3b7177ddc20cf71b95f83c5d5da3d82e8ddb3465891d38713614356f599ab77'
        '2988b3fccd4fa05160cd1883d61ca8098d911265778ce5bd0f38a71baea0fa2ca149be5c968806657e9126af5bd254e5f29fb46fb48f5cfa32401dc58205a259')

prepare() {
  # fix build with system onetbb
  patch -Np1 -i fix-build.patch
}

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname" \
      --configure-vars=TBB_ROOT=/usr
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" Rscript --vanilla doRUnit.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
