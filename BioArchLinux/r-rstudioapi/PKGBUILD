# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=rstudioapi
_pkgver=0.15.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Safely Access the RStudio API"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=(MIT)
depends=(
  r
)
optdepends=(
  r-clipr
  r-covr
  r-knitr
  r-rmarkdown
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('25e0dafe967089165efab98158a0d6da')
b2sums=('06f27d5fe31d24f964a61dc663805274f9d99be4d38c8265d66d3b9ea7e3be88cfe00ca1d478fb35b8bd118522364a075120897dbe7539f61c17ad328bc7b90e')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
