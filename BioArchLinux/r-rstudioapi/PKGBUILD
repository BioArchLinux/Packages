# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=rstudioapi
_pkgver=0.17.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Safely Access the RStudio API"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r
)
optdepends=(
  r-clipr
  r-covr
  r-knitr
  r-rmarkdown
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('e127b1fe52f148584b0ebd789e50b979')
b2sums=('bc270f0c14a859f117119ec4c866cddfce8026702ba59db635b1decee71dd6b2fa0017f5322466157bf5668f09d452041c2c5c525481d8906a079b48786f5fd0')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
