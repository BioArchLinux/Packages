# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=rstudioapi
_pkgver=0.18.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Safely Access the RStudio API"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r
)
optdepends=(
  r-clipr
  r-covr
  r-curl
  r-jsonlite
  r-knitr
  r-rmarkdown
  r-testthat
  r-withr
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('a452eb996c0fcacc913f8c72992ff750')
b2sums=('6987b36944305a8b462cf53662c48ea4b17836aa5df2da9f2e35c297cc1bfedd5a6782be0d96479f4d4fbe5fa00fbdf10174c3ed2ef9c6185e5f7462ee96b42b')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
