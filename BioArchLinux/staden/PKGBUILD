# Maintainer: Michael Schubert <mschu.dev at gmail> github.com/mschubert/PKGBUILDs
# Contributor: Stunts <f.pinamartins@gmail.com>
# Contributor: LoneWolf <lonewolf@xs4all.nl>
pkgname=staden
pkgver=2.0.0b11
_pkgname=$pkgname-$pkgver-2016-src
pkgrel=8
_tklib=0.8
pkgdesc="Tools for DNA sequence assembly (Gap4/5), editing and analysis (Spin)"
arch=('i686' 'x86_64')
url="http://staden.sourceforge.net/"
license=('BSD')
depends=('staden-io_lib' 'tklib')
optdepends=('iwidgets: gap5 prefinish'
            'xz: better gap5 compression'
            'libpng: gap5 Report Mutations')
source=(http://downloads.sourceforge.net/staden/$_pkgname.tar.gz
	gap4.desktop gap5.desktop pregap4.desktop spin.desktop trev.desktop staden.png)
options=('!emptydirs')
sha256sums=('cd7ee17626c25bc845ebd3f6311b5a538815fb73a0dfef2393001bf579ac9649'
            '5ba8c8d03500703ece0fe89ea36c5cd583a83ff6052e57e6b069d63f9b473e73'
            '4c8e4be56a4d871f9e6fa23edf33066b4b902b1fbda0849db116e67aab706832'
            '8156dad5771659203245afe517286efb420292fc3dd0dc9e1b8ad98ae695cd93'
            'dae7ddfae3c060369107faf3d4d4d4414222f41d27d12148d26a7226775743fc'
            'f7ffa06fd9529cd6abfe9b9c7353263953e081bfe26bfa3f4c1b1bee9e30f1a8'
            '820226dbc30063c92262397d0fa480a4e03efe4ae2df336a00b2b13c8d7e635d')

build() {
  cd $_pkgname
  ./configure --prefix=/usr --with-tklib=/usr/lib/tklib${_tklib}
  make
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir" install
  for gui in gap4 gap5 pregap4 spin trev
  do
     install -Dm 755 $srcdir/$gui.desktop $pkgdir/usr/share/applications/$gui.desktop
  done
  install -Dm 644 $srcdir/staden.png $pkgdir/usr/share/icons/staden.png
}
