# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=tinytex
_pkgver=0.39
pkgname=r-${_pkgname,,}
pkgver=0.39
pkgrel=3
pkgdesc='Helper Functions to Install and Maintain TeX Live, and Compile LaTeX Documents'
arch=('any')
url="https://cran.r-project.org/package=${_pkgname}"
license=('MIT')
depends=(
  r
  r-xfun
)
optdepends=(
  r-rstudioapi
  r-testit
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('f22e9b77c200fe44cc073b759c2b2bc3310a2382d897282548aa02dcbabc25ed')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
  install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
