# Maintainer: Fabian Kl√∂tzl <fabian-aur@kloetzl.info>
# Contributor:Bipin Kumar <kbipinkumar@pm.me>

pkgname=andi
pkgver=1.15
pkgrel=1
pkgdesc="program for estimating the evolutionary distance between closely related genomes"
arch=('x86_64')
url="https://github.com/EvolBioInf/andi"
license=('GPL3')
depends=('glibc' 'gsl' 'libdivsufsort' 'gcc-libs')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/EvolBioInf/andi/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('f8b731c3b5233c8401a9eddbccea94f235efe942ce88f21a19ad21d8209991fcb25c6d03a879e86d6f89b1eb82b3434bb2ae0f580582a7bc9132373e4a247753')

prepare(){
  cd "$pkgname-$pkgver"
  autoreconf -fi -Im4 
}

build() {
	cd "${pkgname}-${pkgver}"
	# ./configure --prefix=/usr
	./configure --prefix=/usr --enable-unit-tests --with-seed=123
  make
}

check() {
	cd "${pkgname}-${pkgver}"
	make check
}
package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
