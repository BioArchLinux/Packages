# Maintainer: Malacology <guoyizhang at malacology dot com>
# Contributor: Malacology <guoyizhang at malacology dot com>

pkgname=beast2
pkgver=2.7.2
pkgrel=1
pkgdesc="Bayesian Evolutionary Analysis by Sampling Trees. https://doi.org/10.1371/journal.pcbi.1003537"
arch=('x86_64')
url="http://www.beast2.org/"
license=('LGPL')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/CompEvol/beast2/archive/refs/tags/${pkgver}.tar.gz"
"git+https://github.com/CompEvol/BeastFX.git#commit=e6c9fdb2"
 "beauti.desktop" "densitree.desktop" "logcombiner.desktop" "treeannotator.desktop" "beast2.desktop"
 "loganalyser.desktop" "${pkgname}-applauncher.desktop"
 # executable
 "${pkgname}-beauti" "${pkgname}-densitree" "${pkgname}-logcombiner" "${pkgname}-treeannotator" "${pkgname}-beast2"
 "${pkgname}-loganalyser" "${pkgname}-applauncher"
)
sha256sums=('02f561f593adf54e512d34a9366dd9e4ed0b940cb4540d97a2b4e9b705f327a3'
            'SKIP'
            'f35255c946a105a60e81fcf44ed3f5c28e6903f0e9cedc015b1ae9d08ab6eb42'
            '39d8fe84e7262a11fb49700254fd54a9823ad25069f6cb9539d830c708d8864e'
            '7dc09611d85955ced66dec3f9eb63a3396e59f0bd4f93e6338478a102b01f302'
            '46c3437979fa9a836c5832f1d92c286b90950f7795903cc34051f48e7f0be403'
            'df7bbf1363eec2af224251e77cd0a025003cd0ac751163a3e43cbe2bf52870d1'
            'e4b5398d4e5fe81b0e57b2e5d3a7b1d9621c97d1b8ce40a2d5b169bd94e89397'
            '8e999cabf53742fa818504a10f326e1275e23349f73267baba9058eda6add1ec'
            'e8159c2a85ce43ea0d08ff4cf10525e5089db58a8c4f2bf9bfd0e368b4ecc099'
            'f5cc878029b9d0469fa1db8a37a5f7e510871cc24ae6a24ecc011b5560f3bf9d'
            '67785a6b1109f46203ff5f0169ed9d3f6118d72a3163e9141e10a24b1a10914f'
            'aac5216b54d037f3cff3c76fa293c6afa08ed59e3e1865804f3af84569024e1e'
            'ba9b05aac753a315c7ff373823cb752b535077209f4365a12a5e50d8742970f4'
            '371a6a5b7ae241f77ef008984cf2b004d4d2aac373fe7dbe54ae9165c36f2c70'
            '6efc50e2cf5e099f33634e702586e075fff4b34b9fc9f1a682b1c109e0343b85')
depends=('java-runtime' 'java-openjfx')
makedepends=('ant' 'git')
optdepends=('beagle-lib')
prepare(){
    cd ${srcdir}/${pkgname}-${pkgver}
    sed -i "s|2.7.1|2.7.2|" version.xml
}
build(){
    cd "$srcdir/BeastFX"
    install -Dm755 $srcdir/${pkgname}-${pkgver}/release/Linux/jrebin/* \
        -t release2/Linux/jrebin
    ant linux -nouserlib -noinput \
        -Drelease_dir=release2 \
        -Dbeast2="../${pkgname}-${pkgver}" \
        -Dbeast2path="../${pkgname}-${pkgver}" \
        -lib "/usr/lib/jvm/default-runtime/lib/" \
        -Dcommon_dir="../${pkgname}-${pkgver}/release/common/" \
        -DopenjreLnx="/usr/lib/jvm/default/"
}
package() {
    install -d "$pkgdir"/usr/share/beast2
    install -d ${pkgdir}/usr/bin/$bin
    cd "$srcdir/BeastFX/release2/Linux/"
    cp -rf beast/* "${pkgdir}/usr/share/${pkgname}"

    # correct excutable
    rm -rf $pkgdir/usr/share/beast2/bin/*
    for exe in {"beauti","densitree","logcombiner","treeannotator","beast2","loganalyser","applauncher"}
    do
        install -Dm755 $srcdir/${pkgname}-${exe} -t $pkgdir/usr/share/beast2/bin/
    done
    mv $pkgdir/usr/share/beast2/bin/${pkgname}-beast2  $pkgdir/usr/share/beast2/bin/${pkgname}

    for bin in $(ls $pkgdir/usr/share/beast2/bin)
    do
        ln -s /usr/share/beast2/bin/$bin ${pkgdir}/usr/bin/$bin
    done
    install -Dm 755 ${srcdir}/*.desktop -t ${pkgdir}/usr/share/applications

    # clear files
    rm -rf $pkgdir/usr/share/beast2/jre
    ln -sf /usr/lib/jvm/default/  $pkgdir/usr/share/beast2/jre
}
