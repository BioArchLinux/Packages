# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=R6
_pkgver=2.6.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Encapsulated Classes with Reference Semantics"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r
)
optdepends=(
  r-lobstr
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('f01b1787f12797c29194d63c9afd5d70')
b2sums=('5cd595f908bd17908e9eaf5ccacbccfd6eea794584e7e7f1ff0bf20c3286f6fd11e63b2190d85f7bd0ddd33744c1c2d53e269c2107dbda9e84885599d8a28abb')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
