# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=R6
_pkgver=2.5.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=11
pkgdesc="Encapsulated Classes with Reference Semantics"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r
)
optdepends=(
  r-pryr
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('e84fd376e246738402e5529b6d52e101')
b2sums=('0045d7c37dfb7a773cd24e85e695575eaf6aa5c9392fb6888c10aa9fb3e35b13a230ed0aabcf84d52e656ef506ab58371044c4179b3380f5324b217fae6daeb4')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
