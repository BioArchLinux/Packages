# Maintainer: malacology
# Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>
# Maintainer: lilydjwg <lilydjwg@gmail.com>

_pkgname=lilac
pkgname=$_pkgname
pkgver=2024.03.29
pkgrel=1
pkgdesc='The build bot for bioarchlinux, English version'
arch=(any)
url='https://github.com/archlinuxcn/lilac'
license=(GPL3)
depends=(python git devtools nvchecker gnupg pid_children fakeroot bubblewrap pacman-contrib
         python-requests python-lxml python-yaml python-tomli python-tomli-w pyalpm
         python-structlog python-prctl)
checkdepends=(python-pytest python-pytest-asyncio)
makedepends=(python-setuptools-scm)
optdepends=(
  'smtp-forwarder: for sending error reports'
  'python-sqlalchemy: for recording build results in a database'
)
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=('git+https://github.com/archlinuxcn/lilac.git'
	"$pkgname.patch::https://patch-diff.githubusercontent.com/raw/archlinuxcn/lilac/pull/198.patch")
sha256sums=('SKIP'
            '9bda1a1ae3b241bebf772896c37410b8c6b3afde85f95f5c9ad621a2897804a7')

pkgver() {
  cd $_pkgname
  printf $(TZ=UTC git log --no-walk --pretty="%cd" --decorate=full --date=format-local:%Y.%m.%d | head -n 1)
}

prepare(){
  cd $_pkgname
  patch -p1 < $srcdir/$pkgname.patch
}

build() {
  cd $_pkgname
  python setup.py build
}

check() {
  cd $_pkgname
  pytest
}

package() {
  cd $_pkgname
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 config.toml.sample scripts/dbsetup.sql -t "$pkgdir"/usr/share/doc/lilac
  install -Dm755 scripts/cleanup-dblck "$pkgdir"/usr/bin/cleanup-dblck
  install -Dm644 scripts/cleanup-dblck.service "$pkgdir"/usr/lib/systemd/system/cleanup-dblck.service
}


