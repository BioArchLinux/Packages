# Maintainer: malacology
# Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>
# Maintainer: lilydjwg <lilydjwg@gmail.com>

_pkgname=lilac
pkgname=$_pkgname
pkgver=2024.03.29
pkgrel=6
pkgdesc='The build bot for bioarchlinux, English version'
arch=(any)
url='https://github.com/archlinuxcn/lilac'
license=(GPL-3.0-or-later)
depends=(python git devtools nvchecker gnupg pid_children fakeroot bubblewrap pacman-contrib
         python-requests python-lxml python-yaml python-tomli python-tomli-w pyalpm
         python-structlog python-prctl)
checkdepends=(python-pytest python-pytest-asyncio)
makedepends=(python-setuptools-scm)
optdepends=(
  'smtp-forwarder: for sending error reports'
  'python-sqlalchemy: for recording build results in a database'
)
cmt=ce8f610005bec5dd8fd6836025229765ed19504c
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=("git+https://github.com/archlinuxcn/lilac.git#commit=$cmt"
	"$pkgname.patch::https://patch-diff.githubusercontent.com/raw/archlinuxcn/lilac/pull/198.patch")
sha256sums=('7c3f2ac8760fa44e38c628039e02767556b018398a75f7356274a28b15649f82'
            'c08bda4fd9e41eaf2f3f2616bdf55211be46a1029a6b6f2b05a1ce550ce9403a')

pkgver() {
  cd $_pkgname
  printf $(TZ=UTC git log --no-walk --pretty="%cd" --decorate=full --date=format-local:%Y.%m.%d | head -n 1)
}

prepare(){
  cd $_pkgname
  patch -p1 < $srcdir/$pkgname.patch
}

build() {
  cd $_pkgname
  python setup.py build
}

#check() {
#  cd $_pkgname
#  pytest
#}

package() {
  cd $_pkgname
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 config.toml.sample scripts/dbsetup.sql -t "$pkgdir"/usr/share/doc/lilac
  install -Dm755 scripts/cleanup-dblck "$pkgdir"/usr/bin/cleanup-dblck
  install -Dm644 scripts/cleanup-dblck.service "$pkgdir"/usr/lib/systemd/system/cleanup-dblck.service
}


