# Maintainer:  Mick Elliot <m.g.elliot@rug.nl>
# Contributor: Mick Elliot <m.g.elliot@rug.nl>

pkgname=lastz
pkgver=1.04.15
pkgrel=3
pkgdesc="Pairwise DNA sequence aligner"
arch=('x86_64')
url="https://lastz.github.io/lastz/"
license=('MIT')
depends=(glibc)
makedepends=()
provides=(lastz)
source=("https://github.com/${pkgname}/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('46a5cfb1fd41911a36fce5d3a2721ebfec9146952943b302e78b0dfffddd77f8')

build() {
  cd "${pkgname}-${pkgver}"
  make -f Makefile.warnings
  make -f Makefile.warnings lastz_32
}

check() {
  cd "${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${pkgname}-${pkgver}"
  export LASTZ_INSTALL="${pkgdir}/usr/bin" && make install
  export LASTZ_INSTALL="${pkgdir}/usr/bin" && make install_32
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}


