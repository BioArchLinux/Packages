# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=hrbrthemes
_pkgver=0.8.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Additional Themes, Theme Components and Utilities for 'ggplot2'"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-extrafont
  r-gdtools
  r-ggplot2
  r-htmltools
  r-knitr
  r-magrittr
  r-rmarkdown
  r-scales
)
checkdepends=(
  r-hunspell
  r-testthat
  r-vdiffr
)
optdepends=(
  r-clipr
  r-dplyr
  r-gcookbook
  r-gridextra
  r-hunspell
  r-stringi
  r-svglite
  r-testthat
  r-vdiffr
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('164d952f9627188cff499edd1cce9c5c')
b2sums=('d07c2258a5672b4a9d56e18ae177de919c8dae8a35ecc16d4f9ff629f1c89d185bde8be77e0e6ddf82754bca88b2f8df35d114883b52808dc3f06298b2402668')

prepare() {
  # skip test since importing fonts with r-extrafont doesn't work
  sed -i '/^  invisible(import_roboto_condensed/i\ \ skip("extrafont import does not work")' \
      "$_pkgname/tests/testthat/test-hrbrthemes.R"
}

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla test-all.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
