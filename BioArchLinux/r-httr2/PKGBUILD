# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Alexander Bocken <alexander@bocken.org>

_pkgname=httr2
_pkgver=1.0.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Perform HTTP Requests and Process the Responses"
arch=(any)
url="https://cran.r-project.org/package=${_pkgname}"
license=(MIT)
depends=(
  r-cli
  r-curl
  r-glue
  r-lifecycle
  r-magrittr
  r-openssl
  r-r6
  r-rappdirs
  r-rlang
  r-vctrs
  r-withr
)
checkdepends=(
  r-bench
  r-docopt
  r-httpuv
  r-jose
  r-testthat
  r-webfakes
  r-xml2
)
optdepends=(
  r-askpass
  r-bench
  r-clipr
  r-covr
  r-docopt
  r-httpuv
  r-jose
  r-jsonlite
  r-knitr
  r-rmarkdown
  r-testthat
  r-tibble
  r-webfakes
  r-xml2
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('f9dede77d366a4e7b1bbec53a4e101de')
sha256sums=('cb852d6f560c329a7dc17aa760f09a0950413513dc8abfc3facb6418b2934a49')

build() {
  mkdir -p build
  R CMD INSTALL "$_pkgname" -l build
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
