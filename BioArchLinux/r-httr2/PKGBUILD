# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Alexander Bocken <alexander@bocken.org>

_pkgname=httr2
_pkgver=1.0.4
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=0
pkgdesc="Perform HTTP Requests and Process the Responses"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-cli
  r-curl
  r-glue
  r-lifecycle
  r-magrittr
  r-openssl
  r-r6
  r-rappdirs
  r-rlang
  r-vctrs
  r-withr
)
checkdepends=(
  r-bench
  r-docopt
  r-httpuv
  r-jose
  r-testthat
  r-webfakes
  r-xml2
)
optdepends=(
  r-askpass
  r-bench
  r-clipr
  r-covr
  r-docopt
  r-httpuv
  r-jose
  r-jsonlite
  r-knitr
  r-later
  r-promises
  r-rmarkdown
  r-testthat
  r-tibble
  r-webfakes
  r-xml2
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz"
        "fix-tests.patch")
md5sums=('ac74c17356e094978c594dfb1841486a'
         '7cd23279cf53ca0a0197ee2fad503067')
b2sums=('d758a8aba8ecc23287b67d137b3e33c1e88dd847015e51b03e02f7e2bce0d98ccc74045b3bade43ebe1080e75ef8432968de277f0b491fc9554537a7f6b301fe'
        'b639d73e84ce55c6c056edb97c5f684a7bb5c7269d53d55f49b8507f74af7148019a56db413c17daac41b37f86cce51f4b3ba4be358247ad716bf4b22ef68b48')

prepare() {
  # skip failing tests
  patch -Np1 -i fix-tests.patch
}

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
