# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Alexander Bocken <alexander@bocken.org>

_pkgname=httr2
_pkgver=1.0.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Perform HTTP Requests and Process the Responses"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-cli
  r-curl
  r-glue
  r-lifecycle
  r-magrittr
  r-openssl
  r-r6
  r-rappdirs
  r-rlang
  r-vctrs
  r-withr
)
checkdepends=(
  r-bench
  r-docopt
  r-httpuv
  r-jose
  r-testthat
  r-webfakes
  r-xml2
)
optdepends=(
  r-askpass
  r-bench
  r-clipr
  r-covr
  r-docopt
  r-httpuv
  r-jose
  r-jsonlite
  r-knitr
  r-rmarkdown
  r-testthat
  r-tibble
  r-webfakes
  r-xml2
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('3f2a80e58b643169eb65bed398105f18')
b2sums=('06bcc42de13e71709c18abf2e94fc4e91011f4847606d4246fec7b9039eba90fee4d22e6e48ddfdbfa73005a74142f4ffc69beea810f0114c2a5f39ae5c3da36')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
