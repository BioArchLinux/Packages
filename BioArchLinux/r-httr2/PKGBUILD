# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Alexander Bocken <alexander@bocken.org>

_pkgname=httr2
_pkgver=1.1.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Perform HTTP Requests and Process the Responses"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-cli
  r-curl
  r-glue
  r-lifecycle
  r-magrittr
  r-openssl
  r-r6
  r-rappdirs
  r-rlang
  r-vctrs
  r-withr
  r-nanonext
)
checkdepends=(
  r-bench
  r-docopt
  r-httpuv
  r-jose
  r-testthat
  r-webfakes
  r-xml2
)
optdepends=(
  r-askpass
  r-bench
  r-clipr
  r-covr
  r-docopt
  r-httpuv
  r-jose
  r-jsonlite
  r-knitr
  r-later
  r-paws.common
  r-promises
  r-rmarkdown
  r-testthat
  r-tibble
  r-webfakes
  r-xml2
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('02abcabfa12d721475bf31822384b254')
b2sums=('822d01bb46d0f90680a2f1a6c7050349063802bd6c9602b08e8e204c8c51c0564723c4f81293a1263bf52e3a7b77ac18c083764917eb6e52f09d4bdfd42a9458')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
