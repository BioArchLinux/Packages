# system requirements: FFmpeg (>= 3.2); with at least libx264 and lame(mp3) drivers. Debian/Ubuntu: libavfilter-dev, Fedora/CentOS:ffmpeg-devel (via https://rpmfusion.org), MacOS Homebrew:ffmpeg.
# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=av
_pkgver=0.8.2
pkgname=r-${_pkgname,,}
pkgver=0.8.2
pkgrel=1
pkgdesc='Working with Audio and Video in R'
arch=('x86_64')
url="https://cran.r-project.org/package=${_pkgname}"
license=('MIT')
depends=(
  r
  ffmpeg4.4
)
optdepends=(
  r-gapminder
  r-ggplot2
  r-ps
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('32358ab1e187ce4e212c26245523981b4212fefdab82187a4c39e8027c4c0922')

build() {
  R CMD INSTALL  --configure-vars="LIB_DIR='/usr/lib/ffmpeg4.4' INCLUDE_DIR='/usr/include/ffmpeg4.4'" \
    ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
  install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
