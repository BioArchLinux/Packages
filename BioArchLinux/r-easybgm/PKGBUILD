# Maintainer: shun2wang <shuonwang@gmail.com>

_pkgname=easybgm
_pkgver=0.3.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Extracting and Visualizing Bayesian Graphical Models"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-2.0-or-later')
depends=(
  r-coda
  r-bdgraph
  r-bggm
  r-bgms
  r-dplyr
  r-ggplot2
  r-hdinterval
  r-igraph
  r-qgraph
)
optdepends=(
  r-testthat
  r-vdiffr
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('e0757e846664654d37981113c7df43c3')
b2sums=('241ee752976972c774cdccb3ee116c81f63f484bb0ad8c9d08f88a2f1c288e7f77b480ecb93942cea1cd2effe553b17254be926483426253237f5e57d00e06e9')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
