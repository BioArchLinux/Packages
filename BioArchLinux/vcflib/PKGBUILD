# Maintainer: Guoyi
pkgname=vcflib
pkgver=1.0.3
pkgrel=2
pkgdesc="C++ library and cmdline tools for parsing and manipulating VCF files doi: 10.1101/2021.05.21.445151"
arch=('x86_64')
url="https://github.com/vcflib/vcflib"
license=('MIT')
depends=('python' 'htslib')
makedepends=('git' 'cmake' 'make' 'pybind11'
'pandoc'
)
optdepends=('r: running R scripts'
'perl: running Perl scripts'
)
source=("git+https://github.com/vcflib/vcflib.git"
"git+https://github.com/smarco/WFA2-lib.git"
"fastahack.tar.gz::https://github.com/ekg/fastahack/archive/refs/tags/v1.0.0.tar.gz"
"git+https://github.com/ekg/filevercmp.git"
"git+https://github.com/ekg/fsom.git"
# "intervaltree.tar.gz::https://github.com/ekg/intervaltree/archive/refs/tags/v0.1.tar.gz"
"git+https://github.com/ekg/intervaltree.git"
"git+https://github.com/edawson/libVCFH.git"
"git+https://github.com/ekg/multichoose.git"
"git+https://github.com/ekg/smithwaterman.git"
"simde::git+https://github.com/simd-everywhere/simde-no-tests.git"
"https://github.com/vcflib/tabixpp/archive/refs/tags/v1.1.1.tar.gz"
)

md5sums=('SKIP'
         'SKIP'
         'eeb7d046978d7633fe4d07def29f48c3'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         '6441f3e3bbeb6c642edc7dea1c4cf8c3')

prepare(){
  cd ${srcdir}
  cp -rf WFA2-lib/*  ${pkgname}/contrib/WFA2-lib
  cp -rf tabixpp-1.1.1/*  ${pkgname}/contrib/tabixpp
  cp -rf  fastahack-1.0.0 fastahack
  for module in {fastahack,filevercmp,fsom,intervaltree,libVCFH,multichoose,smithwaterman}; do
    cp -rf ${module}/* ${pkgname}/${module}
  done
}
build() {
  cd $srcdir/${pkgname}
  install -d build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make PREFIX=/usr HTS_HEADERS=/usr/include/htslib  HTS_LIB=/usr/lib/libhts.so

}

package() {
  cd "$pkgname"/build
  make install DESTDIR="$pkgdir"
  mv ${pkgdir}/usr/lib/pyvcf*  ${pkgdir}/usr/lib/pyvcflib.so
  mkdir ${pkgdir}/usr/share
  mv ${pkgdir}/usr/man ${pkgdir}/usr/share
}
