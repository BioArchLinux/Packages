# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=hwriter
_pkgver=1.3.2.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=10
pkgdesc="HTML Writer - Outputs R Objects in HTML Format"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('LGPL-2.1-only')
depends=(
  r
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('2d97b0dcf19b3c9d6d60840b4703453f')
b2sums=('f0ee25ffe858169b38287bebea13d7586e1563fa5303f57cc5d335bf33ce473f03873faa9414fd67c12d3d7dea09c97485248b4b4723d7515a7fdb7a442fb2d9')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
