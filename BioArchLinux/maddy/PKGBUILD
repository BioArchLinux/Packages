# Maintainer: Max Mazurov <fox.cpp at disroot dot org>
# Contributor: Jonas Heinrich <onny@project-insantiy.org>
pkgname='maddy'
pkgver=0.6.3
pkgrel=2
pkgdesc='Composable all-in-one mail server (Development version)'
arch=('x86_64')
url='https://github.com/foxcpp/maddy'
license=('MIT')
depends=('glibc')
makedepends=('go' 'git' 'scdoc')
optdepends=(
    "bash: For rspamd-hook script"
)
source=(
    "${pkgname}::git+https://github.com/foxcpp/maddy.git#tag=v${pkgver}"
    "$pkgname.patch::https://github.com/foxcpp/maddy/pull/516.patch"
    'maddy.sysusers'
    'maddy.tmpfiles'
)
backup=('etc/maddy/maddy.conf')
sha512sums=('SKIP'
            '70ccca2bd74e73a19893d6311deffa9c3538352ff9da6f9286686eea74fa0648e17c169a1a79ec51111e58849cc29faaf1f89f260edd325e84a1a2b817a72cbf'
            '750346110adb8caa61f537560018497f73543dc01ff26aceed2f052f281a35fdc659c9c478cc55775eadf8a3d17b511d5bed86334d1c455732dcb9a273120589'
            'f33135b81129d6ef3006d8e9f410ec0d7e44226ae640dea77d756268d0e97828d8965ac75d0d9b49604a19b8b9e0384d15007d33c4b813f359108d28a10702b5')

prepare() {
    cd $srcdir/$pkgname
    patch -p1 < $srcdir/$pkgname.patch
}

build() {
    cd "${srcdir}/${pkgname}"
    export GOPATH="$PWD/gopath"

    ./build.sh --version "v${pkgver}" --prefix /usr package
}

package() {
    cd "${srcdir}/${pkgname}"
    export GOPATH="$PWD/gopath"

    ./build.sh \
        --version "v${pkgver}" \
        --prefix /usr \
        --destdir "${pkgdir}" \
        install_pkg

    install -Dm 0644 "${srcdir}/maddy.sysusers" "${pkgdir}/usr/lib/sysusers.d/maddy.conf"
    install -Dm 0644 "${srcdir}/maddy.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/maddy.conf"
    # fix path
    sed -i "s@/usr/local/bin@/usr/bin@g" $pkgdir/usr/lib/systemd/system/*.service
}

