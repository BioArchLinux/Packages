# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=margins
_pkgver=0.3.26
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=3
pkgdesc="Marginal Effects for Model Objects"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-data.table
  r-prediction
)
checkdepends=(
  r-aer
  r-betareg
  r-lme4
  r-ordinal
  r-survey
  r-testthat
)
optdepends=(
  r-gapminder
  r-ggplot2
  r-knitr
  r-lme4
  r-rmarkdown
  r-sandwich
  r-stargazer
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('089978cd9b7fd9b0ba75525c9efe4641')
b2sums=('302271f7597f8c98852571935a1ad921026b0cf65ca31e798b0e613d9046837af80bf60256a8763d59a06fc464609cda00435b11e4a0c4adf5d975bd5c729bad')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat-margins.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
