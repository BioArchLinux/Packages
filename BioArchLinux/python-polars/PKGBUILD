# Maintainer: Alexandre Bury <alexandre.bury@gmail.com>

pkgname=python-polars
pkgver=1.30.0
pkgrel=1
pkgdesc="Blazingly fast DataFrames library using Apache Arrow Columnar Format as memory model"
arch=("x86_64")
url="https://www.pola.rs/"
license=('MIT')
depends=('python' 'glibc' 'python-numpy' 'gcc-libs')
options=('!lto')
optdepends=('python-pandas: for interoperability with pandas frames'
            'python-pyarrow: for interoperability with arrow types'
            'python-pytz: to enable conversion to python datetimes with timezones'
            'python-fsspec: to transparently open files locally or remotely'
            'ipython'
            'python-typing_extensions'
            'python-packaging'
            'python-matplotlib'
            )
makedepends=('maturin' 'rustup' 'cmake' 'python-installer')
_name=${pkgname#python-}
_tag="py-$pkgver"
source=("https://github.com/pola-rs/polars/archive/refs/tags/$_tag.tar.gz")
b2sums=('0a15b38b28c8953d7f5d3e54ef0713b191a0da529c29495fc1f3c57ade2a5b040fda81ee4978769f2f4f3bce453c2f08193ad8d40c68b24ccff67661ae648ac1')

prepare() {
    cd polars-$_tag/py-polars
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    rm wheels -rf
    cd polars-$_tag/py-polars
    maturin build -o ../../wheels --release
}

package() {
    python -m installer -d "$pkgdir" ./wheels/*.whl
    install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" polars-$_tag/LICENSE
}
