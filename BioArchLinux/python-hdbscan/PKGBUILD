# import from AUR: Bipin Kumar <kbipinkumar@pm.me>
# Maintainer: peippo <christoph+aur@christophfink.com>

pkgname="python-hdbscan"
pkgdesc="Hierarchical Density-Based Spatial Clustering of Applications with Noise"
url="http://github.com/scikit-learn-contrib/hdbscan"

pkgver=0.8.41
pkgrel=1

arch=("x86_64")
license=("BSD-3-Clause")

makedepends=(
    "python-setuptools"
    "cython"
    "python-wheel"
    "python-build"
    "python-installer"
    "git"
)

depends=(
    "python"
    "python-numpy"
    "python-scipy"
    "python-scikit-learn"
    "python-joblib"
    "python-six"
)

_name=${pkgname#python-}
_tag=release-${pkgver}

source=("${_name}::git+https://github.com/scikit-learn-contrib/hdbscan.git#tag=${_tag}")
sha256sums=('8503d44054d49b4c3f5c33b9d7504454bf7aae9e3c0b4949d7d0903393a5b090')

prepare() {
    git -C "${srcdir}/${_name}" clean -dfx
}

build() {
    cd "${srcdir}"/${_name}
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}"/${_name}
    python -m installer --destdir="$pkgdir" dist/*.whl
    rm "$pkgdir"/usr/dist_metrics.pxd

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
