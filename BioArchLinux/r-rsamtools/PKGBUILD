# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=Rsamtools
_pkgver=2.20.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=0
pkgdesc="Binary alignment (BAM), FASTA, variant call (BCF), and tabix file import"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0 AND MIT')
depends=(
  bzip2
  curl
  r-biocgenerics
  r-biocparallel
  r-biostrings
  r-bitops
  r-genomeinfodb
  r-genomicranges
  r-iranges
  r-s4vectors
  r-xvector
  r-zlibbioc
  xz
  zlib
)
makedepends=(
  r-rhtslib
)
optdepends=(
  r-biocstyle
  r-bsgenome.hsapiens.ucsc.hg19
  r-genomicalignments
  r-genomicfeatures
  r-knitr
  r-rnaseqdata.hnrnpc.bam.chr14
  r-runit
  r-shortread
  r-txdb.dmelanogaster.ucsc.dm3.ensgene
  r-txdb.hsapiens.ucsc.hg18.knowngene
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('a36dd03dffa3e6a830adafaeef5429ab')
b2sums=('4176ccfc113368565de874a223fc9b8fb1fb91dbe67963a03743137f3715a5e70e8f9aebae207b7af54a4c772b662df2f9dac5d715838afbe672741103417fd7')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
