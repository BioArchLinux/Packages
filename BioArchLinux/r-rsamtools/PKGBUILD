# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=Rsamtools
_pkgver=2.24.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Binary alignment (BAM), FASTA, variant call (BCF), and tabix file import"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0 AND MIT')
depends=(
  bzip2
  curl
  r-biocgenerics
  r-biocparallel
  r-biostrings
  r-bitops
  r-genomeinfodb
  r-genomicranges
  r-iranges
  r-s4vectors
  r-xvector
  xz
  zlib
)
makedepends=(
  r-rhtslib
)
optdepends=(
  r-biocstyle
  r-bsgenome.hsapiens.ucsc.hg19
  r-genomicalignments
  r-genomicfeatures
  r-knitr
  r-rnaseqdata.hnrnpc.bam.chr14
  r-runit
  r-shortread
  r-txdb.dmelanogaster.ucsc.dm3.ensgene
  r-txdb.hsapiens.ucsc.hg18.knowngene
  r-variantannotation
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('0ed68b3325ece17f53f9362b50a5e172')
b2sums=('a6d10b340a19b28258725771f992dfe7162ef89dfddaf58887f8fe5bf3bd43d76d69dccd74c6b97509820e789f1574d4380a21babfc41f065aa747dee665ed24')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
