# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=Rsamtools
_pkgver=2.22.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Binary alignment (BAM), FASTA, variant call (BCF), and tabix file import"
arch=(x86_64)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0 AND MIT')
depends=(
  bzip2
  curl
  r-biocgenerics
  r-biocparallel
  r-biostrings
  r-bitops
  r-genomeinfodb
  r-genomicranges
  r-iranges
  r-s4vectors
  r-xvector
  r-zlibbioc
  xz
  zlib
)
makedepends=(
  r-rhtslib
)
optdepends=(
  r-biocstyle
  r-bsgenome.hsapiens.ucsc.hg19
  r-genomicalignments
  r-genomicfeatures
  r-knitr
  r-rnaseqdata.hnrnpc.bam.chr14
  r-runit
  r-shortread
  r-txdb.dmelanogaster.ucsc.dm3.ensgene
  r-txdb.hsapiens.ucsc.hg18.knowngene
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('a2863f9fd8439f30148428b1f2f20714')
b2sums=('bf6335832cf485c44b2f43432b640132b00dfe7951ab18d4b1d85ee8a6109190bec9a9a893b5c10824e1ac2c4d21d3d24be1fa48be1cbc5be4009f4d9f591844')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
