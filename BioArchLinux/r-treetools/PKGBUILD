# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=TreeTools
_pkgver=1.11.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Create, Modify and Analyse Phylogenetic Trees"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-3.0-or-later')
depends=(
  r-ape
  r-bit64
  r-colorspace
  r-fastmatch
  r-lifecycle
  r-plottools
  r-r.cache
  r-rcurl
  r-rdpack
)
makedepends=(
  r-rcpp
)
checkdepends=(
  r-phangorn
  r-purrr
  r-testthat
  r-vdiffr
)
optdepends=(
  r-knitr
  r-phangorn
  r-purrr
  r-rcpp
  r-rlang
  r-rmarkdown
  r-spelling
  r-testthat
  r-vdiffr
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('c799edcc3959f60d4e03e199662737cb')
b2sums=('0dea0f406745b8b074fcfc529ba3fec60a44bb4842fd60f3fbb45abc5e8f3d8a5e33a0970e6e7058f3a53c4958e7ff5bf8d54108a8c7913b62b5a0492310471d')

prepare() {
  # skip failing tests
  sed -e '/"Simple rogue plot"/a\ \ skip("fails")' \
      -e '/"polytomy id"/a\ \ skip("fails")' \
      -e '/"Complex rogue plot"/a\ \ skip("fails")' \
      -i "$_pkgname/tests/testthat/test-RoguePlot.R"
}

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
