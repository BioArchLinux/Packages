# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
pkgname=wayfire
pkgver=0.9.0.20240309.140616
pkgrel=1
pkgdesc="3D wayland compositor"
arch=(x86_64)
url=https://wayfire.org
license=(custom:MIT)
depends=('cairo' 'pango' 'libdrm' 'libevdev'
         'libglvnd' 'libinput' 'libjpeg'
         'libpng' 'libxkbcommon' 'pixman' 'polkit'
         'seatd' 'xcb-util-errors' 'xcb-util-renderutil'
         'xcb-util-wm' 'xorg-xwayland' 'wayland'
         'libdisplay-info' 'libliftoff' 'wf-config')
makedepends=('git' 'glslang' 'meson' 'ninja' 'cmake' 'vulkan-headers' 'doctest' 'pkgconf' 'wayland-protocols'
             'nlohmann-json' 'libxml2' 'glm'
           )
conflicts=("${pkgname}-git")
source=("git+https://github.com/WayfireWM/${pkgname}.git")
sha256sums=('SKIP')

pkgver () {
    cd "$srcdir/wayfire"
    printf "$(cat meson.build | grep '^\sversion:' | sed "s/'/ /g" | awk '{print $2}').%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)"
}

prepare(){
	sed -i 's/{0.5, 0.5, 1, 0.5}/{0.35, 0.75, 1.00, 0.50}/g' $srcdir/$pkgname/plugins/common/wayfire/plugins/common/preview-indication.hpp #change base color
	sed -i 's/{0.25, 0.25, 0.5, 0.8}/{0.40, 0.80, 1.00, 0.80}/g' $srcdir/$pkgname/plugins/common/wayfire/plugins/common/preview-indication.hpp #change bordor color
}

build() {
	cd "$srcdir/wayfire"
    	arch-meson \
        	--buildtype=release \
        	-Dxwayland=auto \
        	-Duse_system_wlroots=disabled \
        	-Duse_system_wfconfig=enabled \
        	-Db_lto=true \
        	-Db_pie=true \
        build
	ninja -C build
	sed "/WF_SRC_DIR/d" -i build/config.h
}

package() {
        cd "$srcdir/wayfire"
	DESTDIR="${pkgdir}/" ninja -C build install
	install -Dm644 wayfire.desktop "${pkgdir}/usr/share/wayland-sessions/wayfire.desktop"
	install -Dm644 wayfire.ini "${pkgdir}/usr/share/doc/${pkgname}/wayfire.ini"
	install -Dm645 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
