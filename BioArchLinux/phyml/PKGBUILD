# Maintainer: Michael Schubert <mschu.dev at gmail> github.com/mschubert/PKGBUILDs
# Contributor: Mick Elliot <micke at sfu dot ca>
pkgname=phyml
pkgver=3.3.20250506
pkgrel=1
pkgdesc="Builds phylogenies from DNA or protein sequences using a maximum likelihood approach. https://doi.org/10.1093/sysbio/syq010"
arch=('i686' 'x86_64')
url="https://github.com/stephaneguindon/phyml"
license=('GPL2')
conflicts=('phyml-mpi')
depends=('gcc-libs')
makedepends=('make' 'autoconf')
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha256sums=('cf71c951e91f74e3152f70a71a5e24409051f76a587a3d4e6e6ae5c18e5ad926')

build() {
  cd "$srcdir/phyml-$pkgver"
  autoreconf --force --install -v
  ./configure --prefix=/usr --enable-phyml
  make
}

package() {
  cd "$srcdir/phyml-$pkgver"
  make DESTDIR="$pkgdir" install
}
