# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=MetaVolcanoR
_pkgver=1.14.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=3
pkgdesc="Gene Expression Meta-analysis Visualization Tool"
arch=(any)
url="https://bioconductor.org/packages/$_pkgname"
license=('GPL-3.0-only')
depends=(
  r-cowplot
  r-data.table
  r-dplyr
  r-ggplot2
  r-htmlwidgets
  r-metafor
  r-metap
  r-plotly
  r-rlang
  r-tidyr
  r-topconfects
)
checkdepends=(
  r-testthat
)
optdepends=(
  r-knitr
  r-markdown
  r-rmarkdown
  r-testthat
)
source=("https://bioconductor.org/packages/3.17/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('697e849a45f3ceae9ff730fc7717d6ca')
b2sums=('5c1f4e5fede095ada993566a6d9ab7466fc9c2c0f9aa05eabad14964503d97218f9baedf58e4b08176ef7f808225ca828bd7c089084df05142b4c6a0b989e840')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
