# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: peippo <christoph+aur@christophfink.com>

_pkgname=RMariaDB
_pkgver=1.3.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Database Interface and MariaDB Driver"
arch=(x86_64)
url="https://cran.r-project.org/package=${_pkgname}"
license=(MIT)
depends=(
  mariadb-libs
  r-bit64
  r-blob
  r-cpp11
  r-dbi
  r-hms
  r-lubridate
  r-plogr
  r-rlang
)
makedepends=(
  boost
  r-cpp11
  r-plogr
)
checkdepends=(
  mariadb
  r-dbitest
  r-testthat
)
optdepends=(
  r-dbitest
  r-decor
  r-readr
  r-rprojroot
  r-testthat
  r-withr
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz"
        "$_pkgname-fix-connection.patch::https://github.com/r-dbi/RMariaDB/pull/306.patch"
        "system-boost.patch")
md5sums=('9219c32690d6e53882ba6a616976c6ad'
         '87535a335087362a4180932f6bd2c018'
         'b53149b42ab18105619bbbcb568de1b7')
sha256sums=('30e6fb03aa60b9e93c6ef203f2b3d582314e796de54a64872ad5259c04ed1720'
            'a6ad2947eea291e6ac18fc2a8433bdb05841a5d75a68874e15cf841913e45e81'
            'eacc28eff677214d42c9b2199e9bd1d5d1a35a47d622047b01353e0d4b282e49')

prepare() {
  # use system boost headers
  patch -Np1 -i system-boost.patch
  # fix handling of connection option groups
  patch -Np1 -d "$_pkgname" < "$_pkgname-fix-connection.patch"
}

build() {
  mkdir -p build
  R CMD INSTALL "$_pkgname" -l build
}

check() {
  cd "$_pkgname/tests"

  # create database for tests
  export HOME="$srcdir" # make sure mariadb doesn't read ~/.my.cnf
  export MARIADB_HOME="$srcdir/mariadb"
  mkdir -p "$MARIADB_HOME/data"
  cat > "$MARIADB_HOME/my.cnf" << EOF
[client-server]
socket="$MARIADB_HOME/mariadb.sock"

[server]
skip_networking=1
datadir="$MARIADB_HOME/data"
EOF
  mariadb-install-db
  mariadbd &
  sleep 1 # wait for the server to start up
  # populate time zone data needed in tests
  mariadb-tzinfo-to-sql /usr/share/zoneinfo | mariadb mysql
  mariadb -e "SET GLOBAL time_zone = 'UTC';" mysql
  mariadb -e "ALTER DATABASE test CHARACTER SET = 'utf8mb4';" test

  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R

  # shut down test database
  mariadb-admin shutdown
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
