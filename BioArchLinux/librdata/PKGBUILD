# Maintainer: sukanka <su975853527 [AT] gmail.com>

pkgname=librdata
_pkgname=librdata
pkgver=0.1.0.r125.33bd276
pkgrel=35
pkgdesc="Read and write R data frames from C"
arch=("x86_64" "aarch64")
url="https://github.com/WizardMac/librdata"
license=("MIT")
depends=('bzip2' 'zlib' 'glibc' 'xz')
provides=($_pkgname)
makedepends=(make autoconf git gettext)
source=("git+${url}.git")
sha512sums=('SKIP')


pkgver() {
    cd ${srcdir}/${_pkgname}
    printf "0.1.0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}
build() {
    cd ${srcdir}/${_pkgname}
    autoreconf -fvi -I /usr/share/gettext/m4
    ./configure --prefix=/usr
    make

}

package() {
    cd ${srcdir}/${_pkgname}
    make install DESTDIR="${pkgdir}"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
