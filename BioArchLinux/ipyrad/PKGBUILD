pkgname=ipyrad
pkgver=0.9.102
pkgrel=1
pkgdesc="Interactive assembly and analysis of RAD-seq data sets."
arch=('any')
url="https://github.com/dereneaton/ipyrad"
license=('GPL-3.0-or-later')
depends=('python' 'python-pandas' 'python-scipy' 'python-numpy' 'python-numba' 'python-h5py' 
	'python-ipyparallel' 'python-requests' 'python-mpi4py' 'python-scikit-learn'
	'python-seaborn' 'python-matplotlib' 'python-setuptools'
	'cutadapt' 'python-pysam'  'bedtools' 'vsearch' 'bwa' 'samtools' 'python-toyplot' 'python-toytree' 'muscle3')
optdepends=('jupyter-notebook: for interactive')
makedepends=('python-build' 'python-setuptools' 'python-installer' 'python-wheel')
source=("https://github.com/dereneaton/ipyrad/archive/${pkgver}.tar.gz")
sha256sums=('2a8dc210d69cdedd34ac92715f23a9c3b7b4593a97b0d16851a566eacf33ef42')

prepare() {
    # fix future
    cd "${srcdir}/${pkgname}-${pkgver}" 
    find . -name "*.py" -exec sed -i '/from __future__/d' {} +
    sed -i '/"future",/d' setup.py
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}

