# Maintainer:  Mick Elliot <m.g.elliot@rug.nl>
# Contributor: Mick Elliot <m.g.elliot@rug.nl>

pkgname=lastz_backtobackgaps
_pkgname=lastz
pkgver=1.04.22
pkgrel=1
pkgdesc="Pairwise DNA sequence aligner. This version allows back to back gaps (adjacent indels) but might break backward compatibility with lastZ and BLASTZ."
arch=('x86_64')
url="https://lastz.github.io/lastz/"
license=('MIT')
depends=(glibc)
makedepends=()
provides=(lastz)
source=("https://github.com/${_pkgname}/${_pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('4c829603ba4aed7ddf64255b528cd88850e4557382fca29580d3576c25c5054a')

build() {
  cd "${_pkgname}-${pkgver}"
  make -f Makefile.warnings allowBackToBackGaps=ON
  make -f Makefile.warnings lastz_32 allowBackToBackGaps=ON
}

check() {
  cd "${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "${_pkgname}-${pkgver}"
  export LASTZ_INSTALL="${pkgdir}/usr/bin" && make install
  export LASTZ_INSTALL="${pkgdir}/usr/bin" && make install_32
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}


