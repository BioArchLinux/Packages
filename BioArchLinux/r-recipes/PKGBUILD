# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Guoyi Zhang <guoyizhang at malacology dot net>
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Alex Branham <branham@utexas.edu>

_pkgname=recipes
_pkgver=1.1.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Preprocessing and Feature Engineering Steps for Modeling"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('MIT')
depends=(
  r-cli
  r-clock
  r-dplyr
  r-generics
  r-glue
  r-gower
  r-hardhat
  r-ipred
  r-lifecycle
  r-lubridate
  r-magrittr
  r-purrr
  r-rlang
  r-tibble
  r-tidyr
  r-tidyselect
  r-timedate
  r-vctrs
  r-withr
)
checkdepends=(
  r-ddalpha
  r-dials
  r-dimred
  r-fastica
  r-igraph
  r-kernlab
  r-mixomics
  r-modeldata
  r-rann
  r-rcppml
  r-rcpproll
  r-rsample
  r-rspectra
  r-splines2
  r-testthat
  r-workflows
)
optdepends=(
  r-covr
  r-ddalpha
  r-dials
  r-ggplot2
  r-igraph
  r-kernlab
  r-knitr
  r-modeldata
  r-parsnip
  r-rann
  r-rcpproll
  r-rmarkdown
  r-rsample
  r-rspectra
  r-splines2
  r-testthat
  r-workflows
  r-xml2
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('ee911057a93ba41dd3054d9ff2cb671c')
b2sums=('8bb1d62416a810a0d04f77f73483abc1d0e04a257cfbf2886f431e8f170d2b27d9f0a4a59336bc5c8c5928853274a405189522ec8344dd9eb8481f3051d1c49e')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
