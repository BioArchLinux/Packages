# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=ncmeta
_pkgver=0.3.6
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Straightforward 'NetCDF' Metadata"
arch=(any)
url="https://cran.r-project.org/package=$_pkgname"
license=('GPL-3.0-only')
depends=(
  r-dplyr
  r-rlang
  r-rnetcdf
  r-tibble
  r-tidyr
)
checkdepends=(
  r-testthat
)
optdepends=(
  r-covr
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('e38fff220a5ed49a5304684562b56ac9')
b2sums=('b89de032ea6d7e44aa180c34c47f322cc3ed2b761c277ac4b6a345c7f802b278820ab16769c1be4e88911d55f7dd2893f2e2609a8295d2f8c1b1d39409e9bddc')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
