# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=SummarizedExperiment
_pkgver=1.38.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="A container (S4 class) for matrix-like assays"
arch=(any)
url="https://bioconductor.org/packages/$_pkgname"
license=('Artistic-2.0')
depends=(
  r-biobase
  r-biocgenerics
  r-delayedarray
  r-genomeinfodb
  r-genomicranges
  r-iranges
  r-matrixgenerics
  r-s4arrays
  r-s4vectors
)
optdepends=(
  r-airway
  r-annotate
  r-annotationdbi
  r-biocstyle
  r-digest
  r-genomicfeatures
  r-hdf5array
  r-hgu95av2.db
  r-jsonlite
  r-knitr
  r-rhdf5
  r-rmarkdown
  r-runit
  r-singlecellexperiment
  r-sparsearray
  r-testthat
  r-txdb.hsapiens.ucsc.hg19.knowngene
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('a16df12f5706edd9422052018997a571')
b2sums=('8772192b8ed360325e7893d47d2ad82cc4ad6d9c05655c23ab5badb7a4ad1191fbf55c1c318646ddb91d54f59a5dd0d4e2b49a867fa5d09429411a6c7787bf11')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
