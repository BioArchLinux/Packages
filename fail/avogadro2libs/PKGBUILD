# Maintainer: Eric Berquist <eric DOT berquist AT gmail DOT com>

_pkgname=avogadrolibs
pkgname=avogadro2libs
pkgver=1.95.1
pkgrel=2
pkgdesc="Avogadro 2: libraries"
url="http://openchemistry.org/projects/avogadro2"
arch=("x86_64")
license=("Kitware")
depends=("libarchive" "glew" "hdf5" "vtk=8" "libmsym" "spglib" "libmmtf" "qt5-webview" "qt5-x11extras" "molequeue" "pybind11" "python-cclib")
# gdal is for proj, which is optional for VTK but required here? same for openmpi
# `msgpack-c` is a workaround for the broken libmmtf PKGBUILD
makedepends=("make" "cmake" "eigen" "gtest" "gdal" "openmpi" "msgpack-c")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("https://github.com/OpenChemistry/avogadrolibs/archive/refs/tags/${pkgver}.zip")
sha256sums=('5b69fd1216e49fde04816e257fb60819c47bc711317827f2759a26295309ed5a')

build() {
  mkdir -p "${srcdir}"/build
  cd "${srcdir}"/build
  cmake \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DBUILD_SHARED_LIBS=ON \
      -DENABLE_TESTING=ON \
      -DUSE_HDF5=ON \
      -DUSE_VTK=ON \
      -DUSE_MMTF=ON \
      -DUSE_PYTHON=ON \
      -DPYTHON_EXECUTABLE=/usr/bin/python \
      "${srcdir}/${_pkgname}-${pkgver}"
  make
}

check() {
  cd "${srcdir}"/build
  make test
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
  install -D -m 644 "${srcdir}/${pkgname}"/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
